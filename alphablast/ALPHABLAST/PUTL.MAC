   		.Z80
		.RADIX 16
		CSEG
;
		.XLIST
		INCLUDE COMMON.MRO
		.LIST
		EXTRN NA2LOGO
		EXTRN NA2DEF
		EXTRN RPATRN,PUTPAT
;
;**************************************************************************
;
;
;	ROUTINE NAME: PUTLO, OR PUTLOGO
;
;------------------------------------------
;
;	FILE NAME: PUTL.MAC	
;
;	DESCRIPTION:
;		
; This routine will put a nabu designed logo onto a page of
; text or graphics on the screen.  The X and Y position of the
; left hand upper corner of the logo must be specified.
; The form of call is :  X location : C reg
;    			 Y location : E reg
;			 logo #     : L reg.
;
; The currently implemented logo numbers are:
;       0 - text mode nabu logo
;       1 - G1 mode nabu logo
;	2 - G2 MODE NABU LOGO
;
;	AUTHOR: Warren Belkin
;	DATE and ISSUE:		(June   ,1982 Version 3.0)
;	CATALOGUE ID:		(HIS - AS - 0027)
;
;	PARAMETERS PASSED: C,E, and L registers
;
;	PARAMETERS RETURNED:
;
;	REGISTERS CLOBBERED: all
;
;	GLOBALS ACCESSED:NALOGO,NA1LOGO,NA2LOGO
;
;	GLOBALS WRITTEN:
;
;	COMMENTS and WARNINGS:
;
;
;
XLOC:		DW 0
YLOC:		DW 0
DUM1:		DW 0
DUM2:		DW 0
DUM12:		DW 0
DUM22:		DW 0
TABLE:		DW DUM1, DUM2, NA2LOGO
TABLE1:		DW DUM12, DUM22, NA2DEF
PSET::		DW 00
PDEF::		DW 00
;
;
		.RADIX 16
;
PUTLO::
PUTLOGO::
		LD B, 0
	  	LD D, 0
		LD (XLOC), BC
		LD (YLOC), DE
		LD H, 0
		SLA L
		RL H
		PUSH HL
		LD BC, TABLE
		ADD HL, BC
		LD C, (HL) 
		INC HL
		LD B, (HL)
		LD (PSET), BC
		POP HL
		LD BC, TABLE1
		ADD HL, BC
		LD C, (HL)
		INC HL
		LD B, (HL)
		LD (PDEF), BC
		JR CLOGO
;
DOLOG::
DOLOG0::	LD B,0
		LD D,0
		LD H,0
         	LD (XLOC),BC	;SAVE X AND Y
		LD (YLOC),DE
CLOGO:		LD BC,(PSET)
		CALL RPATRN	;READ PATTERN DEF IN
		LD HL, (PDEF)
;
GETBLK:		LD C,(HL)	;GET X POS.
		INC HL
		LD E,(HL)	;GET Y POS.
		INC HL
		PUSH HL		;SAVE IT
		LD L,(HL)	;GET PATTERN NO,
		LD A,0FFH
 		CP C		;IS END?
		JR Z,DONE
		LD A,(XLOC)	;ADD RELATIVE
		ADD A,C
		LD C,A
		LD A,(YLOC)
		ADD A,E
		LD E,A
		CALL PUTPAT	;PUT PATTERN
		POP HL
		INC HL
		JR GETBLK	;KEEP DOING THIS
;
DONE:		POP HL		;GET RID OF STACK EXTRA
 		RET
;
		END
