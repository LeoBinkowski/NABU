;***********************************************
;
;	ALPHA/MAC
;
;	(c)1982 by John Allen
;
;	Miscellaneous FORTRAN subroutines
;	$INIT,$M9,$D9,BYEBYE
;
;***********************************************
;MACRO ROUTINES
;
	.Z80
;
;
	INCLUDE STRUCT.MRO
	INCLUDE COMMON.MRO
	INCLUDE NLINKTAB.EXA
;
	EXTRN	V.PNT		;My incore copy of
;	    ;the address of the pattern name table
;
;
;	GET A WORD FROM THE DICTIONARY
;
;	CALL GETWD (WDBUF,WDNO,WDLEN)
;		GET WORD NUMBER IN DICT
;		PUT INTO WDBUF
;		PUT LENGTH IN WDLEN
;
;	The dictionary consists of ascii words separated
;	by commas.
;
;
GETWD::
	LD	(WDBUF),HL	;POINTER TO BUF
	LD	(WDNO),DE	;POINTER TO NUMBER
	LD	(WDLEN),BC	;POINTER TO LENGTH
;
	EX	DE,HL		;(WDNO)=>HL
	LD	E,(HL)		;NUMBER TO DE
	INC	HL
	LD	D,(HL)
;
	LD	HL,WDTAB	;DICTIONARY (HL)
;
DONE:	DEC	DE		;IS THIS THE WORD
	LD	A,D
	OR	E
	JP	Z,TRANS
;
OVER:	LD	A,(HL)		;PASS OVER WORDS
	INC	HL
	CP	','		;END OF WORD?
	JP	NZ,OVER
	JP	DONE		;SEE IF GOT THERE
;
;	HL POINTS TO FIRST CHAR OF THE STRING
;	TRANSFER IT TO THE CALLING BUFFER
;
TRANS:	LD	BC,0		;SET UP WDLEN COUNTER
	LD	DE,(WDBUF)	;RESTORE POINTER
;
TRANS1: LD	A,(HL)
	INC	HL
	CP	','		;DONE YET?
	JP	Z,EXIT
	LD	(DE),A		;PUT IN BUF
	INC	DE		;INC TWICE FOR..
	INC	DE		;.. WORDS
	INC	BC		;UP LEN
	JP	TRANS1
;
;	WORD MOVED INTO CALLING BUFFER
;	NOW WE JUST HAVE TO SET THE LENGTH
;
EXIT:
	LD	HL,(WDLEN)
	LD	(HL),C
	INC	HL
	LD	(HL),B
;
	RET
;
WDBUF:	DW	0
WDNO:	DW	0
WDLEN:	DW	0
;
;	 LEV 1 8 PTS
WDTAB:	DEFM 'ORIENTAL,AGENDA,NATIONAL,ASTHMA,INTEREST,'
	DEFM 'ATHEIST,LETTUCE,TREADLE,FLUSTER,VILLAIN,'
	DEFM 'LEOTARD,THISTLE,EBONY,ABSURD,DOUBLE,'	
	DEFM 'SUBDUE,ENOUGH,STIRRUP,CARROM,FEDORA,'
	DEFM 'IVORY,COMMA,MUSEUM,COLONEL,FIASCO,'
	DEFM 'ABACUS,BABOON,RAZOR,FORMAT,ALMOND,'
	DEFM 'CINEMA,BEAGLE,SERPENT,ARSENIC,PETUNIA,'
	DEFM 'SERVANT,AMNESIA,CONDOR,REFEREE,NUMBER,'
	DEFM 'COUGAR,ANTHEM,CACTUS,MENACE,CARBON,'	
	DEFM 'JUDO,ARTICLE,MURMUR,MINERAL,AGONY,'  
;	 LEV 1	9 PTS
	DEFM 'COFFEE,ERUPTION,UNFASTEN,SAILBOAT,PEERLESS,'
	DEFM 'GLACIER,IGNITION,ESTIMATE,HARVEST,PURPOSE,'
	DEFM 'MELLOW,RATCHET,SNORKEL,FASHION,SUICIDE,'
	DEFM 'TRACTION,UNIQUE,MEMENTO,VARNISH,OCTAGON,'
	DEFM 'ABANDON,COCONUT,ASKEW,LAMPOON,BAMBOO,'
	DEFM 'MIDGET,NEGLECT,GREMLIN,OFFICE,ABSENCE,'
	DEFM 'DUNGEON,PRISONER,TRUMPET,IDIOTIC,ROMANCE,'
	DEFM 'PARSNIP,BANJO,ACROBAT,MUSTANG,AMBUSH,'
	DEFM 'GLEEFUL,FRAGILE,OBSOLETE,PREVAIL,COYOTE,'
	DEFM 'ALTITUDE,TEASPOON,FANATIC,ALMANAC,OVERALLS,'
;	 LEV 1 10 PTS
	DEFM 'TEXTILE,WAITRESS,DISCLOSE,ESQUIRE,HEADLINE,'
	DEFM 'BOREDOM,RESEMBLE,PERHAPS,ROOMMATE,PENALTY,'
	DEFM 'SUBURBAN,UMBRELLA,FROGMAN,BUFFOON,PUNCTURE,'
	DEFM 'NARCOTIC,SHOEHORN,BRAMBLE,IODIZE,CAULDRON,'
	DEFM 'MOLECULE,CONFETTI,DECIBEL,GIZMO,ADJUST,'
	DEFM 'BASEBALL,MACARONI,COZY,NAIVETY,REQUIRE,'
	DEFM 'ELEPHANT,ABUNDANT,SKEWER,DIAGNOSE,LAUGHTER,'
	DEFM 'TEETOTALER,AIRCRAFT,BARNACLE,EPITAPH,CASHEW,'
	DEFM 'UNIVERSAL,MUFFLER,CAVEMAN,LEMMING,ORCHARD,'
	DEFM 'BUFFALO,ALLERGY,CHESTNUT,ABDOMEN,MINIATURE,'
;	 LEV 1 11 PTS
	DEFM 'POSTCARD,BIRDSEED,OVERHEAD,FAREWELL,TAXATION,'
	DEFM 'PURCHASE,DARKNESS,MORTGAGE,SPOOKY,LAZINESS,'
	DEFM 'SAPPHIRE,LARYNX,GASLIGHT,GRAFFITI,HILARITY,'
	DEFM 'LOPSIDED,TRANQUIL,ABJECT,PAPRIKA,CALYPSO,'
	DEFM 'ABYSMAL,BARBECUE,GRAVITY,ENTERTAINER,NAVIGATOR,'
	DEFM 'RICOCHET,CHARCOAL,FOREIGNER,NOTEBOOK,MOONSHINE,'
	DEFM 'ZOMBIE,TICTACTOE,ALPHABET,SPECIMEN,DANDELION,'
	DEFM 'REASONABLE,DELICIOUS,ILLUMINATE,BINOCULAR,EMBARRASS,'
	DEFM 'COMPUTER,ACQUIRE,COLLECTOR,MARGARINE,NONENTITY,'
	DEFM 'BOXCAR,NAUSEATING,GENTLEMAN,WELCOME,MORTICIAN,'
;	 LEV1 12 PTS
	DEFM 'CHAMPION,MISMATCH,CEMETERY,PLAYMATE,CHIMNEY,'
	DEFM 'BREWERY,UNICYCLE,TRANSFIX,BUMPKIN,WHIMPER,'
	DEFM 'CAMPAIGN,JAGGED,OXYGEN,SPROCKET,FACILITY,'
	DEFM 'CHARMING,ENQUIRY,CONJUROR,AVIATRIX,PLATYPUS,'
	DEFM 'EXORCIST,HARMONICA,AQUARIUM,CHOCOLATE,EAVESDROP,'
	DEFM 'AUTHORITY,OBEDIENCE,ACCORDION,SACRIFICE,CALCULATOR,'
	DEFM 'MOSQUITO,DYNAMITE,SCAVENGER,MONOPOLY,GOLDENROD,'
	DEFM 'FREEWAY,UNLIKELY,VIDEOTAPE,CEREMONY,TRIPLICATE,'
	DEFM 'DECATHLON,BALLYHOO,DAIQUIRI,HEADACHE,PLUMBING,'
	DEFM 'CRIBBAGE,INTELLIGENT,SURFBOARD,CENTIPEDE,VELOCITY,'
;	 LEV 1 13 PTS
	DEFM 'WEREWOLF,QUIRKY,HELICOPTER,QUIBBLER,MANNEQUIN,'
	DEFM 'VAPORIZE,BEQUEATH,HEARTTHROB,EXERCISER,MONARCHY,'
	DEFM 'HIJACK,FARMYARD,JUDGING,DOWNTOWN,EQUINOX,'
	DEFM 'GIMMICK,HADDOCK,HEDGEHOG,KUMQUAT,FLUMMOX,'
	DEFM 'FRUITCAKE,GODFATHER,COOPERATION,MAVERICK,NEWSPAPER,'
	DEFM 'QUALIFY,OPTOMETRIST,INTERRUPTION,GROTESQUE,PERJURY,'
	DEFM 'BUMBLEBEE,PUZZLE,MICROFILM,DAZZLE,SCOREBOARD,'
	DEFM 'TOOTHBRUSH,FOOLISHNESS,INDIGESTION,OMBUDSMAN,BAZOOKA,'
	DEFM 'EARTHWORM,PROFANITY,PLYWOOD,LABORATORY,BURLESQUE,'
	DEFM 'ORTHODOX,GOVERNMENT,HILLBILLY,REGULARITY,WATERMELON,'
;	 LEV 1 14 PTS
	DEFM 'WEEKDAY,DRIZZLE,PARALYZE,BULLDOZER,SQUEEZER,'
	DEFM 'SKYWRITE,COOKBOOK,JITTERBUG,EXPLOSIVE,BACKHAND,'
	DEFM 'EQUIVOCAL,HORSERADISH,HOWITZER,DIAPHRAGM,EUCALYPTUS,'
	DEFM 'SCHISMATIC,INVALUABLY,PYJAMAS,PHARMACY,SUPREMACY,'
	DEFM 'HORIZONTAL,CRITICIZE,STOREKEEPER,APPENDIX,PARTICIPATE,'
	DEFM 'RUCKSACK,GREYHOUND,PUBLICITY,WONDERLAND,SNOWMOBILE,'
	DEFM 'PERSONALITY,MEMORANDUM,CHEMISTRY,TYPEWRITE,WITHDRAW,'
	DEFM 'CHROMOSOME,PROHIBITION,IMPERIALISM,ACUPUNCTURE,DWINDLING,'
	DEFM 'CRYBABY,SIGNIFICANT,PREHISTORIC,CLERGYMAN,BRIDEGROOM,'
	DEFM 'WASHCLOTH,HOVERCRAFT,MACHINERY,DACHSHUND,DRAMATIZE,'
;
;	CLRAUD	CLEAR THE AUDIO REGISTERS
;
CLRAUD::LD	A,0
	LD	(COUNT),A
;
	REPEAT.
		PCALL	AUDIOWR,(COUNT),0
		LD	A,(COUNT)
		INC	A
		LD	(COUNT),A
	UNTIL8. (COUNT),.GT.,	0DH
;
	RET
;
;	SOUND OF AN EXPLOSIN
;
SNDEXP::CALL	CLRAUD
	PCALL	AUDIOWR,6,30
	PCALL	AUDIOWR,7,7
	PCALL	AUDIOWR,8,16	;ALL CHANELS
	PCALL	AUDIOWR,9,16
	PCALL	AUDIOWR,10,16
	PCALL	AUDIOWR,12,150	;THE LENGTH
	PCALL	AUDIOWR,13,0
;
	RET
;
;	SOUND OF A GOOD LETTER 'DING'
;
SNDGLR::CALL	CLRAUD
	PCALL	AUDIOWR,7,62
	PCALL	AUDIOWR,0,200
	PCALL	AUDIOWR,8,16
	PCALL	AUDIOWR,12,50
	PCALL	AUDIOWR,13,0
	RET
;
;	SOUND OF FUSE HISS
;
SNDHIS::CALL	CLRAUD
	PCALL	AUDIOWR,6,5
	PCALL	AUDIOWR,7,55
	PCALL	AUDIOWR,8,16
	PCALL	AUDIOWR,8,15
	RET
;
;	TURN OFF HISS SOUND
;
SNDNDH::CALL	CLRAUD
	PCALL	AUDIOWR,8,0
	RET
;
;
;
COUNT:	DB	0
;
;	TURN ON SPRITE FOR BOMB FUSE
;
BOMFUS::
	PCALL	SPMOVE,0,53,83
	PCALL	SPNAME,0,68
	PCALL	SPCOLR,0,8
	RET
;
;	TURN OFF BOMB FUSE SPRITE
;
BOMOFF::
	PCALL	SPCOLR,0,0
	RET
;
;	TURN ON SPRITE FOR DYNAMITE FUSE
;
DYNFUS::
	PCALL	SPMOVE,1,53,211
	PCALL	SPNAME,1,68
	PCALL	SPCOLR,1,8
;	RUN INTO NEXT CODE
;
;	TURN ON DYNAMITE CLEANUP SPRITES
;
DYNCLN::
	PCALL	SPMOVE,2,79,176
	RET
;
;	TURN OFF DYNAMITE FUSE AND CLEAN UP
;
DYNOFF::
	PCALL	SPCOLR,1,0
	PCALL	SPMARK,2
	RET
;
;
;
;
;
;	PRTAT	CALL PRTAT(POSITION,STRING,LENGTH)
;
;
;	THIS IS A FORTRAN CALLABLE ROUTINE.
;
;
PRTAT::PUSH	DE	;(STRING) ON STACK
	LD	A,(BC)	;GET LENGTH
	LD	E,A
	INC	BC
	LD	A,(BC)
	LD	D,A
	PUSH	DE	;LENGTH ON STACK
;
	LD	E,(HL)	;OFSET INTO DE
	INC	HL
	LD	D,(HL)
PRTAT1: 		;ENTRY FOR PRTCHR
;
	LD	HL,(V.PNT)	;BASE OF NAME TABLE
	ADD	HL,DE	;ABSOLUTE INTO HL
	POP	BC
	POP	DE
	CALL	VRAMLD
	RET
;
;
;
;	PRTCHR		CALL PRTCHR(POSITION,CHARACTER)
;
PRTCHR::
	PUSH	DE	;(CHARACTER) ON STACK
	LD	BC,1	;STRING LENGTH
	PUSH	BC
	PUSH	HL	;SAVE HL
	LD	E,(HL)	;LD DE WITH OFSET
	INC	HL
	LD	D,(HL)
	POP	HL	;INC SCREEN POINTER..
	INC	(HL)	;.. FOR NEXT CALL
	JP	PRTAT1
;
;
;
;	SET UP ALL THE REGISTERS
;
INIT::
;
;	Called at the beginning of the FORTRAN program
;	to initialize anything that needs initing.
;
	RET
;
;
;
;	CLEAR THE SCREEN
;
CLRSCN::
	CALL	BOMOFF	;BOMB SPRITES OFF
	CALL	DYNOFF	;DYNAMITE SPRITES OFF
	PCALL	VFILL,768,0,(V.PNT)
	RET
;
;
;
;	WAITFT Wait for x millisecs fortran callable
;
;	Wait for the count of millisecs pointed to by HL
;
WAITFT::
	LD	A,(HL)		;GET VALUE
	INC	HL
	LD	H,(HL)
	LD	L,A
;
;
;	WAIT	Wait for HL millisecs
;
WAIT::
	PUSH	AF
	PUSH	HL
;
WAIT1:	PUSH	BC
	LD	B,10
WAIT2:	EX	(SP),HL
	EX	(SP),HL
	CALL	KEYPRS
	DJNZ	WAIT2
	POP	BC
;
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,WAIT1
;
	POP	HL
	POP	AF
	RET
;
KEYPRS:
	PUSH 	HL
	PUSH  	BC
	PUSH	AF
	N.DEVRDY KEYBOARD,KEYSTROKE
	AND 	A
	JR	Z,BKWAIT
	N.DEVIO KEYBOARD,KEYSTROKE
   	CP	0DH
	JR	NZ,BKWAIT
;
	POP     AF
	POP	BC
	POP	HL
	LD	B,1
	LD	HL,1
	RET
;
BKWAIT:	POP	AF
	POP	BC
	POP	HL
	RET
;
;
;
;
;	KYBD get a character from CPM
;	     dont wait if non there
;
;
KYBD::
	PUSH	HL	;HL contains the addr to put it
;	LD	C,6
;	LD	E,0FFH		;THIS IS SHIT, DOES NOT READ SYM KEY
;	CALL	0008H	;NABUSYS		;/LEO.
	N.DEVRDY KEYBOARD,KEYSTROKE	;IS THERE A KEY
	AND	A
	JR	Z,KEY			;NO...THEN DON'T READ
	N.DEVIO KEYBOARD,KEYSTROKE	;YES...THEN READ
KEY:	POP	HL
	LD	(HL),A
	INC	HL
	LD	(HL),0
	RET
;
;
;
;	MKADDR	make an address from the bytes pointed
;	to by BC and DE. Fortran callable
;	To make unsigned nos in fortran
;	CALL MKADDR(ADDR,LOBYTE,HIBYTE)
;	Normally used before RAMWR
;
MKADDR::
	LD	A,(DE)
	LD	(HL),A
	INC	HL
	LD	A,(BC)
	LD	(HL),A
	RET
;
;
;
;      JRAMWR	To write blocks of video ram from
;		fortran
;		CALL RAMWR (ADDR,DATA,COUNT)
;			     (HL) (DE) (BC)
;		=>ASSEMBLER
;		CALL VRAMLD LENGTH,(DATA),ADDR
;			     BC     (DE)   HL
;
JRAMWR::
	PUSH	DE	;(DATA)=>DE
	LD	A,(BC)
	LD	E,A
	INC	BC
	LD	A,(BC)
	LD	D,A
	PUSH	DE	;LENGTH=>BC
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A	;ADDR=>HL
	POP	BC
	POP	DE
	JP	VRAMLD
;
;
;
;
;
;	VIDOUT Fortran callable routine to put
;	one byte at an address
;	CALL VIDOUT ( LOWADR,HIADR,BYTE)
;
VIDOUT::
	PUSH	BC	;ADDR OF BYTE => DE
	LD	L,(HL)	;LO ADDR
	LD	A,(DE)	;HI ADDR
	LD	H,A
	POP	DE
	LD	BC,1	;BYTE COUNT
	JP	VRAMLD	;GO DO IT
;
;
;
;
;
;	$INIT	FORTRAN calls this routine to set
;	up the stack and initialize the system.
;	We assume the stack is set and just go back.
;
;
;
$INIT::
	PUSH	BC
	RET
;
;
;
;
;
;	BYEBYE	My program always calls this just before
;	it exits. it can be used to turn off the sound
;	blank the screen etc.
;
;
BYEBYE::
	JP	0000H
;
;
;MY ERROR ROUTINE
$ERR::	JP	BYEBYE
;
;
;INTEGER DIVISION
$D9::	EX	DE,HL
	LD	A,D
	OR	E
	JP	Z,$D90
$D99:	LD	A,D
	OR	A
	LD	B,D
	CALL	M,$NGD
	LD	A,H
	XOR	B
	LD	B,A
	LD	A,H
	OR	L
	RET	Z
	LD	A,H
	OR	A
	EX	DE,HL
	CALL	M,$NGD
	PUSH	BC
	CALL	$IDV
	POP	AF
	OR	A
	CALL	M,$NGD
	EX	DE,HL
	RET
$D90:	CALL	$ERR
$NGD:	XOR	A
	LD	C,A
	SUB	E
	LD	E,A
	LD	A,C
	SBC	A,D
	LD	D,A
	RET
$IDV:	LD	A,H
	CPL
	LD	B,A
	LD	A,L
	CPL
	LD	C,A
	INC	BC
	LD	HL,0
	LD	A,11H
	PUSH	AF
	OR	A
	JP	$IDV3$
$IDV1$: PUSH	AF
	PUSH	HL
	ADD	HL,BC
	JP	NC,$IDV2$
	POP	AF
	SCF
	DEFB	3EH	;JR $+1
$IDV2$: POP	HL
$IDV3$: LD	A,E
	RLA
	LD	E,A
	LD	A,D
	RLA
	LD	D,A
	LD	A,L
	RLA
	LD	L,A
	LD	A,H
	RLA
	LD	H,A
	POP	AF
	DEC	A
	JP	NZ,$IDV1$
	LD	A,H
	OR	A
	RRA
	LD	H,A
	LD	A,L
	RRA
	LD	L,A
	RET
;
;INTEGER MULTIPLY
$M9::	LD	A,H
	OR	L
	RET	Z
	EX	DE,HL
	LD	A,H
	OR	L
	RET	Z
$M90:	LD	B,H
	LD	C,L
	LD	HL,0
	LD	A,10H
$M91$:	ADD	HL,HL
	EX	DE,HL
	ADD	HL,HL
	EX	DE,HL
	JP	NC,$M92$
	ADD	HL,BC
$M92$:	DEC	A
	JP	NZ,$M91$
	RET
;
	END
 
