	.Z80
	.RADIX	10
	CSEG
	INCLUDE	COMMON.MRO
	INCLUDE	STRUCT.MRO
	INCLUDE	LINKTAB.EXA
	INCLUDE VARIABLE.EXA
	EXTRN	PLOPTAB,PLOPDEF,FLIPLOC,LIMVADR,CURVADR,CURRIP,TMP16,TDISKS
	EXTRN	LEVEL,ROUND,CHATAB,MISCOL,FLIP3,MAKERS,CTCOLR,CURFLIP,ADDSCORE
	EXTRN	WAIT1S
	;
	;	THIS FILE CONTAINS THE CODE FOR USE OF THE DISCS
	;
ROTDISK::
	;THIS ROUTINE IS SET UP AS A TASK BY THE MAINLINE AND SETS THE DISKS
	;	TO ROTATE (RUN EVERY 15 INTERRUPTS)
	;
	;
	LD	A,(CURDCL)
	INC	A
	CP	4
	JR	NZ,CROT1
	XOR	A
CROT1:	LD	(CURDCL),A
	LD	C,28
	LD	HL,CHATAB+28
R1L:	LD	A,(HL)			;GET THE CURRENT VALUE OF THE TABLE
	AND	A			;SOMETHING THERE?
	JR	Z,ENDR1L		;NO...GO TO NEXT VALUE
	;
	PUSH	BC			;SAVE NUMBER OF DISK
	PUSH	HL			;SAVE CURRENT TABLE ADDRESS
	LD	E,1			;YES WE DO WANT A DISK THERE
	CALL	PUTDISK
	POP	HL			;GET TABLE ADDRESS
	POP	BC			;GET NUMBER OF DISK
ENDR1L:	INC	C			;INCREMENT NUMBER OF DISK
	INC	HL			;INCREMENT TABLE ADDRESS
	LD	A,42			;GET LAST NUMBER OF TABLE
	CP	C			;ARE WE AT THE LAST ENTRY?
	JR	NZ,R1L			;NO...GO THRU AGAIN
	;		
	RET				;LEAVE
	;
SETRDISK::
	;	THIS ROUTINE WILL SET UP THE POSITION OF THE DISKS
	;	FOR A CERTAIN ROUND
	;
	;
	LD	A,(LEVEL)	;GET THE LEVEL NUMBER
	DEC	A		
	SLA	A		;MULTIPLY IT BY 4
	SLA	A
	LD	BC,(ROUND)	;GET THE ROUND NUMBER
	DEC	C		;
	ADD	A,C		;ADD IT TO THE LEVEL * 4
	LD	E,A		
	PCALL	MUL88,7		;MULTIPLY ALL OF IT BY 7
	LD	A,C		
	CP	126		;IS IT GREATER THAN 5TH LEVEL ROUND 3
	JR	C,SKIPSET	;NO....THEN DON'T MODIFY THIS NUMBER
	;
	LD	C,126		;MAKE IT 5TH LEVEL ROUND 3
SKIPSET:
	LD	HL,DISKRND	;GET THE LOCATION OF THE DISK LOCATIONS TABLE
	ADD	HL,BC		;ADD IT TO THE ROUND/LEVEL OFFSET
	LD	B,7		;DO FOR UP TO 7 LOOPS
PUTDLP:
	LD	A,(HL)		;GET THE NUMBER THERE
	AND	A		;IS IT A ZERO?
	RET	Z		;YES...THEN STOP PUTTING UP DISKS
	;
	PUSH	BC		;SAVE LOOP #
	PUSH	HL		;SAVE CURRENT ADDRESS
	LD	C,A		;GET THE CURRENT DISK NUMBER
	LD	E,1		;YES WE WANT A DISK THERE
	CALL	PUTDISK		;PUT IT ON THE SCREEN
	POP	HL		;GET CURRENT ADDRESS
	POP	BC		;GET LOOP NUMBER
	INC	HL		;INCREMENT ADDRESS
	DJNZ	PUTDLP		;DO FOR ALL DISKS FOR THIS ROUND
	RET			;LEAVE
	;
PUTDISK::
	;	THIS ROUTINE WILL PUT A DISK ON ONE OF THE 14 POSSIBLE
	;	SPOTS THEY COULD BE ON THE SCREEN
	;
	;	PARAMETERS PASSED:
	;
	;	C	-CONTAINS THE LOCATION OF THE DISK (28-41)
	;	E	-TELLS ROUTINE WHETHER TO CLEAR OR PUT DISK
	;
	LD	A,C		;GET THE LOCATION OF THE DISK
	LD	(CURSPIN),A
	LD	HL,CHATAB	;GET THE LOCATION OF THE CHANGE TABLE
	LD	B,0		;CLEAR GARBAGE
	ADD	HL,BC		;ADD TO DISK NUMBER
	LD	(TMP16),HL
	LD	(HL),5		;STORE A 5 (DISK) IN THAT LOCATION
	LD	HL,PLOPTAB	;GET THE ADDRESS OF MY BLOCK NAME TABLE
	ADD	HL,BC		;ADD THE OFFSET
	LD	C,(HL)		;GET MY BLOCK NAME FOR THAT DISK
	LD	B,0		;CLEAR GARBAGE
	SLA	C		
	SLA	C		;MULTIPLY BY 8
	SLA	C
	LD	A,E		;NOW SEE IF WE WANT TO CLEAR OR NOT
	AND	A		;TEST FLAG
	JR	NZ,NOCLR	;NOT CLEAR?
	;
	XOR	A
	LD	HL,(TMP16)
	LD	(HL),A
	LD	(DIWRK),A
	LD	BC,5
	LD	DE,DIWRK+1
	LD	HL,DIWRK
	LDIR
	JP	OUTWRK		;DON'T CRAP ON THE CLEAR TABLE
	;
NOCLR:	LD	HL,PLOPDEF
	ADD	HL,BC
	LD	DE,DIWRK	;GET THE ADDRESS OF THE DISK RAM WORK AREA
	LD	BC,6
	LDIR			;COPY THOSE BYTES
	;
	LD	DE,(CURDCL)	;GET THE CURRENT COLOR START
	SLA	E
	SLA	E
	SLA	E	;MULTIPLY BY 16 FOR OFFSET OF NAME
	SLA	E
	LD	B,6
	LD	HL,DIWRK
ZAPWRK:
	LD	A,(HL)		;GET A BYTE
	CP	0FFH		;HAVE WE REACHED A LIMIT
	JR	Z,OUTWRK	;GET OUT OF THE WORK LOOP
	;
	ADD	A,E		;OTHERWISE ADD IT TO THE OFFSET
	LD	(HL),A		;WRITE IT BACK
	INC	HL
	DJNZ	ZAPWRK
OUTWRK:
	LD	HL,FLIPLOC	;GET THE BASE ADDRESS OF THE VRAM NAME TABLE
	LD	BC,(CURSPIN)
	LD	B,0
	SLA	C
	ADD	HL,BC
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	HL,3C00H
	ADD	HL,DE
	LD	(TMP16),HL
	PCALL	VRAML8,2,DIWRK	;WRITE FIRST TWO BYTES TO VRAM
	LD	HL,(TMP16)
	LD	BC,32			;GOTO NEXT LINE
	ADD	HL,BC			;NEW OFFSET NOW
	LD	(TMP16),HL
	PCALL	VRAML8,2,DIWRK+2	;WRITE THESE TWO BYTES NOW
	LD	A,(DIWRK+4)		;GET THE NEXT BYTE
	CP	0FFH			;DON'T WANT TO WRITE ANY MORE?
	RET	Z			;WE DON'T...SO GET LOST
	;
	LD	BC,32			;GO TO NEXT LINE
	LD	HL,(TMP16)
	ADD	HL,BC			;NEW OFFSET AGAIN
	PCALL	VRAML8,2,DIWRK+4	;WRITE EM
	RET				;LEAVE
SETDISK::
	PCALL	RPATRN,TDISKS
	PCALL	LPATRN,TDISKS,10H*8
	PCALL	LPATRN,TDISKS,20H*8
	PCALL	LPATRN,TDISKS,30H*8
	PCALL	LPATRN,TDISKS,800H
	PCALL	LPATRN,TDISKS,800H+10H*8
	PCALL	LPATRN,TDISKS,800H+20H*8
	PCALL	LPATRN,TDISKS,800H+30H*8
	PCALL	LPATRN,TDISKS,1000H
	PCALL	LPATRN,TDISKS,1000H+10H*8
	PCALL	LPATRN,TDISKS,1000H+20H*8
	PCALL	LPATRN,TDISKS,1000H+30H*8
	XOR	A
	LD	BC,4
	LD	DE,ROT1
	LD	HL,ROTDATA
	LDIR
	PCALL	SDISK,2000H+70H*8
	PCALL	SDISK,2800H+70H*8
	PCALL	SDISK,3000H+70H*8
	LD	BC,4
	LD	DE,ROT1
	LD	HL,ROTDATA+4
	LDIR
	PCALL	SDISK,2000H+80H*8
	PCALL	SDISK,2800H+80H*8
	PCALL	SDISK,3000H+80H*8
	LD	BC,4
	LD	DE,ROT1
	LD	HL,ROTDATA+8
	LDIR
	PCALL	SDISK,2000H+90H*8
	PCALL	SDISK,2800H+90H*8
	PCALL	SDISK,3000H+90H*8
	LD	BC,4
	LD	DE,ROT1
	LD	HL,ROTDATA+12
	LDIR
	PCALL	SDISK,2000H+0A0H*8
	PCALL	SDISK,2800H+0A0H*8
	PCALL	SDISK,3000H+0A0H*8
	RET
SDISK::
	;	THIS ROUTINE WILL SETUP THE COLORS OF THE DISKS
	;
	;	PARAMETERS PASSED:
	;				BC - VRAM ADDRESS TO START AT
	;
	;
	LD	(CURVADR),BC	;STORE THE STARTING VRAM ADDRESS
	PUSH	BC
	POP	HL		;WHAT WE WANT TO DO NOW IS SET THE LIMIT
	LD	BC,80		;BY ADDING THE LIMIT OF THAT SET OF PATTERNS
	ADD	HL,BC		;TO THE STARTING ADDRESS
	LD	(LIMVADR),HL	;NOW STORE THAT
	LD	BC,DIMASK	;GET THE ADDRESS OF THE RAM MASK COLOR TABLE
	LD	(CURRIP),BC	;STORE THE CURRENT TABLE POINTER
	REPEAT.
	  LD	DE,(CURRIP)	;GET THE CURRENT POINTER
	  LD	A,(DE)		;GET THE VALUE THERE
	  LD	B,A
	  AND	0FH		;MASK OUT THE UPPER NYBBLE
	  LD	E,A		;HOLD ONTO IT TEMPORARILY
	  LD	A,B		;GET IT AGAIN
	  AND	0F0H		;NOW MASK OUT THE LOWER NYBBLE
	  LD	D,A		;NOW HOLD THAT TEMPORARILY
	  CP	10H		;IS IT A ONE?
	  JR	NZ,RTRY2	;NO....TRY ANOTHER NUMBER
	  ;
	  LD	A,(ROT1)	;GET THE FIRST QUARTER COLOR
	  SLA	A
	  SLA	A
	  SLA	A
	  SLA	A		
	  JR	NXTNYR		;GOTO NEXT NYBBLE
	  ;
RTRY2:	  CP	20H		;IS IT A 2 THEN?
	  JR	NZ,RTRY3	;NO...TRY ANOTHER NUMBER
	  ;
	  LD	A,(ROT2)	;GET THE SECOND QUARTER COLOR
	  SLA	A
	  SLA	A
	  SLA	A		;MOVE IT INTO THE UPPER NYBBLE
	  SLA	A
	  JR	NXTNYR		;GOTO NEXT NYBBLE
	  ;
RTRY3:	  CP	30H		;IS IT A 3 THEN?
	  JR    NZ,RTRY4	;NO..TRY ANOTHER NIBBLE
	  ;
	  LD	A,(ROT3)
	  SLA	A
	  SLA	A
	  SLA	A
	  SLA	A
	  JR	NXTNYR
	  ;
RTRY4:	  CP	40H		;IS IT A 4 THEN?
	  JR	NZ,RTRY5	;NO...TRY ANOTHER NIBBLE
	  ;
	  LD	A,(ROT4)	;GET THE 4TH QUARTER COLOR
	  SLA	A
	  SLA	A
	  SLA	A
	  SLA	A
	  JR	NXTNYR
	  ;
RTRY5:	  CP	50H		;HOW ABOUT THE BOTTOM COLOR
	  JR	NZ,NXTNYR
NXTNYR:
	  LD	C,A		;NOW WE WORK WITH THE OTHER NYBBLE
	  LD	A,E
	  CP	1		;IS IT A ONE?
	  JR	NZ,RTRY6	;NO TRY ANOTHER ONE
	  ;
	  LD	A,(ROT1)	;ELSE USE THE FIRST QUARTER COLOR
	  JR	RGLUEM		;PUT EM TOGETHER
	  ;
RTRY6:	  CP	2		;IS IT A TWO?
	  JR	NZ,RTRY7	;NO...TRY ANOTHER ONE
	  ;
	  LD	A,(ROT2)	;GET THE SECOND QUARTER COLOR
	  JR	RGLUEM		;PUT EM TOGETHER
	  ;
RTRY7:	  CP	3		;IT IS THREE?
	  JR	NZ,RTRY8	;NO....TRY ANOTHER ONE
	  ;
	  LD	A,(ROT3)	;ELSE USE THE THIRD QUARTER COLOR
	  JR	RGLUEM
	  ;
RTRY8:	  CP	4		;IS IT A 4?
	  JR	NZ,RTRY9	;NO...TRY ANOTHER ONE
	  ;
	  LD	A,(ROT4)
	  JR	RGLUEM
	  ;
RTRY9:	  CP	5		;IS IT A 5?
	  JR	NZ,RGLUEM	;NO MUST BE BACKROUND
	  ;
	  LD	A,7
	  ;
RGLUEM:	  OR	C		;MASK EM TOGETHER
	  LD	E,A
	  PCALL	VRAMWR,(CURVADR) ;PUT IT IN THE COLOR TABLE IN VRAM
	  LD	HL,(CURVADR)
	  INC	HL
	  LD	(CURVADR),HL
	  LD	HL,(CURRIP)
	  INC	HL
	  LD	(CURRIP),HL
	UNTIL16. (CURVADR),.EQ.,(LIMVADR)   ;UNTIL THE LIMIT HAS BEEN REACHED
	RET
BONDISK::
	;	THIS ROUTINE WILL CALCULATE THE BONUS DERIVED AT THE END
	;	OF A ROUND FROM THE DISKS LEFT OVER
	;
	LD	HL,CHATAB+28		;GET THE DISK AREA OF THE CHANGE TABLE
	LD	C,28			;DO FOR 14 POSSIBLE DISKS
BONDILP:
	LD	A,(HL)			;GET THE NUMBER THERE
	AND	A			;IS IT ANYTHING BESIDES A ZERO?
	JR	Z,NOBOND		;NO...THEN NO BONUS FOR THIS DISK
	PUSH	HL
	PUSH	BC
	LD	E,0			;TELL IT TO GET RID OF THE DISK
	CALL	PUTDISK
	PCALL	ADDSCORE,250H		;ADD THE BONUS AMOUNT
;	MAKE THE DISK REMOVAL SOUND HERE
	CALL	WAIT1S
	POP	BC
	POP	HL
NOBOND:	INC	C
	INC	HL
	LD	A,42
	CP	C
	RET	Z
	JR	BONDILP
CTSETUP::
	;	THIS ROUTINE WILL SET UP THE COLOR IN THE "CHANGE TO" CUBE.
	;
	LD	HL,2000H+8CH*8
	LD	(CURVADR),HL
	LD	BC,32
	ADD	HL,BC
	LD	(LIMVADR),HL
	ASIGN16. (CURRIP),CTCOLR
	ASIGN8.	(CURFLIP),(FLIP3)
	JP	MAKERS
	;
PUTARROWS::
	; THIS	ROUTINE WILL RUN THROUGH THE ARROWS POINTING ANIMATION
	;
	INC8.	(ARRCNT)	;INCREMENT THE TOGGLE
	CP	3		;IS IT AT IT'S LIMIT
	JR	NZ,SKPRST	;NO...THEN JUST CONTINUE
	;
	XOR	A		;RESET THE TOGGLE
	LD	(ARRCNT),A
SKPRST:
	JR	Z,CLRARR	;IS IT ZERO? THEN CLEAR ALL ARROWS
	;
	CP	1		;IS IT ONE?
	JR	Z,FSTARR	;YES...THEN PUT UP THE FIRST ARROWS
	;
	;	OTHERWISE PUT UP THE REST OF THE ARROWS
	;
	PCALL	PUTPAT,5,3,8AH
	PCALL	PUTPAT,8,3,8BH
	RET
FSTARR:
	;	PUT UP THE FIRST ARROWS
	;
	PCALL	PUTPAT,4,3,8AH
	PCALL	PUTPAT,9,3,8BH
	RET
CLRARR:
	;	CLEAR ALL ARROWS
	;
	PCALL	VFILL,2,0,3C00H+3*20H+4
	PCALL	VFILL,2,0,3C00H+3*20H+8
	RET
ROTDATA:
	DB	DGREEN,DYELLOW,DBLUE,DRED
	DB	DRED,DGREEN,DYELLOW,DBLUE
	DB	DBLUE,DRED,DGREEN,DYELLOW	
	DB	DYELLOW,DBLUE,DRED,DGREEN
	;
DIMASK::
	.RADIX	16
	DB	00,00,10,10,10,40,40,43
	DB	34,34,35,35,35,05,05,00
	DB	00,00,00,10,10,10,12,12
	DB	21,20,25,35,35,05,05,00
	;
	DB	00,00,00,00,00,00,00,10
	DB	10,40,40,43,34,34,35,35
	DB	00,00,00,00,00,00,00,10
	DB	10,10,12,12,21,20,25,35
	DB	35,05,05,00,00,00,00,00
	DB	35,05,05,00,00,00,00,00
	;
DISKRND:
	.RADIX	10
	DB	36,37,0,0,0,0,0
	DB	38,41,0,0,0,0,0
	DB	34,39,0,0,0,0,0
	DB	28,41,0,0,0,0,0
	;
	DB	29,34,37,0,0,0,0
	DB	29,38,39,0,0,0,0
	DB	32,39,0,0,0,0,0
	DB	30,35,0,0,0,0,0
	;
	DB	32,33,35,36,0,0,0
	DB	32,34,35,39,0,0,0
	DB	28,32,37,0,0,0,0
	DB	31,37,41,0,0,0,0
	;
	DB	28,34,38,39,41,0,0
	DB	28,31,36,38,39,41,0
	DB	32,33,35,36,41,0,0	;******NOT SURE*****
	DB	28,30,35,41,0,0,0
	;
	DB	28,29,30,33,34,38,41
	DB	28,31,35,36,37,38,0
	DB	32,33,35,36,41,0,0
	;
	;	AFTER 5 LEVEL 3 ROUND, SAME FOREVER
	END

