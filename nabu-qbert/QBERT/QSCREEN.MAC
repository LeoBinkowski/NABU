	.Z80
	.RADIX	10
	CSEG
	INCLUDE COMMON.MRO
	INCLUDE	LINKTAB.EXA
	INCLUDE STRUCT.MRO
	INCLUDE CLKAT.MRO
	EXTRN	QBOARD,QNAME,RICOLR,FLIP,CHATAB,ROTDISK,SETDISK,MISCGRA,MISCOL
	EXTRN	ENGLISH,SETRDISK,TIMGRAPH,TIMINIT,PUTDISK,DOFLASH,RSTBRD
	EXTRN	CTSETUP,PUTARROWS,PLAYER,PUTFM,LEVEL,ROUND,DETPLA,QCONTROL
	EXTRN	P1SCORE,P2SCORE,P1FMSC,P2FMSC,P1MEN,P2MEN,HISC24,LEVCOL
	EXTRN	BCDTOASC,BUF1A,DOBONUS,P1BONUS,P2BONUS,BONDISK,CSHELL,OPENQB
	EXTRN	P1LEVEL,P1ROUND,P2LEVEL,P2ROUND,P2CHATAB,LEVELSC,QPAT,PUTNG
	EXTRN	DODEMO,INDEMO,DECFM,CDECFM,PLAYER,NEXTUP,P1XC,P1YC,P2XC,P2YC
	EXTRN	SOUND2,A_OFF,B_OFF,C_OFF,TASK1,UNITIME,UNICLOCK
	QBERT	EQU	0
	KHELP	EQU	0EAH
	REST1	EQU	0ABH
	REST2	EQU	0BDH
	;
GAMEOPT:: DB	00H
P1DEMO::  DB	00H
P2DEMO::  DB	00H
DOFLAG:	DB	00H
;
FLIP1:	DB	0DH		;THE COLOR RESTITUTION DATA TABLE
FLIP2:	DB	04H
FLIP3::	DB	03H
COLOR1:	DB	0FH
COLOR2:	DB	0EH
;
CURVADR::DW	00H
LIMVADR::DW	00H
CURRIP::DW	00H
CURFLIP::DB	0DH
ROTCB:	DS	6
ARRTCB:	DS	6
	;	
QSCREEN::
	;	THIS IS THE START OF THE Q*BERT GAME.
	;
	;
	LD	SP,(6)	;SET STACK
	N.LINKIO LNKTB##	;ATTACH TO IOS
	SETCOLR BLACK,BLACK
	LD HL,OPENQB		;LOAD HL WITH THE OPENING SCREEN ADDRESS
	PUSH HL
	CALL PUTNG
	XOR A
	LD (UNITIME),A		;CLEAR TIME COUNTER
	SETSK UNICLOCK,TASK1,60,1	;SET CLOCK TASK TO RUN EVERY SECOND
QCP1:
	N.DEVRDY KEYBOARD,JSTCK1
	CP 0
	JR Z,QCP15
	N.DEVIO KEYBOARD,JSTCK1
	AND 10H
	CP 10H
	JP Z,QCP2	;IF FIRE LEAVE OPENNING SCREEN
QCP15:
	N.DEVRDY KEYBOARD,KEYSTROKE
	CP 0
	JR Z,QCP16
	N.DEVIO KEYBOARD,KEYSTROKE
	CP 0DH		;CARRIAGE RETURN
	JR Z,QCP2	;IF SO LEAVE OPENING GRAPHIC
QCP16:
	LD A,(UNITIME)
	CP 6		;5 SECONDS YET
	JP C,QCP1	;IF NOT KEEP CHECKING FOR GO/FIRE
QCP2:
	N.CLKRV TASK1	;REMOVE TIMER TASK
	LD BC,0000
	PUSH BC
	LD BC,0000
	PUSH BC
	LD BC,0000
	PUSH BC
CASHELL::
	PCALL SOUND2,A_OFF
	PCALL SOUND2,B_OFF
	PCALL SOUND2,C_OFF	;TURN OFF ALL 3 SOUND CHANNELS
	CALL CSHELL
	LD A,(HL)		;GET THE RESULTS FROM THE OPTION SCREEN
	LD (GAMEOPT),A		;SAVE THE GAME OPTION IN THIS VARIABLE
	PCALL AUDIOWR,7,0F8H	;FIX THE SOUND REGISTER
	ASIGN8.	(DOFLAG),0FFH
QPUTUP::
	CALL	VSETG2
	PCALL	VPTRNST,0000H
	PCALL	VCOLRST,2000H
	PCALL	VSPRIST,1800H
	PCALL	VNAMEST,3C00H
	PCALL	VATRIST,3F00H
	PCALL	VFILL,4000H,0,0
	PCALL	RPATRN,QBOARD
	PCALL	LPATRN,QBOARD,23H*8
	PCALL	LPATRN,QBOARD,23H*10H
	PCALL	LPATRN,QBOARD,800H
	PCALL	LPATRN,QBOARD,800H+23H*8
	PCALL	LPATRN,QBOARD,800H+23H*10H
	PCALL	LPATRN,QBOARD,1000H
	PCALL	LPATRN,QBOARD,1000H+23H*8
	PCALL	LPATRN,QBOARD,1000H+23H*10H
	PCALL	VFILL,300H,0F0H,2500H
	PCALL	VFILL,300H,0F0H,3500H
	PCALL	VFILL,300H,0F0H,2D00H
	PCALL	LPATRN,ENGLISH,400H
	PCALL	LPATRN,ENGLISH,0C00H
	PCALL	LPATRN,ENGLISH,1400H
	PCALL	RPATRN,MISCGRA
	PCALL	LPATRN,MISCGRA,800H
	PCALL	LPATRN,MISCGRA,1000H
	PCALL	LPATRN,MISCOL,2000H
	PCALL	LPATRN,MISCOL,2800H
	PCALL	LPATRN,MISCOL,3000H
	PCALL	VRAMLD,300H,QNAME,3C00H
	SETCOLR	BLACK,BLACK
	LD	A,2				;SET THE NUMBER OF MEN
	LD	(P1MEN),A
	LD	(P2MEN),A
	LD	BC,0750H
	LD	(P1BONUS),BC
	LD	(P2BONUS),BC
	XOR	A
	LD 	(P1DEMO),A		;SET THE LEVEL DEMO SEEN TO 0 
	LD	(P2DEMO),A		
	LD	(P1BONUS+2),A
	LD	(P2BONUS+2),A
	LD	(NEXTUP),A		;SET NEXT PLAYER UP TO PLAYER 1
	LD	(PLAYER),A		;SET INITIAL PLAYER TO 1
	LD	A,1
	LD	(LEVEL),A
	LD	(ROUND),A
	LD	(P1LEVEL),A
	LD	(P1ROUND),A
	LD	(P2LEVEL),A
	LD	(P2ROUND),A
	LD	A,5
	LD	(P2CHATAB+36),A
	LD	(P2CHATAB+37),A
	LD	(CHATAB+36),A
	LD	(CHATAB+37),A
	;	NOW INITIALIZE THE SCORE VARIABLES
	;
	XOR	A
	LD	HL,P1SCORE	;CLEAR SCORE VARIABLES
	LD	(HL),A		;SET FIRST BYTE TO 0
	LD	DE,P1SCORE+1
	LD	BC,12		;ALL BUT HIGH SCORE
	LDIR
	LD	A,60H		;INITIALIZE FREE MAN SCORE VARS
	LD	(P1FMSC+1),A
	LD	(P2FMSC+1),A
	LD	A,120
	LD	(P1XC),A
	LD	(P2XC),A	;RESET THE PLAYER 1 + 2 TO THETOP CUBE
	LD	A,22
	LD	(P1YC),A
	LD	(P2YC),A	;RESET THE PLAYER 1 + 2 TO THE TOP CUBE
	CALL	TIMGRAPH
;	CALL	CREST
	CALL	SETDISK
	CALL	DETPLA
	CALL	VBLKOFF
	JP	FLIPCK
CREST::
	;THIS ROUTINE WILL RESTITUTE THE COLORS IN THE COLOR TABLE TO THE
	;PROPER FLIP COLORS ETC (NOW ALSO SETS UP SCREEN, ETC.)
	;
	CALL VBLKON		;TIM TRYING TO REMOVE COLOR FLASH
	SETCOLR BLACK,BLACK	;THIS IS NEEDED INCASE PLAYER DIES WITH MAGENTA
				;SCREEN.
	CALL	CSCHEME				;SET UP THE COLOR SCHEME
	PCALL	RESTIT,(FLIP1),2000H		;SET UP COLOR 1 TABLE 1
	PCALL	RESTIT,(FLIP2),2000H+23H*8	;SET UP COLOR 2 TABLE 1
	PCALL	RESTIT,(FLIP3),2000H+23H*10H	;SET UP COLOR 3 TABLE 1
	PCALL	RESTIT,(FLIP1),2800H		;SET UP COLOR 1 TABLE 2	
	PCALL	RESTIT,(FLIP2),2800H+23H*8	;SET UP COLOR 2 TABLE 2
	PCALL	RESTIT,(FLIP3),2800H+23H*10H	;SET UP COLOR 3 TABLE 2
	PCALL	RESTIT,(FLIP1),3000H		;SET UP COLOR 1 TABLE 3
	PCALL	RESTIT,(FLIP2),3000H+23H*8	;SET UP COLOR 2 TABLE 3
	PCALL	RESTIT,(FLIP3),3000H+23H*10H	;SET UP COLOR 3 TABLE 3
	CALL	CTSETUP
	LD A,(ROUND)
	CP 1		;IS THIS THE FIRST ROUND?
	JP 	NZ,NODEMO		;IF IT IS GO DO DEMO
	LD A,(LEVEL)
	LD C,A			;MOVE ACTUAL LEVEL TO C
	LD A,(PLAYER)
	CP 0
	JP NZ,GRG2		;=IF NOT FIRST THEN GET SECOND PLAYERS LEVEL
	LD A,(P1DEMO)
	JP GRG3			;MOVE PLAYER 1 LEVEL TO A
GRG2:
	LD A,(P2DEMO)
GRG3:
	CP C
	JP NC,NODEMO		;IF ACTUAL LEVEL IS NOT GREATER DON'T DEMO	
	LD A,(INDEMO)
	CP 0
	JP NZ,NODEMO
	CALL DODEMO
	CALL ZIPTHRU
NODEMO:
	PCALL	VRAMLD,300H,QNAME,3C00H
	LD	A,(LEVEL)
	OR	0B0H
	LD	L,A
	PCALL	PUTPAT,29,4
	LD	A,(ROUND)
	OR	0B0H
	LD	L,A
	PCALL	PUTPAT,29,5
	;
	PCALL	BCDTOASC,P1SCORE,BUF1A		;SET UP THE SCORE INITIALLY
	PCALL	VRAML8,6,BUF1A,3C24H		;FOR THIS SCREEN
	PCALL	BCDTOASC,P2SCORE,BUF1A
	PCALL	VRAML8,6,BUF1A,3C37H
	PCALL	BCDTOASC,HISC24,BUF1A
	PCALL	VRAML8,6,BUF1A,3C0DH
	CALL	SETFM
	CALL	TIMINIT
	SETSK	ROTDISK,ROTCB,6,1
	SETSK	PUTARROWS,ARRTCB,20,1
	CALL VBLKOFF	;TIM TRYING TO FIX COLOR FLASH
	LD	A,(PLAYER)
	AND	A
	JR	NZ,C2PUT
	;
	PCALL	VRAMWR,3C0AH,7AH
	RET
C2PUT:
	PCALL	VRAMWR,3C1CH,7BH
	RET
RESTIT::
	;	THIS ROUTINE WILL RESTITUTE A PORTION OF THE COLOR TABLE TO
	;	THE PROPER FLIP COLORS (AS CALLED)
	;	
	;	PARAMETERS PASSED:
	;				C - FLIP COLOR
	;			       DE - VRAM ADDRESS TO START AT
	;
	;
	LD	A,C		;GET THE FLIP COLOR
	LD	(CURFLIP),A	;STORE IT AWAY
	LD	(CURVADR),DE	;NOW STORE THE STARTING VRAM ADDRESS
	EX	DE,HL		;WHAT WE WANT TO DO NOW IS SET THE LIMIT
	LD	BC,118H		;BY ADDING THE LIMIT OF THAT SET OF PATTERNS
	ADD	HL,BC		;TO THE STARTING ADDRESS
	LD	(LIMVADR),HL	;NOW STORE THAT
	LD	BC,RICOLR	;GET THE ADDRESS OF THE RAM MASK COLOR TABLE
	LD	(CURRIP),BC	;STORE THE CURRENT TABLE POINTER
MAKERS::
	REPEAT.
	  LD	DE,(CURRIP)	;GET THE CURRENT POINTER
	  LD	A,(DE)		;GET THE VALUE THERE
	  LD	B,A
	  AND	0FH		;MASK OUT THE UPPER NYBBLE
	  LD	E,A		;HOLD ONTO IT TEMPORARILY
	  LD	A,B		;GET IT AGAIN
	  AND	0F0H		;NOW MASK OUT THE LOWER NYBBLE
	  LD	D,A		;NOW HOLD THAT TEMPORARILY
	  CP	10H		;IS IT A ONE?
	  JR	NZ,CTRY2	;NO....TRY ANOTHER NUMBER
	  ;
	  LD	A,(CURFLIP)	;GET THE CURRENT FLIP COLOR
	  SLA	A
	  SLA	A
	  SLA	A
	  SLA	A		
	  JR	NXTNYB		;GOTO NEXT NYBBLE
	  ;
CTRY2:	  CP	20H		;IS IT A 2 THEN?
	  JR	NZ,CTRY3	;NO...TRY ANOTHER NUMBER
	  ;
	  LD	A,(COLOR1)	;GET THE FIRST SIDE COLOR
	  SLA	A
	  SLA	A
	  SLA	A		;MOVE IT INTO THE UPPER NYBBLE
	  SLA	A
	  JR	NXTNYB		;GOTO NEXT NYBBLE
	  ;
CTRY3:	  CP	30H	;EITHER ITS A 3 OR IT'S BACKGROUND
	  JR    NZ,NXTNYB
	  ;
	  LD	A,(COLOR2)
	  SLA	A
	  SLA	A
	  SLA	A
	  SLA	A
	  ;
NXTNYB:
	  LD	C,A		;NOW WE WORK WITH THE OTHER NYBBLE
	  LD	A,E
	  CP	1		;IS IT A ONE?
	  JR	NZ,CTRY4	;NO TRY ANOTHER ONE
	  ;
	  LD	A,(CURFLIP)	;ELSE USE THE CURRENT FLIP COLOR
	  JR	GLUEM		;PUT EM TOGETHER
	  ;
CTRY4:	  CP	2		;IS IT A TWO?
	  JR	NZ,CTRY5	;NO...TRY ANOTHER ONE
	  ;
	  LD	A,(COLOR1)	;GET THE FIRST SIDE COLOR
	  JR	GLUEM		;PUT EM TOGETHER
	  ;
CTRY5:	  CP	3		;IT IS THREE?
	  JR	NZ,GLUEM	;NO....MUST BE BACKGROUND THEN...CLEAR
	  ;
	  LD	A,(COLOR2)	;ELSE USE THE SECOND SIDE COLOR
	  ;
GLUEM:	  OR	C		;MASK EM TOGETHER
	  LD	E,A
	  PCALL	VRAMWR,(CURVADR) ;PUT IT IN THE COLOR TABLE IN VRAM
	  LD	HL,(CURVADR)
	  INC	HL
	  LD	(CURVADR),HL
	  LD	HL,(CURRIP)
	  INC	HL
	  LD	(CURRIP),HL
	UNTIL16. (CURVADR),.EQ.,(LIMVADR)   ;UNTIL THE LIMIT HAS BEEN REACHED
	RET
CSCHEME::
	;THIS ROUTINE WILL FIGURE OUT THE COLOR SCHEME FOR A PARTICULAR LEVEL
	;
	LD	A,(LEVEL)
	DEC	A
	SLA	A
	SLA	A
	LD	DE,(ROUND)
	DEC	E
	ADD	A,E
	LD	E,A
	PCALL	MUL88,5
	LD	HL,CSTAB
	ADD	HL,BC
	LD	BC,5		;NOW MOVE THOSE BYTES INTO THE COLOR VARS
	LD	DE,FLIP1
	LDIR
	RET
FLIPCK:
	LD A,(CDECFM)
	CP 1
	JP NZ,FLIPC2
CHTES::	N.CLKRV 0
	CALL DECFM
	XOR A
	LD (CDECFM),A		;THIS BETTER FIX THE NO JOYSTICK BUSINESS
FLIPC2:
	;  THIS IS A TEST ROUTINE THAT WILL FLIP CERTAIN SQUARES
	;  AND HANDLE RESTART,HELP CHECKING
	;
	N.DEVRDY KEYBOARD,KEYSTROKE
	AND	A
	JR	Z,CONTIT
	N.DEVIO	 KEYBOARD,KEYSTROKE
;	PUSH AF
;	CP	20H		;IS IT A SPACE?
;	CALL	Z,ZIPTHRU	;INCREMENT THE ROUND, LEVEL
;	POP AF
	CP KHELP		;IS IT HELP 
	JR NZ,FLIPC3		;IF NOT GO CHECK 
	N.CLKRV 0
	LD HL,0001		;SET SHELL SELECTION TO HELP
	PUSH HL
	LD HL,0000		;SET SCORE TO 0
	PUSH HL
	PUSH HL			;SET TWO SCORE VARIABLESTO 0
	JP CASHELL
FLIPC3:
	CP REST1		;IS IT RESTART
	JR NZ,FLIPC4
	N.CLKRV 0
	LD HL,0000
	PUSH HL
	PUSH HL
	PUSH HL
	JP CASHELL		;GO CALL SHELL
FLIPC4:
	CP REST2
	JR NZ,CONTZ
	N.CLKRV 0
	LD HL,0000
	PUSH HL
	PUSH HL
	PUSH HL
	JP CASHELL		;GO CALL SHELL
CONTZ:	CP	'Z'-40H		;CNTRL-Z?
	JP 	Z,DEBUG
	;
CONTIT:
	LD	A,(RSTBRD)
	AND	A
	CALL	NZ,ZIPTHRU
	JR	FLIPCK
DEBUG::	
;	CALL PRISMPRINT
	JP	FLIPCK
ZIPTHRU::
	;	THIS IS A TEST ROUTINE THAT WILL RUN THROUGH THE COLORS
	;
;	N.DEVIO KEYBOARD,KEYSTROKE	;LOOK FOR KEY
;	JR	Z,ZIPTHRU		;IF NONE PRESSED THEN TRY AGAIN
	;
	INC8.	(ROUND)			;INCREMENT THE ROUND
	CP	5			;IS IT TOO HIGH?
	JP	NZ,AREST		;NO...THE DO THE COLOR CHANGE
	;
	LD	A,1			;RESET THE ROUND
	LD	(ROUND),A
	INC8.	(LEVEL)			;INCREMENT THE LEVEL
	CP	10			;TOO HIGH?
	JP	NZ,AREST		;NO...COLOR IT
	;
	LD	A,9
	LD	(LEVEL),A
AREST:	
	N.CLKRV	0
	LD A,(INDEMO)
	CP 1
	JP Z,ARE2
	CALL	DOBONUS
	CALL	DOFLASH
	CALL	BONDISK
ARE2:
	XOR	A
	LD	(CHATAB),A
	LD	HL,CHATAB
	LD	DE,CHATAB+1
	LD	BC,41
	LDIR
	LD A,(INDEMO)
	CP 1
	JP Z,ARE3
	CALL	CREST
ARE3:
	CALL	SETRDISK
	XOR A
	LD (INDEMO),A	;RESET THIS FLAG BY NOW THE DEMO IS DONE
	RET
	;
SETFM::
	;THIS ROUTINE WILL SET UP A NUMBER OF FREE MEN ON THE SCREEN
	LD	A,(PLAYER)		;GET THE CURRENT PLAYER
	AND	A			;IF ZERO THEN PLAYER 1
	JR	NZ,FMP2
	;
	LD	BC,(P1MEN)		;GET PLAYER 1'S MEN
	JR	GOSETFM
	;
FMP2:	LD	BC,(P2MEN)		;GET PLAYER 2'S MEN
GOSETFM:
	LD	A,C		;IF NO QBERTS THEN DON'T DO 256 OF THEM
	AND	A
	RET	Z
	;
FMLOOP:	LD	B,C		;PUT IN BOTH
	PUSH	BC		;SAVE THE NUMBER BEING WORKED ON
	CALL	PUTFM		;DO IT
	POP	BC		;RESTORE NUMBER
	DEC	C		;DECREMENT NUMBER BEING WORKED ON
	DJNZ	FMLOOP		;DO UNTIL FINISHED
	RET
CSTAB::
	.RADIX	16
	DB	DBLUE,MRED,DYELLOW,WHITE,GREY
	DB	LGREEN,MRED,DBLUE,DYELLOW,WHITE
	DB	DYELLOW,MRED,GREY,WHITE,LGREEN
	DB	WHITE,MRED,DYELLOW,GREY,LGREEN
	;
	DB	LGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	DBLUE,DYELLOW,GREY,WHITE,MGREEN
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	WHITE,DYELLOW,DBLUE,BLACK,BLACK
	;
	DB	DYELLOW,MGREEN,MGREEN,GREY,WHITE
	DB	DBLUE,DYELLOW,DYELLOW,MGREEN,GREY
	DB	MGREEN,DYELLOW,DYELLOW,GREY,WHITE
	DB	DBLUE,GREY,GREY,WHITE,MGREEN
	;
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	WHITE,DYELLOW,DBLUE,BLACK,BLACK
	DB	GREY,DBLUE,MGREEN,DYELLOW,WHITE
	DB	WHITE,DBLUE,DYELLOW,MGREEN,GREY
	;
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	DBLUE,DYELLOW,GREY,WHITE,MGREEN
	DB	MGREEN,DBLUE,DYELLOW,WHITE,DBLUE
	DB	WHITE,DYELLOW,DBLUE,MGREEN,GREY
	;
	DB	GREY,DBLUE,MGREEN,DYELLOW,WHITE
	DB	DBLUE,MGREEN,DYELLOW,BLACK,BLACK
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	DBLUE,DYELLOW,GREY,WHITE,MGREEN
	;
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	WHITE,DYELLOW,DBLUE,MGREEN,GREY
	DB	GREY,DBLUE,MGREEN,DYELLOW,WHITE
	DB	DBLUE,MGREEN,DYELLOW,BLACK,BLACK
	;
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	DBLUE,DYELLOW,MGREEN,WHITE,LGREEN
	DB	LGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	WHITE,DYELLOW,DBLUE,MGREEN,GREY
	;
	DB	GREY,DBLUE,MGREEN,DYELLOW,WHITE
	DB	DBLUE,MGREEN,DYELLOW,BLACK,BLACK
	DB	MGREEN,DBLUE,DYELLOW,GREY,WHITE
	DB	DBLUE,DYELLOW,GREY,WHITE,MGREEN
	END

