	;

;*****************************************************************
;*                                                               *
;*    FILE NAME:         INKMOV.MAC                              *
;*                                                               *
;*    MODULE PART NO.: 73-90034420 NAME:  PAC MAN 		 *
;*                                                               *
;*    FILE DESCRIPTION:                                          *  
;*      THIS contains the personality logic for inky.            *
;*      (the light blue ghost)                                   *
;*                                                               *
;*                                                               *
;*                                                               *
;*                                                               *
;*                                                               *
;*    PROGRAMMER: L. Binkowski                                   *
;*                                                               *
;*    ENVIRONMENT- Development Machine: NABU 1100, MCP 1.8       *
;*                                                               *
;*               - Assembler: MACRO-80                           *
;*                                                               *
;*                                                               *
;*****************************************************************
;*                                                               *
;*    REVISION HISTORY: (current first)                          *
;*                                                               *
;*    Rev.    Date       Created By         Revision Details     *
;*    ---  ----------  ---------------  -----------------------  *
;*    02   19 OCT 83 	L. Binkowski	changed relate code	 *
;*                                                               *
;*    01   04 OCT 83    L. Binkowski    Initial release to spec  *
;*                                                               *
;*****************************************************************
 
	.Z80
	.PRINTX	/INKY'S MOVEMENT ROUTINES....OCTOBER 19, 1983/
	.RADIX	10
	.XLIST
	INCLUDE PACMAN.EQU
	INCLUDE COMMON.MRO
	INCLUDE	ARITH.MRO
	INCLUDE STRUCT.MRO
	INCLUDE	LINKTAB.EXA
	INCLUDE SUPRTASK.MRO
INKTIMR::DS	1	;TIMER FOR WHEN INKY SHOULD COME OUT
ISICK::	 DB	FALSE	;INKY SICK FLAG
IQFLG::	DB	00H
	EXTRN	INKDIR,INKST,OPTDIR,GHODIR,XDIF,YDIF,XLINE,YLINE,EYES,TMP
	EXTRN	XLINE,YLINE,BESTX,BESTY,OLDIR,INKPX,CLYX,CLYY,RELATE
	EXTRN	INKPAT,INKX,INKY,LASTI,LAST2I,PGHOMX,FINDP,RSTPAT,PGHOMY
	EXTRN	TMPX,TMPY,XMDFY,YMDFY,OFFPAT,DEADFL,PWRTIMR,GHOFLG,EYEFLG
	EXTRN	INKHIT,HTOD,DISCOR,UPFL,DNFL,LTFL,RTFL,ABS,INKTCB,PACX
	EXTRN	MOVGHO,FINDIR,CHADIR,CHA1DIR,FINREV,NOREV,REVFL,SMOVGH
	EXTRN	HMOVGHO,CHDIR,GHOHIT,STFL,HOMEDIR,SGVAL,SGPAT,LIDIR,INKDEL
	EXTRN	FACEME,IREV1F,PACX,PACY,CKQUAD,QUADAD,CKREL
	.LIST	
INKMOV::
	ASIGN8. (LIDIR),(INKDIR)
	LD	A,(INKST)		;GET INKY'S STATUS
	CP	SICK			;IS HE SICK?
	JP	Z,SICK_INKY		;YES...THEN DO THE SICK ACT
	CP	EATEN			;HAS HE BEEN EATEN WHILE SICK
	JP	Z,HOME_INKY		;YES...DO THE GO HOME ACT
	IF8.	(INKST),.EQ.,HOME	;IF WE ARE HOME THEN BOUNCE UP AND DOWN
	  IF8. (INKY),.EQ.,4CH
	    ASIGN8. (INKDIR),10H
	  ENDIF.
	  IF8. (INKY),.EQ.,54H
	    ASIGN8. (INKDIR),30H
	  ENDIF.
	  CALL IMOVGH	
	  ADD8. (INKTIMR),1
	  IF8. (INKTIMR),.EQ.,90	;IF IT'S TIME TO LEAVE THEN GO TOWARDS
	    ASIGN8. (INKST),GETOUT	;THE DOOR
	  ENDIF.
	  RET
	ELSE.
          IF8. (INKST),.EQ.,GETOUT		;IF WE ARE SUPPOSED TO GET OUT
	    PCALL HOMEDIR,(INKX),(INKY),0
	    IF8. (INKX),.NE.,7AH		;IF NOT LINED UP WITH THE
	      IF8.(LTFL),.EQ.,TRUE		;DOOR THEN LINE US UP
	        ASIGN8. (INKDIR),20H
	      ELSE.
	        ASIGN8. (INKDIR),00H
	      ENDIF.
	    ELSE.
	      IF8. (INKY),.NE.,3CH		;IF NOT OUT THE DOOR THEN
	        IF8. (DNFL),.EQ.,TRUE		;GO TOWARD THE DOOR
		  ASIGN8. (INKDIR),10H
	        ELSE.
	          ASIGN8. (INKDIR),30H
	        ENDIF.
	      ELSE.
		ASIGN8. (INKDIR),20H
		IF8. (ISICK),.EQ.,TRUE
		  ASIGN8. (INKST),SICK
		ELSE.
		  IF8. (IQFLG),.EQ.,FALSE
		    ASIGN8. (INKST),OK
		  ELSE.
	    	    ASIGN8. (INKST),GOQUAD
		    ASIGN8. (IQFLG),00H
		  ENDIF.
		ENDIF.
	      ENDIF.
	    ENDIF.
  	    CALL IMOVGH
	    RET
	  ELSE.
	    IF8. (INKST),.EQ.,GETIN		;IF ARRIVED AT ENTRANCE TO
	      ASIGN8. (INKDIR),10H		;HIDEOUT THEN GO IN
	      PCALL HMOVGHO,INKX,(INKDIR),INKYE
	      IF8. (INKY),.EQ.,4CH
		CALL INKRST
		ASIGN8. (INKTIMR),00H
	      ENDIF.
	      RET
	    ENDIF.
	  ENDIF.
	ENDIF.
	
	;
	; This is the movement routine for INKY, the blue ghost.  His
	; strategy is to seek out and get you wherever you are.  If you
	; are behind a wall, he will find the nearest exit to get you,
	; AS LONG AS IT IS NOT IN THE REVERSE OF THE CURRENT DIRECTION
	; THAT HE IS TRAVELLING IN.  He also has priority over the other
	; ghosts as far as who gets to chase the pacman.
	IF8.	(INKST),.EQ.,GOQUAD
	  PCALL	HOMEDIR,(INKX),(INKY),INKYE
	  PCALL	CKQUAD,INKX,QUADAD+6
	  JR	NZ,QUCKIN
	  ASIGN8. (INKST),OK
QUCKIN:	  ASIGN8. (GHODIR),(OPTDIR)
	  LD	A,(IREV1F)
	  AND	A
	  ASIGN8. (IREV1F),00H
	  JP	Z,IREVCK
	  PCALL	GHOHIT,(INKX),(INKY)
	  LD	A,(CHDIR)
	  AND	A
	  JP	NZ,INKCD
	  JP	MOVINK
	ELSE.
	  PCALL	FINDIR,(INKX),(INKY),0	;FIND THE OPTIMUM DIRECTION
	ENDIF.
IREVCK:	PCALL	FACEME,INKX,OPTDIR	;SEE IF THE GHOST IS FACING PACMAN
	JP	NZ,INKCD		;YES..TRY ANOTHER
	ASIGN8.	(GHODIR),(OPTDIR)	;PUT OPTDIR INTO GHODIR
	PCALL	GHOHIT,(INKX),(INKY)	;THERE A WALL THERE?
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,CHKRV		;GO ANOTHER DIRECTION THEN
	LD	A,(XLINE)		
	CP	TRUE
	JP	Z,CHKINK
	LD	A,(YLINE)
	CP	TRUE
	JP	Z,CHKINK
	JP	INKCD
CHKRV:	PCALL	NOREV,(OPTDIR),(INKDIR)	;SEE IF WE HAVE TO REVERSE DIRECTION
	ASIGN8.	(GHODIR),(INKDIR)
	LD	A,(REVFL)		;GET THE REVERSE FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,MOVINK		;NO....THEN MOVE HIM
INKCD::	CALL	CHADIR		;YES...CHANGE IT TO A DIFFERENT DIRECTION
	PCALL	NOREV,(INKDIR),(OPTDIR)
	LD	A,(REVFL)
	CP	TRUE
	JP	Z,IANOTH
	ASIGN8.	(GHODIR),(OPTDIR)	;GET THE OPTIMUM DIRECTION
	PCALL	GHOHIT,(INKX),(INKY)	;CHECK FOR WALLS IN THAT DIRECTION
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,MOVINK		;NO....THEN MOVE HIM
	ASIGN8.	(GHODIR),(INKDIR)
	LD	(OPTDIR),A
	PCALL	GHOHIT,(INKX),(INKY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,MOVINK
IANOTH:	PCALL	CHA1DIR,(INKX),(INKY),(INKDIR)	;TRY ANOTHER DIRECTION
	LD	A,(STFL)		;GET THE STOP FLAG
	CP	TRUE			;IS IT SET?
	JP	Z,NOINK			;YES...DON'T MOVE HIM
MOVINK::
	ASIGN8. (INKDIR),(OPTDIR)	;MAKE THE OPTIMUM INKY'S DIRECTION
NOINK::	CALL	IMOVGH			;GO TO IT INKY!
	PCALL	ABS,(PACX),(INKX)
	CP	08H
	JR	C,I1CNT
	CP	03H	
	JR	C,I2CNT
	RET
I1CNT:	PCALL	ABS,(PACY),(INKY)
	CP	03H
	RET	NC
I3CNT:	ASIGN8.	(DEADFL),TRUE
	RET
I2CNT:	PCALL	ABS,(PACY),(INKY)
	CP	08H
	RET	NC
	JR	I3CNT
CHKINK::ASIGN8.	(GHODIR),(INKDIR)
	PCALL	GHOHIT,(INKX),(INKY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	Z,INKCD
	JP	NOINK
RSTINK::
        PCALL RSTPAT,INKPX,(INKDIR)
	ASIGN8. (LASTI),0FFH
	RET
IMOVGH::
	IF8. (ISICK),.EQ.,FALSE
	  CALL	INKDEL
	  PCALL	SPCOLR,7,WHITE		;MAKE SURE THE COLORS ARE SET
	  PCALL	SPCOLR,3,CYAN
	  IF8. (INKDIR),.NE.,(LIDIR)
	    SPEED INKTCB,-1
	    IF8. (LASTI),.NE.,0FFH
	    CALL RSTINK
	    ENDIF.
	  ENDIF.
	  IF8.	(INKY),.EQ.,4CH
	    IF8. (INKX),.GE.,0CCH
	      SPEED INKTCB,-2
	    ENDIF.
	    IF8. (INKX),.LE.,26H
	      SPEED INKTCB,-2
	    ENDIF.
	  ENDIF.
;	  IF8. (INKX),.GT.,0E0H
;	    JP	ISMOVE
;	  ENDIF.
;	  IF8.	(INKX),.LT.,010H
;	    JP	ISMOVE
;	  ENDIF.
	   IF8.	(INKST),.EQ.,GETOUT
	     JP ISMOVE
	   ENDIF.
;	   PCALL RELATE,INKX,CLYX
;	   JP Z,ISMOVE
;	   PCALL RELATE,INKX,PACX
;	   JP Z,ISMOVE
	   LD	A,(INKDIR)
	   AND	A
	   JP	Z,IXLRIGHT
	   CP	20H
	   JP	Z,IXLRIGHT
	   JP	IYLRIGHT
IXLRIGHT:: PCALL PGHOMX,INKX,(INKDIR),INKYE
	   RET
IYLRIGHT:: PCALL PGHOMY,INKX,(INKDIR),INKYE
	   RET
ISMOVE::    IF8. (LASTI),.NE.,0FFH
	      CALL RSTINK      
	    ENDIF.
	    PCALL MOVGHO,INKX,(INKDIR),INKYE
	ELSE.
	  IF8.	(LASTI),.NE.,0FFH
	    CALL RSTINK
	  ENDIF.	  
	  PCALL SMOVGH,INKX,(INKDIR),INKYE
	ENDIF.
	RET
SICK_INKY::
	;This is the routine that will be executed for blinky while sick
	;
	PCALL	FINDIR,(INKX),(INKY),0
	PCALL	CHA1DIR,(INKX),(INKY),(INKDIR)
	LD	A,(OPTDIR)
	LD	C,A
	LD	A,(INKDIR)
	CP	C
	JP	Z,SNOINK
	;
	ASIGN8.	(OLDIR),(INKDIR)
	PCALL	FINDIR,(INKX),(INKY),0	;FIND THE OPTIMUM DIRECTION
	PCALL	FINREV,(OPTDIR)
	LD	(OPTDIR),A
	LD	(GHODIR),A
	PCALL	GHOHIT,(INKX),(INKY)	;THERE A WALL THERE?
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,SCHKRV		;GO ANOTHER DIRECTION THEN
	LD	A,(XLINE)		
	CP	TRUE
	JP	Z,SCHKINK
	LD	A,(YLINE)
	CP	TRUE
	JP	Z,SCHKINK
	JP	SINKCD
SCHKRV:	PCALL	NOREV,(OPTDIR),(INKDIR)	;SEE IF WE HAVE TO REVERSE DIRECTION
	ASIGN8.	(GHODIR),(INKDIR)
	LD	A,(REVFL)		;GET THE REVERSE FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,SMVINK		;NO....THEN MOVE HIM
SINKCD::CALL	Z,CHADIR		;YES...CHANGE IT TO A DIFFERENT DIRECTION
	PCALL	FINREV,(OPTDIR)
	LD	(OPTDIR),A
	PCALL	NOREV,(OLDIR),(OPTDIR)
	LD	A,(REVFL)
	CP	TRUE
	JP	Z,SNEWDR
	ASIGN8.	(GHODIR),(OPTDIR)	;GET THE OPTIMUM DIRECTION
	PCALL	GHOHIT,(INKX),(INKY)	;CHECK FOR WALLS IN THAT DIRECTION
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,SMVINK		;NO....THEN MOVE HIM
	ASIGN8.	(GHODIR),(INKDIR)
	LD	(OPTDIR),A
	PCALL	GHOHIT,(INKX),(INKY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,SMVINK
SNEWDR:
	ASIGN8.	(GHODIR),(OLDIR)	;IF HE CAN'T MOVE IN THE OPTIMUM
	PCALL	GHOHIT,(INKX),(INKY)	;DIRECTIONS, MOVE HIM FORWARD TO HIS
	LD	A,(CHDIR)		;DEATH
	CP	TRUE
	JP	Z,SDOAD
	ASIGN8.	(INKDIR),(OLDIR)
	JP	SNOINK
SDOAD:	PCALL	CHA1DIR,(INKX),(INKY),(OLDIR)	;TRY ANOTHER DIRECTION
SMVINK::ASIGN8.	(INKDIR),(OPTDIR)
SNOINK::SPEED	INKTCB,-2
	PCALL	SMOVGH,INKX,(INKDIR),INKYE	;MOVE THE SICK INKY IN THAT DIRECTION
	PCALL	CKREL,PACX,INKX,07H
	RET	NZ
	    ASIGN8. (INKHIT),TRUE
	    ASIGN8. (INKST),EATEN
	    ASIGN8. (ISICK),FALSE
     	    ASIGN8. (GHOFLG),TRUE
	    ADD8.   (SGPAT),04H
	    PCALL   SPNAME,3,(SGPAT)
	    PCALL   SPCOLR,3,WHITE
	    PCALL   SPCOLR,7,WHITE	
	    LD	    A,(SGVAL)
	    SLA	    A
	    LD	    (SGVAL),A
	    IF8.    (SGVAL),.EQ.,0
	      ASIGN16. (SGVAL),160H
	    ENDIF.
	    PCALL   HTOD,(SGVAL)
	    CALL    DISCOR
	RET
SCHKINK::ASIGN8. (GHODIR),(INKDIR)
	PCALL	GHOHIT,(INKX),(INKY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	Z,SINKCD
	JP	SNOINK
HOME_INKY::
	ASIGN8.	(EYEFLG),TRUE
	ASIGN8.	(INKTCB+3),1
INKHM::	PCALL	HOMEDIR,(INKX),(INKY),0	;FIND THE OPTIMUM DIRECTION
	ASIGN8.	(GHODIR),(OPTDIR)	;PUT OPTDIR INTO GHODIR
	PCALL	GHOHIT,(INKX),(INKY)	;THERE A WALL THERE?
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,HHKRV		;GO ANOTHER DIRECTION THEN
	LD	A,(XLINE)		
	CP	TRUE
	JP	Z,HHKINK
	LD	A,(YLINE)
	CP	TRUE
	JP	Z,HHKINK
	JP	HINKCD
HHKRV:	PCALL	NOREV,(OPTDIR),(INKDIR)	;SEE IF WE HAVE TO REVERSE DIRECTION
	ASIGN8.	(GHODIR),(INKDIR)
	LD	A,(REVFL)		;GET THE REVERSE FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,HMOVINK		;NO....THEN MOVE HIM
HINKCD::CALL	Z,CHADIR		;YES...CHANGE IT TO A DIFFERENT DIRECTION
	ASIGN8.	(GHODIR),(OPTDIR)	;GET THE OPTIMUM DIRECTION
	PCALL	GHOHIT,(INKX),(INKY)	;CHECK FOR WALLS IN THAT DIRECTION
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,HMOVINK		;NO....THEN MOVE HIM
	ASIGN8.	(GHODIR),(INKDIR)
	LD	(OPTDIR),A
	PCALL	GHOHIT,(INKX),(INKY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,HMOVINK
	PCALL	CHA1DIR,(INKX),(INKY),(INKDIR)	;TRY ANOTHER DIRECTION
	LD	A,(STFL)		;GET THE STOP FLAG
	CP	TRUE			;IS IT SET?
	JP	Z,HNOINK			;YES...DON'T MOVE HIM
HMOVINK::
	ASIGN8. (INKDIR),(OPTDIR)	;MAKE THE OPTIMUM INKY'S DIRECTION
HNOINK::PCALL	HMOVGHO,INKX,(INKDIR),INKYE	;GO TO IT INKY!
	IF8.	(INKX),.EQ.,78H
	  IF8.    (INKY),.EQ.,3CH
	    ASIGN8. (INKST),GETIN
	  ENDIF.
	ENDIF.
	RET
INKRST::ASIGN8.	(INKST),HOME
	ASIGN8.	(INKHIT),FALSE
	ASIGN8.	(DEADFL),FALSE
	ASIGN8.	(INKX),7CH
	ASIGN8.	(INKY),4CH
	ASIGN8. (INKDIR),10H
	PCALL	SPMOVE,3,(INKY),(INKX)
	PCALL	SPMOVE,7,(INKY),(INKX)
	PCALL	SPNAME,3,0C0H
	PCALL	SPNAME,7,40H
	PCALL	SPCOLR,7,WHITE
	PCALL	SPCOLR,3,CYAN
	RET
HHKINK::ASIGN8.	(GHODIR),(INKDIR)
	PCALL	GHOHIT,(INKX),(INKY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,HINKCD
	JP	NOINK
END

