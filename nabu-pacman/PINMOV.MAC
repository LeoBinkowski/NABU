	;*****************************************************************
;*                                                               *
;*    FILE NAME:         PINMOV.MAC                              *
;*                                                               *
;*    MODULE PART NO.: 73-90034420 NAME:  PAC MAN 		 *
;*                                                               *
;*    FILE DESCRIPTION:                                          *  
;*         This file contains the personality logic for          *
;*	PINKY (the pink ghost)                                   *
;*                                                               *
;*                                                               *
;*                                                               *
;*                                                               *
;*                                                               *
;*    PROGRAMMER: L. Binkowski                                   *
;*                                                               *
;*    ENVIRONMENT- Development Machine: NABU 1100, MCP 1.8       *
;*                                                               *
;*               - Assembler: MACRO-80                           *
;*                                                               *
;*                                                               *
;*****************************************************************
;*                                                               *
;*    REVISION HISTORY: (current first)                          *
;*                                                               *
;*    Rev.    Date       Created By         Revision Details     *
;*    ---  ----------  ---------------  -----------------------  *
;*    02   19 OCT 83	L. Binkowski	changed relate code	 *
;*                                                               *
;*    01   04 OCT 83   L. Binkowski     Initial release to spec  *
;*                                                               *
;*****************************************************************
 
	.Z80
	.PRINTX	/PINKY'S MOVEMENT ROUTINE....OCTOBER 19, 1983/
	.RADIX	10
	.XLIST
	INCLUDE PACMAN.EQU
	INCLUDE COMMON.MRO
	INCLUDE	ARITH.MRO
	INCLUDE STRUCT.MRO
	INCLUDE	LINKTAB.EXA
	INCLUDE	SUPRTASK.MRO
PINTIMR::DS	1	;TIMER FOR WHEN PINKY SHOULD COME OUT
PSICK::	 DB	FALSE	;PINKY SICK FLAG
PINTGL:	DB	00H
PQFLG::	DB	00H
	EXTRN	PINDIR,PINST,OPTDIR,GHODIR,XDIF,YDIF,XLINE,YLINE,EYES,TMP
	EXTRN	XLINE,YLINE,BESTX,BESTY,OLDIR,PINPX,PINTCB,RELATE
	EXTRN	PINPAT,PINX,PINY,LASTP,LAST2P,PGHOMX,FINDP,RSTPAT,PGHOMY
	EXTRN	TMPX,TMPY,XMDFY,YMDFY,OFFPAT,DEADFL,PWRTIMR,PACX,EYEFLG
	EXTRN	PINHIT,HTOD,DISCOR,UPFL,DNFL,LTFL,RTFL,ABS,INKX,INKY,CLYX,CLYY
	EXTRN	MOVGHO,FINDIR,CHADIR,CHA1DIR,FINREV,NOREV,REVFL,SMOVGH
	EXTRN	HMOVGHO,CHDIR,GHOHIT,STFL,HOMEDIR,SGVAL,SGPAT,LPDIR,GHOFLG,PINDEL
	EXTRN	PREV1F,PACX,PACY,CKQUAD,QUADAD,CKREL
	.LIST	
PINMOV::
	ASIGN8. (LPDIR),(PINDIR)
	LD	A,(PINST)		;GET PINKY'S STATUS
	CP	SICK			;IS HE SICK?
	JP	Z,SICK_PINKY		;YES...THEN DO THE SICK ACT
	CP	EATEN			;HAS HE BEEN EATEN WHILE SICK
	JP	Z,HOME_PINKY		;YES...DO THE GO HOME ACT
	IF8.	(PINST),.EQ.,HOME	;IF WE ARE HOME THEN BOUNCE UP AND DOWN
	  IF8. (PINY),.EQ.,4CH
	    ASIGN8. (PINDIR),10H
	  ENDIF.
	  IF8. (PINY),.EQ.,54H
	    ASIGN8. (PINDIR),30H
	  ENDIF.
	  CALL PMOVGH	
	  ADD8. (PINTIMR),1
	  IF8. (PINTIMR),.EQ.,120	;IF IT'S TIME TO LEAVE THEN GO TOWARDS
	    ASIGN8. (PINST),GETOUT	;THE DOOR
	  ENDIF.
	  RET
	ELSE.
          IF8. (PINST),.EQ.,GETOUT		;IF WE ARE SUPPOSED TO GET OUT
	    PCALL HOMEDIR,(PINX),(PINY),0
	    IF8. (PINX),.NE.,7CH		;IF NOT LINED UP WITH THE
	      IF8.(LTFL),.EQ.,TRUE		;DOOR THEN LINE US UP
	        ASIGN8. (PINDIR),20H
	      ELSE.
	        ASIGN8. (PINDIR),00H
	      ENDIF.
	    ELSE.
	      IF8. (PINY),.NE.,3CH		;IF NOT OUT THE DOOR THEN
	        IF8. (DNFL),.EQ.,TRUE		;GO TOWARD THE DOOR
		  ASIGN8. (PINDIR),10H
	        ELSE.
	          ASIGN8. (PINDIR),30H
	        ENDIF.
	      ELSE.
		ASIGN8. (PINDIR),20H
		IF8. (PSICK),.EQ.,TRUE
		  ASIGN8. (PINST),SICK
		ELSE.
	        IF8. (PQFLG),.EQ.,FALSE
		  ASIGN8. (PINST),OK
		ELSE.
		  ASIGN8. (PINST),GOQUAD
		  ASIGN8. (PQFLG),00H
		ENDIF.
		ENDIF.
	      ENDIF.
	    ENDIF.
  	    CALL PMOVGH
	    RET
	  ELSE.
	    IF8. (PINST),.EQ.,GETIN		;IF ARRIVED AT ENTRANCE TO
	      ASIGN8. (PINDIR),10H		;HIDEOUT THEN GO IN
	      PCALL HMOVGH,PINX,(PINDIR),PINKY
	      IF8. (PINY),.EQ.,4CH
		CALL PINRST
		ASIGN8. (PINTIMR),00H
	      ENDIF.
	      RET
	    ENDIF.
	  ENDIF.
	ENDIF.
	;
	; This is the movement routine for PINKY, the pink ghost.  His
	; strategy is to seek out and get you wherever you are.  Only the
	; first priority direction is different from BLINKY so that he will
	; cover the direction that BLINKY doesn't follow.
	;
	IF8.	(PINST),.EQ.,GOQUAD
	  PCALL	HOMEDIR,(PINX),(PINY),PINKY
	  PCALL	CKQUAD,PINX,QUADAD+4
	  JR	NZ,QUCKPI
	  ASIGN8. (PINST),OK
QUCKPI:	  ASIGN8. (GHODIR),(OPTDIR)
	  LD	A,(PREV1F)
	  AND	A
	  ASIGN8. (PREV1F),00H
	  JP	Z,PREVCK
	  PCALL	GHOHIT,(PINX),(PINY)
	  LD	A,(CHDIR)
	  AND	A
	  JP	NZ,PINCD
	  JP	MOVPIN
	ELSE.
	  PCALL	FINDIR,(PINX),(PINY),0
	  JP	PINCD
	ENDIF.
PREVCK:	ASIGN8.	(GHODIR),(OPTDIR)	;PUT OPTDIR INTO GHODIR
	PCALL	GHOHIT,(PINX),(PINY)	;THERE A WALL THERE?
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,CHKRV		;GO ANOTHER DIRECTION THEN
	LD	A,(XLINE)		
	CP	TRUE
	JP	Z,CHKPIN
	LD	A,(YLINE)
	CP	TRUE
	JP	Z,CHKPIN
	JP	PINCD
CHKRV:	PCALL	NOREV,(OPTDIR),(PINDIR)	;SEE IF WE HAVE TO REVERSE DIRECTION
	ASIGN8.	(GHODIR),(PINDIR)
	LD	A,(REVFL)		;GET THE REVERSE FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,MOVPIN		;NO....THEN MOVE HIM
PINCD::	CALL	CHADIR		;YES...CHANGE IT TO A DIFFERENT DIRECTION
	PCALL	NOREV,(PINDIR),(OPTDIR)
	LD	A,(REVFL)
	CP	TRUE
	JP	Z,PANOTH
	ASIGN8.	(GHODIR),(OPTDIR)	;GET THE OPTIMUM DIRECTION
	PCALL	GHOHIT,(PINX),(PINY)	;CHECK FOR WALLS IN THAT DIRECTION
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,MOVPIN		;NO....THEN MOVE HIM
	ASIGN8.	(GHODIR),(PINDIR)
	LD	(OPTDIR),A
	PCALL	GHOHIT,(PINX),(PINY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,MOVPIN
PANOTH:	PCALL	CHA1DIR,(PINX),(PINY),(PINDIR)	;TRY ANOTHER DIRECTION
	LD	A,(STFL)		;GET THE STOP FLAG
	CP	TRUE			;IS IT SET?
	JP	Z,NOPIN			;YES...DON'T MOVE HIM
MOVPIN::
	ASIGN8. (PINDIR),(OPTDIR)	;MAKE THE OPTIMUM PINKY'S DIRECTION
NOPIN::	CALL	PMOVGH			;GO TO IT PINKY!
	PCALL	ABS,(PACX),(PINX)
	CP	08H
	JR	C,P1CNT
	CP	03H
	JR	C,P2CNT
	RET
P1CNT:	PCALL	ABS,(PACY),(PINY)
	CP	03H
	RET	NC
P3CNT:	ASIGN8.	(DEADFL),TRUE
	RET
P2CNT:	PCALL	ABS,(PACY),(PINY)
	CP	08H
	RET	NC
	JR	P3CNT
CHKPIN::ASIGN8.	(GHODIR),(PINDIR)
	PCALL	GHOHIT,(PINX),(PINY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	Z,PINCD
	JP	NOPIN
RSTPIN::
        PCALL RSTPAT,PINPX,(PINDIR)
	ASIGN8.	(LASTP),0FFH
	RET
PMOVGH::
	IF8. (PSICK),.EQ.,FALSE
	CALL	PINDEL
	PCALL	SPCOLR,6,WHITE		;MAKE SURE THE COLORS ARE SET
	PCALL	SPCOLR,2,LRED
	IF8. (PINDIR),.NE.,(LPDIR)
	  SPEED PINTCB,-1
	  IF8. (LASTP),.NE.,0FFH
	  CALL RSTPIN
	  ENDIF.
	ENDIF.
	IF8. (PINY),.EQ.,4CH
	  IF8. (PINX),.GE.,0CCH
	    SPEED PINTCB,-2
	  ENDIF.
	  IF8. (PINX),.LE.,26H
	    SPEED PINTCB,-2
	  ENDIF.
;	ELSE.
;	  LD	A,(PINTGL)
;	  XOR	1
;	  LD	(PINTGL),A
;	  JR	Z,FLIB
;	  SPEED	PINTCB,1
FLIB:	ENDIF.
;	  IF8. (PINX),.GT.,0E0H
;	    JP	PSMOVE
;	  ENDIF.
;	  IF8.	(PINX),.LT.,010H
;	    JP	PSMOVE
;	  ENDIF.
	   IF8.	(PINST),.EQ.,GETOUT
	     JP PSMOVE
	   ENDIF.
;	   PCALL RELATE,PINX,INKX
;	   JP Z,PSMOVE
;	   PCALL RELATE,PINX,CLYX
;	   JP Z,PSMOVE
;	   PCALL RELATE,PINX,PACX
;	   JP Z,PSMOVE
	   LD	A,(PINDIR)
	   AND	A
	   JP	Z,PXLRIGHT
	   CP	20H
	   JP	Z,PXLRIGHT
	   JP	PYLRIGHT
PXLRIGHT:: PCALL PGHOMX,PINX,(PINDIR),PINKY
	   RET
PYLRIGHT:: PCALL PGHOMY,PINX,(PINDIR),PINKY
	   RET
PSMOVE::    IF8. (LASTP),.NE.,0FFH
	      CALL RSTPIN      
	    ENDIF.
	    PCALL MOVGHO,PINX,(PINDIR),PINKY
	ELSE.
	  IF8. (LASTP),.NE.,0FFH
	    CALL RSTPIN
	  ENDIF.
	  PCALL SMOVGH,PINX,(PINDIR),PINKY
	ENDIF.
	RET
SICK_PINKY::
	;This is the routine that will be executed for blinky while sick
	;
	PCALL	FINDIR,(PINX),(PINY),0
	PCALL	CHA1DIR,(PINX),(PINY),(PINDIR)
	LD	A,(OPTDIR)
	LD	C,A
	LD	A,(PINDIR)
	CP	C
	JP	Z,SNOPIN
	;
	ASIGN8.	(OLDIR),(PINDIR)
	PCALL	FINDIR,(PINX),(PINY),0	;FIND THE OPTIMUM DIRECTION
	PCALL	FINREV,(OPTDIR)
	LD	(OPTDIR),A
	LD	(GHODIR),A
	PCALL	GHOHIT,(PINX),(PINY)	;THERE A WALL THERE?
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,SCHKRV		;GO ANOTHER DIRECTION THEN
	LD	A,(XLINE)		
	CP	TRUE
	JP	Z,SCHKPIN
	LD	A,(YLINE)
	CP	TRUE
	JP	Z,SCHKPIN
	JP	SPINCD
SCHKRV:	PCALL	NOREV,(OPTDIR),(PINDIR)	;SEE IF WE HAVE TO REVERSE DIRECTION
	ASIGN8.	(GHODIR),(PINDIR)
	LD	A,(REVFL)		;GET THE REVERSE FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,SMVPIN		;NO....THEN MOVE HIM
SPINCD::CALL	Z,CHADIR		;YES...CHANGE IT TO A DIFFERENT DIRECTION
	PCALL	FINREV,(OPTDIR)
	LD	(OPTDIR),A
	PCALL	NOREV,(OLDIR),(OPTDIR)
	LD	A,(REVFL)
	CP	TRUE
	JP	Z,SNEWDR
	ASIGN8.	(GHODIR),(OPTDIR)	;GET THE OPTIMUM DIRECTION
	PCALL	GHOHIT,(PINX),(PINY)	;CHECK FOR WALLS IN THAT DIRECTION
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,SMVPIN		;NO....THEN MOVE HIM
	ASIGN8.	(GHODIR),(PINDIR)
	LD	(OPTDIR),A
	PCALL	GHOHIT,(PINX),(PINY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,SMVPIN
SNEWDR:
	ASIGN8.	(GHODIR),(OLDIR)	;IF HE CAN'T MOVE IN THE OPTIMUM
	PCALL	GHOHIT,(PINX),(PINY)	;DIRECTIONS, MOVE HIM FORWARD TO HIS
	LD	A,(CHDIR)		;DEATH
	CP	TRUE
	JP	Z,SDOAD
	ASIGN8.	(PINDIR),(OLDIR)
	JP	SNOPIN
SDOAD:	PCALL	CHA1DIR,(PINX),(PINY),(OLDIR)	;TRY ANOTHER DIRECTION
SMVPIN::ASIGN8.	(PINDIR),(OPTDIR)
SNOPIN::SPEED	PINTCB,-2
	PCALL	SMOVGH,PINX,(PINDIR),PINKY	;MOVE THE SICK PINKY IN THAT DIRECTION
	PCALL	CKREL,PINX,PACX,07H
	RET	NZ
	    ASIGN8. (PINHIT),TRUE
	    ASIGN8. (PINST),EATEN
	    ASIGN8. (GHOFLG),TRUE
	    ASIGN8. (PSICK),FALSE
	    ADD8.   (SGPAT),04H
	    PCALL   SPNAME,2,(SGPAT)
	    PCALL   SPCOLR,2,WHITE
	    PCALL   SPCOLR,6,WHITE	
	    LD	    A,(SGVAL)
	    SLA	    A
	    LD	    (SGVAL),A
	    IF8.    (SGVAL),.EQ.,0
	      ASIGN16. (SGVAL),160H
	    ENDIF.
	    PCALL   HTOD,(SGVAL)
	    CALL    DISCOR
	RET
SCHKPIN::ASIGN8. (GHODIR),(PINDIR)
	PCALL	GHOHIT,(PINX),(PINY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	Z,SPINCD
	JP	SNOPIN
HOME_PINKY::
	ASIGN8.	(EYEFLG),TRUE
	ASIGN8.	(PINTCB+3),1
PINHM::	PCALL	HOMEDIR,(PINX),(PINY),0	;FIND THE OPTIMUM DIRECTION
	ASIGN8.	(GHODIR),(OPTDIR)	;PUT OPTDIR INTO GHODIR
	PCALL	GHOHIT,(PINX),(PINY)	;THERE A WALL THERE?
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,HHKRV		;GO ANOTHER DIRECTION THEN
	LD	A,(XLINE)		
	CP	TRUE
	JP	Z,HHKPIN
	LD	A,(YLINE)
	CP	TRUE
	JP	Z,HHKPIN
	JP	HPINCD
HHKRV:	PCALL	NOREV,(OPTDIR),(PINDIR)	;SEE IF WE HAVE TO REVERSE DIRECTION
	ASIGN8.	(GHODIR),(PINDIR)
	LD	A,(REVFL)		;GET THE REVERSE FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,HMOVPIN		;NO....THEN MOVE HIM
HPINCD::CALL	Z,CHADIR		;YES...CHANGE IT TO A DIFFERENT DIRECTION
	ASIGN8.	(GHODIR),(OPTDIR)	;GET THE OPTIMUM DIRECTION
	PCALL	GHOHIT,(PINX),(PINY)	;CHECK FOR WALLS IN THAT DIRECTION
	LD	A,(CHDIR)		;GET THE CHANGE DIRECTION FLAG
	CP	TRUE			;IS IT SET?
	JP	NZ,HMOVPIN		;NO....THEN MOVE HIM
	ASIGN8.	(GHODIR),(PINDIR)
	LD	(OPTDIR),A
	PCALL	GHOHIT,(PINX),(PINY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,HMOVPIN
	PCALL	CHA1DIR,(PINX),(PINY),(PINDIR)	;TRY ANOTHER DIRECTION
	LD	A,(STFL)		;GET THE STOP FLAG
	CP	TRUE			;IS IT SET?
	JP	Z,HNOPIN			;YES...DON'T MOVE HIM
HMOVPIN::
	ASIGN8. (PINDIR),(OPTDIR)	;MAKE THE OPTIMUM PINKY'S DIRECTION
HNOPIN::PCALL	HMOVGHO,PINX,(PINDIR),PINKY	;GO TO IT PINKY!
	IF8.	(PINX),.EQ.,78H
	  IF8.    (PINY),.EQ.,3CH
	    ASIGN8. (PINST),GETIN
	  ENDIF.
	ENDIF.
	RET
PINRST::ASIGN8.	(PINST),HOME
	ASIGN8.	(PINHIT),FALSE
	ASIGN8.	(DEADFL),FALSE
	ASIGN8.	(PINX),5CH
	ASIGN8.	(PINY),4CH
	ASIGN8. (PINDIR),10H
	PCALL	SPMOVE,2,(PINY),(PINX)
	PCALL	SPMOVE,6,(PINY),(PINX)
	PCALL	SPNAME,2,0C0H
	PCALL	SPNAME,6,40H
	PCALL	SPCOLR,6,WHITE
	PCALL	SPCOLR,2,LRED
	RET
HHKPIN::ASIGN8.	(GHODIR),(PINDIR)
	PCALL	GHOHIT,(PINX),(PINY)
	LD	A,(CHDIR)
	CP	TRUE
	JP	NZ,HPINCD
	JP	NOPIN
END
