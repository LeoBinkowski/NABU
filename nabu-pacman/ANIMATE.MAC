;*****************************************************************
;*                                                               *
;*    FILE NAME:        ANIMATE.MAC                              *
;*                                                               *
;*    MODULE PART NO.: 73-90034420 NAME:  PAC MAN 		 *
;*                                                               *
;*    FILE DESCRIPTION:                                          *  
;*                                                               *
;*      this module contains the opening animation for PACMAN    *
;*                                                               *
;*                                                               *
;*                                                               *
;*                                                               *
;*                                                               *
;*    PROGRAMMER: L. Binkowski                                   *
;*                                                               *
;*    ENVIRONMENT- Development Machine: NABU 1100, MCP 1.8       *
;*                                                               *
;*               - Assembler: MACRO-80                           *
;*                                                               *
;*                                                               *
;*****************************************************************
;*                                                               *
;*    REVISION HISTORY: (current first)                          *
;*                                                               *
;*    Rev.    Date       Created By         Revision Details     *
;*    ---  ----------  ---------------  -----------------------  *
;*    01   14 DEC 83	L. Binkowski	NAMCO copyright change	 *
;*								 *
;*    01   19 OCT 83	L. Binkowski	Change to logo (PAC-MAN) *
;*                                                               *
;*    01   04 OCT 83    L. Binkowski    Initial release to spec  *
;*                                                               *
;*****************************************************************
 		.Z80
 		.RADIX 10
 		CSEG
		INCLUDE	COMMON.MRO
		INCLUDE	STRUCT.MRO
		INCLUDE	LINKTAB.EXA
		INCLUDE	PACMAN.EQU
		INCLUDE CLKAT.MRO
;
	EXTRN	SETVDP,WAIT,TICK,TICTCB,TICKFL,TMP16,PACLOGO,SPACSP
	EXTRN	PACX,PACY,PACDIR,BLIX,BLIY,BLIDIR,PINX,PINY,PINDIR
	EXTRN	INKX,INKY,INKDIR,CLYX,CLYY,CLYDIR,PLAYER,CKREL,PWRTIMR
	EXTRN   MOVGHO,PACMOV,ENGLISH,SGVAL,PACMTH,SMOVGHO,RACK
	EXTRN	BLISCK,PINSCK,INKSCK,CLYSCK,PACST
	
;
;	THIS MODULE IS THE ANIMATION FOR THE GAME
;
;	STACK HAS BEEN SET TO LOCATION 006H
;	BASE ADDRESSES HAVE BEEN SET
;	VRAM HAS BEEN FILLED WITH 00H
;	THE SCREEN HAS BEEN FILLED WITH 20H
;	SET TO GRAPHICS MODE 1
;	YOU MUST - LOAD YOUR OWN PATTERN TABLES,COLOR TABLE
;		 - SET BACKGROUND COLOR
;		 - SET UP ALL TASKS AND VARIABLES TO BE USED ONLY
;		 - ONCE IN THIS ROUTINE
;		 - CHECK FOR GO KEY AND RETURN TO MAIN LINE
;
;	FOR MORE INFO SEE SETUP.MAC
;
MSGA:	DB	08H,00H,10H,00H,02H,04H,06H,08H,0AH,1CH,1EH,0CH,0EH,04H
		DB	06H,10H,12H,14H,16H
MSGB:	DB	08H,01H,10H,01H,03H,05H,07H,09H,0BH,1DH,1FH,0DH,0FH,05H
		DB	07H,11H,13H,15H,17H
MSG1:	DEFMSG	7,6,'CHARACTER  /  NICKNAME'
MSG2:	DEFMSG	7,8,'-SHADOW       "BLINKY"'
MSG3:	DEFMSG	7,10,'-SPEEDY       "PINKY"'
MSG4:	DEFMSG	7,12,'-BASHFUL      "INKY"'
MSG5:	DEFMSG  7,14,'-POKEY        "CLYDE"'
MSG6:	DEFMSG  12,16,'10 pts'
MSG7:	DEFMSG	12,18,'50 pts'
MSG8:	DEFMSG	3,20,'(c) 1980 BALLY/MIDWAY MFG CO'
;MSG9:	DEFMSG	3,22,'   PROGRAMMED FOR NABU BY'
;MSG10:	DEFMSG	3,23,'       LEO BINKOWSKI'
MSGLST:	DW	MSG1,MSG2,MSG3,MSG4,MSG5,MSG6,MSG7,MSG8 ;,MSG9,MSG10
ENDLST:
DOTCB:	DS	6
PILTCB:	DS	6
DEMOV:: DB	04H
SLOWFL::DB	00H
PILFLG:	DB	00H
ENDFLG: DB	00H
GHOFLG:	DB	00H
	;
ANIMATE::
	CALL	VBLKON
	PCALL	SETVDP
	PCALL	VNAMEST,3800H
	PCALL	RPATRN,ENGLISH
	PCALL	RPATRN,PACLOGO
	PCALL	LPATRN,SPACSP,1800H
	PCALL	VRAML8,10H,COLR,2000H
	PCALL	VFILL,300H,20H,3800H
	SETCOLR BLACK,BLACK
	CALL	VBLKOF
	PCALL	PUTMSG,MSGA
	PCALL	PUTMSG,MSGB
	PCALL	PUTPAT,10,18,18H
	PCALL	PUTPAT,10,16,19H
	;
	ASIGN16. (TMP16),MSGLST
	REPEAT.
	  LD	HL,(TMP16)
	  LD	C,(HL)
	  INC	HL
	  LD	B,(HL)
	  CALL	PUTMSG
	  LD	BC,(TMP16)
	  INC	BC
	  INC	BC
	  LD	(TMP16),BC
	UNTIL16. (TMP16),.EQ.,ENDLST
SETGHO:	PCALL	VFILL,128,0C0H,3C00H
	PCALL	SPCOLR,5,WHITE
	PCALL	SPCOLR,6,WHITE
	PCALL	SPCOLR,7,WHITE
	PCALL	SPCOLR,8,WHITE
	PCALL	SPCOLR,1,DRED
	PCALL	SPCOLR,2,LRED
	PCALL	SPCOLR,3,CYAN
	PCALL	SPCOLR,4,DYELLOW
	PCALL	SPCOLR,0,LYELLOW
	XOR	A
	LD	(BLIX),A
	LD	(PINX),A
	LD	(INKX),A
	LD	(CLYX),A
	LD	(PACMTH),A
	LD	(BLIDIR),A
	LD	(PINDIR),A
	LD	(INKDIR),A
	LD	(CLYDIR),A
	LD	(PWRTIMR),A
	LD	A,4
	LD	(DEMOV),A
	ASIGN8.	(BLIY),7*8
	ASIGN8.	(PINY),9*8
	ASIGN8.	(INKY),11*8
	ASIGN8.	(CLYY),13*8
	ASIGN8.	(PACY),15*8+4
	ASIGN8.	(PLAYER),3
	ASIGN8.	(RACK),1
	ASIGN8.	(PACX),-3
	PCALL	SPMOVE,0,(PACY),(PACX)
	;
	; THIS PART WILL MOVE THE GHOST'S INTO POSITION BESIDE THEIR
	; RESPECTIVE NAMES.
	;
	N.CLKRV	0
	SETSK	DO_OPEN,DOTCB,3,1
	SETSK	PILFLA,PILTCB,10,1
	SETSK	TICK,TICTCB,10,1
LOOP:	IF8.	(ENDFLG),.EQ.,TRUE
	PCALL	WAIT,8
GOOMBYE:   
	   N.CLKRV 0
	   PCALL SPMARK,0
	   RET
	ENDIF.
	N.DEVRDY KEYBOARD,KEYSTROKE		;GET KEYBOARD STATUS
	AND	A				;A KEY MAYHAPS?
	JR	Z,LOOP1				;NAH...TRY JOYSTICK
	N.DEVIO KEYBOARD,KEYSTROKE		;YES...READ IT
	CP	0DH				;CARRIAGE RETURN?
	JP	Z,GOOMBYE
LOOP1:	N.DEVRDY KEYBOARD,JSTCK1		;GET JOY 1'S STATUS
	AND	A				;MOVED?
	JP	Z,LOOP				;NO...GO AND SEE AGAIN
	N.DEVIO KEYBOARD,JSTCK1			;READ IT
	BIT	4,A				;CHECK FIRE BUTTON
	JP	NZ,GOOMBYE
	JP	LOOP				
DO_OPEN:
	LD	A,(PWRTIMR)
	AND	A
	JP	NZ,A@CON7
	LD	A,(BLIX)		;GET THE X POSITION
	CP	4*8+4			;IS HE AT THE FIRST PLACE YET?
	JR	Z,A@CON1		;YES...DO PINKY THEN
	PCALL	MOVGHO,BLIX,(BLIDIR),BLINKY ;OTHERWISE MOVE BLINKY
	RET
A@CON1:	LD	A,(PINX)		;GET PINKY'S X
	CP	4*8+4			;IS HE THERE YET?
	JR	Z,A@CON2		;YES...TRY INKY
	PCALL	MOVGHO,PINX,(PINDIR),PINKY
	RET
A@CON2:	LD	A,(INKX)		;GET INKY'S X
	CP	4*8+4			;IS HE THERE YET?
	JR	Z,A@CON3		;YES...TRY CLYDE
	PCALL	MOVGHO,INKX,(INKDIR),INKYE
	RET
A@CON3:	LD	A,(CLYX)		;GET CLYDE'S X
	CP	4*8+4			;HAS HE ARRIVED?
	JR	Z,A@CON4		;YES...THEN MOV THE PACMAN IN
	PCALL	MOVGHO,CLYX,(CLYDIR),CLYDE
	RET
A@CON4:	;
	;
	; THIS PART WILL MOVE THE PACMAN INTO THE PICTURE
	;
	;
	LD	A,(PACX)		;IS HE THERE YET?
	CP	4*8+5
	JR	Z,A@CON5		;YES...THEN MOVE DOWN
	CALL	PACMOV
	RET
A@CON5:	LD	A,(PACY)		;HAS HE EATEN THE PILL YET?
	CP	18*8	
	JP	Z,A@CON6		;YES...THEN CHASE THE GHOSTS
	ASIGN8.	(DEMOV),2
	CALL	PACMOV
	LD	A,10H
	LD	(BLIDIR),A
	LD	(PINDIR),A
	LD	(INKDIR),A
	LD	(CLYDIR),A
	PCALL	MOVGHO,BLIX,(BLIDIR),BLINKY
	PCALL	MOVGHO,PINX,(PINDIR),PINKY
	PCALL	MOVGHO,INKX,(INKDIR),INKYE
	PCALL	MOVGHO,CLYX,(CLYDIR),CLYDE
	RET
A@CON6:	
	CALL	BLISCK
	CALL	PINSCK
	CALL	INKSCK
	CALL	CLYSCK
	ASIGN8. (PWRTIMR),0FFH
	N.CLKRV	PILTCB
	PCALL	PUTPAT,5,18,20H
A@CON7:	LD	A,8
	LD	(DEMOV),A
	CALL	PACMOV
	PCALL	CKREL,PACX,BLIX,07H
	CALL	Z,BLIGON
	PCALL	CKREL,PACX,PINX,07H
	CALL	Z,PINGON
	PCALL	CKREL,PACX,INKX,07H
	CALL	Z,INKGON
	PCALL	CKREL,PACX,CLYX,07H
	CALL	Z,CLYGON
	LD	A,30H
	LD	(BLIDIR),A
	LD	(INKDIR),A
	LD	(PINDIR),A
	LD	(CLYDIR),A
	LD	A,(SLOWFL)
	XOR	1
	LD	(SLOWFL),A
	RET	Z
	IF8.	(GHOFLG),.LT.,1
	  PCALL	SMOVGHO,CLYX,(CLYDIR),CLYDE
	ENDIF.
	IF8.	(GHOFLG),.LT.,2
	  PCALL	SMOVGHO,INKX,(INKDIR),INKYE
	ENDIF.
	IF8.	(GHOFLG),.LT.,3
	  PCALL	SMOVGHO,PINX,(PINDIR),PINKY
	ENDIF.
	IF8.	(GHOFLG),.LT.,4
	  PCALL	SMOVGHO,BLIX,(BLIDIR),BLINKY
	ENDIF.
	IF8.	(PACY),.LT.,10H
	  ASIGN8. (PACST),2
	ENDIF.
	RET
BLIGON:	PCALL	SPNAME,5,0FCH
	PCALL	SPCOLR,1,CLEAR
	PCALL	SPMOVE,1,0C0H,00H
	ASIGN8.	(GHOFLG),4
	ASIGN8.	(ENDFLG),TRUE
	RET
PINGON:	PCALL	SPNAME,6,0F8H
	PCALL	SPCOLR,2,CLEAR
	PCALL	SPMOVE,2,0C0H,00H
	ASIGN8.	(GHOFLG),3
	RET
INKGON:	PCALL	SPNAME,7,0F4H
	PCALL	SPCOLR,3,CLEAR
	PCALL	SPMOVE,3,0C0H,00H
	ASIGN8.	(GHOFLG),2
	RET
CLYGON:	PCALL	SPNAME,8,0F0H
	PCALL	SPCOLR,4,CLEAR
	PCALL	SPMOVE,4,0C0H,00H
	ASIGN8.	(GHOFLG),1
	RET
PILFLA:	LD	A,(PILFLG)
	XOR	1
	LD	(PILFLG),A
	JR	Z,PUTIT
	PCALL	PUTPAT,5,18,18H
	RET
PUTIT:	PCALL	PUTPAT,5,18,20H
	RET
COLR:	DB	0A0H,0A0H,0A0H,0A0H,0F0H,0F0H,0F0H,0F0H
	DB	0F0H,0F0H,0F0H,0F0H,0F0H,0F0H,0F0H,0F0H
END
