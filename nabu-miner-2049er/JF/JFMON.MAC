	.Z80
;------------------------------------------------------------------------------
;MINER 2049er SCREEN LAYOUT DEVELOPMENT
;	MONSTER PART OF MONSTER TASK
;	17MAY84 JF	MAKE SURE MONSTER KEEPS MOVING AFTER KILLING BOB
;	10MAY84	JF	MADE MONSTER LIMITS EASIER FOR BOB TO JUMP
;	08MAY84	JF	SOUND: 	SKILL= KILL MONSTER
;	27APR84
;------------------------------------------------------------------------------
	.RADIX	10
	.XLIST

CCP	EQU	0	;1=JF TEST [DEFINE "RADIOACTIVE", 0=EXTRN "RADIOACTIVE"

	INCLUDE	COMMON.MRO
	INCLUDE	STRUCT.MRO
	INCLUDE	LINKTAB.EXA
	INCLUDE	CLKAT.MRO
	INCLUDE ARITH.MRO

FILL	MACRO	FRADD,BYTES,WHAT
	LOCAL	ZEROM,LOOP
	LD A,WHAT		;MAY BE VARIABLE, SO PUT IN A
ZEROM:	LD B,BYTES		;LENGTH OF BUFFER TO CLEAR
	LD HL,FRADD		;BASE ADDRESS
LOOP:	LD (HL),A		;LOAD INTO (HL) GIVEN VALUE WHAT
	INC HL			;INCREMENT BUFFER POINTER
	DJNZ LOOP		;DEC B, JUMP TO LOOP IF NOT ZERO
	ENDM

COPBUF	MACRO	FRADD,TOADD,TOTBTS
	LD DE,TOADD
	LD HL,FRADD
	LD BC,TOTBTS
	LDIR
	ENDM

	.LIST

	EXTRN SKILL,CKCOLL
	EXTRN FAKEFLAG,MONTOG,MONTCB,MONNUM,MONOFF,MONCOL,COLFLG,FLACNT
	EXTRN GREENF,GRETOG,FRBUFF,TXDIM,TYDIM
;------------------------------------------------------------------------------
	EXTRN MVALUE,MTOPPY,MLEFTX,MRIGHX,MPRESX,MDIREC,MINTER,MCOUNT
	EXTRN MSPTNU,MTOGGL,MSTEPS,MSTNUM,MSTCNT
;------------------------------------------------------------------------------
	EXTRN TVALUE,TTOPPX,TTOPPY,TBOTTX,TBOTTY,TDX,TDY,TCOUNT
;------------------------------------------------------------------------------
	EXTRN VALENT,VALTAB,BBTX,BBTY,BBBX,BBBY,ZOTFLG,ZOVAFG,TSEFLG
;------------------------------------------------------------------------------

	EXTRN CLAIM,LNKTB,DOPIC,PREZON,ORFLAG,OVFLAG
	EXTRN SPNAME,SPCOL,SPMOVE
	EXTRN ZOTTSE,TSEX,TSEY,TSEDX,TSEDY,ZOTX,ZOTY,ZOTDX,ZOTDY
	EXTRN VALX,VALY,VALDX,VALDY
	EXTRN TSETAB,MONTAB,BBX,BBY
	EXTRN SCRNUM,NXT_GUY
	EXTRN SCORUP
	EXTRN N80,N90,N100,N200,N300,N400,N500,N1100
	EXTRN ADSCOR,COLADD,PRELEV,PTRADD
	EXTRN RADIOACTIVE,BOBX,BOBY

JFMON::
;	MONSTER PART OF MONTSK

;**************** TAKE CARE OF FEET MOVE TOGGLE **************
	SUB8. (MSTCNT),1		;DECREMENT EVERY INTERRUPT
	IF8. (MSTCNT),.EQ.,0		;IF MONSTER TOGGLE COUNT SAYS
	    LD A,8
	    LD (MSTCNT),A		;MOVE INTERVAL [IIII]=4; TOGGLE INT=8
	    LD A,(MTOGGL)		;TOGGLE (MTOGGL)
	    XOR 1
	    LD (MTOGGL),A
	    IF8. (MTOGGL),.EQ.,0		;TOGGLE FEET FOR NEXT TIME
		ASIGN8. (MSTNUM),4		; 0/4
	    ELSE.
		ASIGN8. (MSTNUM),0
	    ENDIF.
	ENDIF.

;********** TABLE LOOP *************

	LD HL,MONTAB			;START AT TOP OF MONTAB
	LD (FRBUFF),HL			;KEEP ENTRY POINTER
MOVLOO:		;TOP OF LOOP THAT GOES THROUGH MONSTER TABLE	
	LD IX,(FRBUFF)			;USE ENTRY POINTER
	LD A,(IX+0)			;VAL
	CP 0				;END-OF-TABLE
	JP Z,XMVMON			;YES, EXIT LOOP
CKMODD:		;CHECK FOR MONSTER DEAD
	CP 0DDH				;IS IT DEAD
	JP Z,NXTMON			;YES, INCREMENT TO NEXT ENTRY
CKMOCC:		;CHECK FOR MONSTER VALUE IN COUNT-DOWN
	CP 0CCH				;DO WE HAVE A VALUE COUNTER GOING ?
	JP NZ,CKMOCO			;NO, GO CHECK MONSTER COLLISION
	;YES, DO MONSTER VALUE COUNTER ROUTINE

;***************** MONSTER DIEING ********************

	LD A,(IX+8)			;MSPTNU
	LD (MSPTNU),A			;
	LD A,(IX+7)			;DECREMENT COUNT
	DEC A				;
	LD (IX+7),A			;PUT BACK IN (MCOUNT)
	CP 0				;ARE WE DOWN TO 0 ?
	JP NZ,TOGCOL			;NO, GO TOGGLE SCORE COLOR
	LD (IX+0),0DDH			;YES, FINALLY DEAD DEAD
	PCALL SPMOVE,(MSPTNU),0C0H,0	;PUT OUT OF SIGHT (ZAP SUCKER)

	JP NXTMON			;GO FOR NEXT ENTRY

TOGCOL:		;************* TOGGLE DIEING VALUE ***********

	SRL A				;SHIFT RIGHT TO CHECK ODD/EVEN
	JP NC,TOG0			;ENDS IN 0
TOG1:	PCALL SPCOL,(MSPTNU),GREY		;GREY IF ODD
	JP NXTMON			;GO FOR NEXT MONSTER
TOG0:	PCALL SPCOL,(MSPTNU),LBLUE		;BLUE IF EVEN

	JP NXTMON			;NEXT ENTRY

;******************** MONSTER IS ALIVE **********************

CKMOCO:		;CHECK MONSTER COLLISION WITH BOB
	LD A,(IX+4)			;FOR CKCOLL (TTOPPX)_(MPRESX)
	LD (MPRESX),A
	ADD A,2		;MAKE EASIER
	LD (TTOPPX),A			;
	LD C,7				;(TBOTTX)_(MPRESX)+11 [7 IS EASIER]
	ADD A,C				;
	LD (TBOTTX),A			;
	LD A,(IX+1)			;(TTOPPY)_(MTOPPY)
	LD (MTOPPY),A
	INC A				;ADJUST TO MATCH BB CO-ORDINATES = PATS
	ADD A,2		;MAKE EASIER
	LD (TTOPPY),A			;
	LD C,7				;(TBOTTY)_(MTOPPY)+9 [-2 TO MATCH]
	ADD A,C				;
	LD (TBOTTY),A			;
	CALL CKCOLL			;CHECK COLLISION WITH BOB
	AND A				;DID WE COLLIDE

	JP Z,DOMOMO			;NO, GO DO MONSTER MOVE

;******************** MONSTER COLLISION ***********************

DOMOCO:		;WE HAVE A MONSTER COLLISION, START COUNTER ETC.
	IF8. (GREENF),.EQ.,0		;SUCKERS ARE MEAN
		IF CCP	;FOR TESTING ONLY CAUSE LEO DOES THIS NOW
		  ASIGN8. (NXT_GUY),1	;TO TRIGGER PUTSCR
		ENDIF
		ASIGN8. (RADIOACTIVE),1	;FOR LEO'S RADIOACTIVE ROUTINE
;		JP XMVMON		;TAKE OFF EH
		JP DOMOMO		;KEEP MOVING
	ENDIF. 
	ASIGN8. (SKILL),1		;KILL SOUND, HE TURNS IT OFF
	LD A,(IX+7)			;LOAD (MINTER) AS RANDOM NUMBER
	SRL A				;SHIFT RIGHT TO CHECK ODD/EVEN
	JP NC,VTOG0			;ENDS IN 0
VTOG1:	ASIGN8. (MVALUE),0F8H		;80 POINTS
	JP CHAMON			;GO ON OVER
VTOG0:	ASIGN8. (MVALUE),0FCH		;90 POINTS

CHAMON:		;**************** CHANGE MONSTER TO SCORE ****************

	LD A,(IX+8)			;(MSPTNU)
	LD (MSPTNU),A			;ASIGN IT
	PCALL SPNAME,(MSPTNU),(MVALUE)		;CHANGE MONSTER TO SCORE
	PCALL SPCOL,(MSPTNU),LBLUE		;START IT BLUE
	PCALL SPMOVE,(MSPTNU),(MTOPPY),(MPRESX)	;PUT UP SCORE
	IF8. (MVALUE),.EQ.,0F8H		;MONSTER WORTH 80
		PCALL ADSCOR,080H	;ADD TO SCORE
	ELSE.
		PCALL ADSCOR,090H
	ENDIF.
	LD (IX+0),0CCH		;FLAG THAT COUNTER STARTED
	LD (IX+7),182		;START 3 SECOND COUNT

	JP NXTMON		;NEXT ENTRY PLEASE

;**************** MOVE LIVE MONSTER *******************

DOMOMO:		;DO MONSTER MOVE

;******************* CHECK FOR COLOR CHANGE ************

	IF8. (COLFLG),.EQ.,1	;WE HAVE A COLOR CHANGE
	  LD A,(IX+8)			;MSPTNU
	  LD (MSPTNU),A			;				
	  PCALL SPCOL,(MSPTNU),(MONCOL)	;PUT COLOR
	ENDIF.

;******************* CHECK FOR COORDINATE CHANGE ************

	LD A,(IX+7)			;(MCOUNT)
	DEC A				;DECREMENT
	LD (IX+7),A			;SAVE IT BACK
	CP 0				;DO WE MOVE ?
	JP Z,CHACOR			;YES, GO CHANGE COORDINATES
	JP NXTMON			;NO, GET NEXT MONSTER

CHACOR:		;****************** CHANGE COORDINATES *************

	LD A,(IX+8)			;MSPTNU
	LD (MSPTNU),A			;
	LD (IX+7),4			;MOVE INTERVAL=4
	LD A,(IX+5)			;MDIREC
	LD (MDIREC),A
	LD A,(IX+6)			;SPEED= # OF PIXS / MOVE
	LD (MSTEPS),A			;PUT IN (MSTEPS)
	IF8. (MDIREC),.EQ.,1		;MOVE RIGHT
	    ADD8. (MPRESX),(MSTEPS)	;MOVE ALONG (MDIREC) FOR (MSTEPS)
	ELSE.				;MOVE LEFT
	    SUB8. (MPRESX),(MSTEPS)	;MOVE ALONG (MDIREC) FOR (MSTEPS)
	ENDIF.
	LD A,(IX+2)			;MLEFTX
	LD (MLEFTX),A
	IF8. (MPRESX),.LE.,(MLEFTX)	;AT LEFT EDGE
	    LD A,(IX+2)			;MLEFTX
	    LD (MPRESX),A		;PUT IN (MPRESX)
	    LD (IX+5),1			;MDIREC
	    ASIGN8. (MDIREC),1		;RESET (MDIREC)
	ELSE.
	  LD A,(IX+3)			;MRIGHX
	  LD (MRIGHX),A
	  IF8. (MPRESX),.GE.,(MRIGHX)	;AT RIGHT EDGE
	    LD A,(IX+3)			;MRIGHX
	    LD (MPRESX),A		;PUT IN (MPRESX)
	    LD (IX+5),0			;MDIREC 0=LEFT
	    ASIGN8. (MDIREC),0		;RESET (MDIREC)
	  ENDIF.
	ENDIF.
	LD A,(MPRESX)			;SAVE (MPRESX)
	LD (IX+4),A			;MPRESX

DRAMON:		;********** DRAW ACTUAL MONSTER **********

	IF8. (GREENF),.EQ.,0		;MEAN
	    IF8. (MDIREC),.EQ.,1	;GOING RIGHT
		ASIGN8. (MONOFF),0E4H
	    ELSE.			;GOING LEFT
		ASIGN8. (MONOFF),0DCH	
	    ENDIF.
	ELSE.				;GREEN
	    ASIGN8. (MONOFF),0ECH
	ENDIF.

ADDTOG:
	ADD8. (MONOFF),(MSTNUM)		;MOVE EVERY TIME 

	PCALL SPNAME,(MSPTNU),(MONOFF)
	PCALL SPMOVE,(MSPTNU),(MTOPPY),(MPRESX)

;********* NEXT ENTRY PLEASE ***********

NXTMON:		;INCREMENT FRBUFF TO POINT TO NEXT ENTRY IN MONSTER TABLE
	LD HL,(FRBUFF)			;INCREMENT TO NEXT ENTRY
	LD BC,09			;AND ADD 09
	ADD HL,BC			;  TO HL
	LD (FRBUFF),HL			;PUT BACK IN POINTER

	JP MOVLOO			;CONTINUE LOOP

;******** EXIT LOOP ***********

XMVMON:		;EXIT OF MONSTER TABLE LOOP

	ASIGN8. (COLFLG),0		;RESET FOR NEXT TIME AROUND

	RET	;END OF JFMON

	END	;END OF PROGRAM

