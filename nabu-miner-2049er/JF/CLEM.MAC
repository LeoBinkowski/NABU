	.Z80
;------------------------------------
;	CLEMENTINE		    |
;------------------------------------------------------------------------------
;	29MAY84		JF	VERSION 1:
;					LOW OCTAVE,MID OCTAVE,THEN L/M/H OCTS.
;	25MAY84		JF	START
;------------------------------------------------------------------------------
	.RADIX	10

	.XLIST

	INCLUDE COMMON.MRO
	INCLUDE	STRUCT.MRO
	INCLUDE ARITH.MRO
	INCLUDE LINKTAB.EXA
	INCLUDE CLKAT.MRO

	EXTRN	LNKTB,AUDWR,VSETG1
	
	;********* NOTE: THESE ARE ROUGH NUMBERS (MAY BE RE-DEFINED) **********

	LG EQU 02165Q	;LOW G
	RB EQU 01612Q	;REGULAR B
	RC EQU 01527Q	; " C
	RD EQU 01372Q	; " D
	RE EQU 01247Q	; " E
	RF EQU 01201Q   ; " F
	RG EQU 01073Q	; " G

	T1 EQU 12	;INTERRUPTS TO DELAY FOR HALF NOTE
	T2 EQU 24	;FOR WHOLE NOTE

	;********** REGISTERS ************

	TONEAF 	EQU 0	;CHANNEL A FINE PERIOD
	TONEAC	EQU 1	;COARSE
	TONEBF	EQU 2	;B FINE
	TONEBC	EQU 3	;B COARSE
	TONECF	EQU 4	;C FINE
	TONECC	EQU 5	;C COARSE
	NOISEP	EQU 6	;NOISE PERIOD 0-31
	ENABLE	EQU 7	;CHANNEL ENABLES
	AMPA	EQU 8	;AMPLITUDE FOR A
	AMPB	EQU 9   ;AMPLITUDE FOR B
	AMPC	EQU 10	;AMPLITUDE FOR C
	EVPERF	EQU 11	;ENVELOPE PERIOD FINE
	EVPERC	EQU 12	;ENVELOPE PERIOD COARSE
	EVSHAP	EQU 13	;ENVELOPE SHAPE
	
	.LIST

START::
	LD SP,(6)	;INITIALIZE STACK POINTER
	N.LINKIO LNKTB	;INITIALIZE IO LINK TABLE
	PCALL VSETG1	;FAKE CALL TO SET GRAPHICS 1
LLL:
	IF8. (CLEFLG),.EQ.,2		;END OF SONG [SET BY TASK]
	    CALL CLEMEN
	ENDIF.
	N.DEVREADY KEYBOARD,JSTCK1	;GET INPUT FROM JOY STICK
	CP 0				;DO WE HAVE INPUT ?
	JP Z,LLL			;NO, GO LOOP
	N.DEVIO KEYBOARD,JSTCK1		;GET INPUT
	AND 010H			;IS IT FIRE ?
	JP Z,LLL			;NO, GO LOOP
	CALL CLEMEN			;YES, CONTROL SOUND
	JP LLL				;BACK TO LOOP 

	;************ ROUTINES *************

CLETSK::
;	READS SONG TABLE AND PUTS OUT NOTES OR DELAYS

	LD A,(CLEFLG)		;IF (CLEFLG)#1 EXIT
	CP 1			;IS IT 1 ?
	JP NZ,XCLTSK		;NO, EXIT  ELSE CARRY ON

	LD HL,(ADELAY)		;DEC (ADELAY)
	DEC HL			;
	LD (ADELAY),HL		;
	LD A,L			;GET A NEW NOTE WHEN DELAY IS FINISHED
	CP 0			;
	JP NZ,XCLTSK		;IF NOT 0, GET OUT

	LD HL,(SONADD)	;WHERE WE ARE IN THE SONG
	LD C,(HL)		;PUT IN BC SONG ENTRY
	INC HL		;HIGH ORDER
	LD B,(HL)
	LD (SONVAL),BC	;SAVE IT IN (SONVAL)

	;************** END OF TABLE *************
	LD HL,(SONVAL)	;CHECK FOR END OF TABLE
	LD BC,0FFH		;FFH IS E-O-T MARK
	SCF
	CCF
	SBC HL,BC
	JP NZ,DLAY		;NO, CHECK FOR DELAY

	LD A,(SCACNT)		;INCREMENT SCALE COUNT
	INC A		;
	LD (SCACNT),A
	CP 4		;HAVE WE DONE THEM ALL ?

	JP NZ,SAGAIN	;NO, GO RESET SONG ADDRESS
	ASIGN8. (CLEFLG),2	;YES, FLAG TO TURN IT OFF
	JP XCLTSK	;EXIT

SAGAIN:	
	ASIGN16. (ADELAY),1		;SO WE START RIGHT UP
	ASIGN16. (SONADD),SONG	;START ANEW
	JP XCLTSK	;EXIT

DLAY:	    ;***************** DELAY *****************
	LD BC,(SONVAL)		;PUT POSSIBLE DELAY VALUE IN BC
	SCF 			;SET CARRY FLAG
	CCF				;COMPLIMENT CARRY FLAG
	LD HL,T2			;PUT WHOLE IN HL FOR COMPARE
	SBC HL,BC			;IS (SONVAL)=T2 ?
	JP NZ,TRYT1 		;NO, TRYT1
        ASIGN16. (ADELAY),(WHOLE)	;YES,RESET DELAY
	ADD16. (SONADD),2		;TO GET NEXT WORD
	JP XCLTSK			;EXIT

TRYT1:	    
	SCF 			;SET CARRY FLAG
	CCF				;COMPLIMENT CARRY FLAG
	LD HL,T1			;PUT WHOLE IN HL FOR COMPARE
	SBC HL,BC			;IS (SONVAL)=T2 ?
	JP NZ,NOTE 			;NO, WE MUST HAVE A NOTE
	ASIGN16. (ADELAY),(HALF)	;RESET DELAY
	ADD16. (SONADD),2		;TO GET NEXT WORD
	JP XCLTSK			;EXIT

	;********** WE HAVE A NOTE *************
NOTE:	LD HL,(SONVAL)	;PUT VALUE IN HL FOR SPLIT
	LD A,H		;PUT COARSE INTO A
	LD (COAA),A		;SAVE IN COARSE
	LD A,L		;PUT FINE IN A
	LD (FINA),A		;SAVE IN FINE

	LD A,(COAA)		;GET COARSE A
	SRL A		;SHIFT RIGHT (/2) AND SAVE CARRY
	LD (COAB),A		;PUT IN B
	LD A,(FINA)		;
	RRA			;ROTATE RIGHT AND PICK UP CARRY
	LD (FINB),A		;SAVE IN FINB

	LD A,(COAB)		;GET COARSE B
	SRL A		;SHIFT RIGHT (/2) AND SAVE CARRY
	LD (COAC),A		;PUT IN C
	LD A,(FINB)		;
	RRA			;ROTATE RIGHT AND PICK UP CARRY
	LD (FINC),A		;SAVE IN FINC
	
	LD A,(SCACNT)	;SCALE COUNT
SCA1:	CP 1		;IS IT 1 ?
	JP NZ,SCA2		;NO, TRY FOR 2
	PCALL AUDWR,AMPA,16			;SET A FOR ENVELOPE
	PCALL AUDWR,AMPB,0			;B
	PCALL AUDWR,AMPC,0			;C
	PCALL AUDWR,TONEAF,(FINA)	;(FINE) INTO FINE A TONE
	PCALL AUDWR,TONEAC,(COAA) 	;(COARSE) INTO COARSE A TONE
	JP DOSHAP	;GO DEFINE SHAPE
SCA2:	CP 2		;IS IT 2
	JP NZ,SCA3		;NO, TRY SCA3
	PCALL AUDWR,AMPA,0			;SET B FOR ENVELOPE
	PCALL AUDWR,AMPB,16			;B
	PCALL AUDWR,AMPC,0			;C
	PCALL AUDWR,TONEBF,(FINB)	;(FINE) INTO FINE B TONE
	PCALL AUDWR,TONEBC,(COAB) 	;(COARSE) INTO COARSE B TONE
	JP DOSHAP	;GO DEFINE SHAPE
SCA3:	    
	PCALL AUDWR,AMPA,16			;SET ALL FOR ENVELOPE
	PCALL AUDWR,AMPB,16			;B
	PCALL AUDWR,AMPC,16			;C
	PCALL AUDWR,TONEAF,(FINA)	;(FINE) INTO FINE A TONE
	PCALL AUDWR,TONEAC,(COAA) 	;(COARSE) INTO COARSE A TONE
	PCALL AUDWR,TONEBF,(FINB)	;(FINE) INTO FINE B TONE
	PCALL AUDWR,TONEBC,(COAB) 	;(COARSE) INTO COARSE B TONE
	PCALL AUDWR,TONECF,(FINC)	;(FINE) INTO FINE C TONE
	PCALL AUDWR,TONECC,(COAC) 	;(COARSE) INTO COARSE C TONE
DOSHAP:
	PCALL AUDWR,EVSHAP,3	;RESET ENVELOPE SHAPE
	ASIGN16. (ADELAY),(HALF)	;HALF NOTE ASSUMED
	ADD16. (SONADD),2		;TO GET NEXT WORD

XCLTSK:

	RET	;END OF CLETSK

CLEMEN::
	;CONTROL SONG TASK
	;IF CLEFLG=0 -> ATTACH TASK
	;IF CLEFLG#0 -> REMOVE TASK

	IF8. (CLEFLG),.GT.,0			;SUCKER IS ON, SO TURN OFF
	    PCALL AUDWR,AMPA,0			;TURN OFF A
	    PCALL AUDWR,AMPB,0			;B
	    PCALL AUDWR,AMPC,0			;C
	    ASIGN8. (CLEFLG),0			;FLAG THAT IT IS OFF
	    N.CLKRV CLETCB			;TURN IT OFF
	ELSE.			;IT IS OFF (0), SO TURN ON
	    ASIGN16. (HALF),T1			;FIRST VALUE
	    ASIGN16. (WHOLE),T2			;FIRST VALUE
	    ASIGN8. (SCACNT),1			;FIRST TIME AROUND
	    ASIGN16. (ADELAY),(HALF)		;PRESET FIRST DELAY
	    ASIGN16. (SONADD),SONG		;PRESET FIRST SONG ENTRY
	    PCALL AUDWR,ENABLE,0F8H		;ENABLE A,B,C CHANNELS
	    PCALL AUDWR,AMPA,16			;SET A FOR ENVELOPE
	    PCALL AUDWR,AMPB,0			;B
	    PCALL AUDWR,AMPC,0			;C
	    PCALL AUDWR,EVPERC,027H		;  MAKE IT 10000
	      PCALL AUDWR,EVPERF,010H		;    2710H
	    SETSK CLETSK,CLETCB,1,1		;ATTACH SONG DRIVER
	    ASIGN8. (CLEFLG),1			;FLAG THAT IT IS RUNNING
	ENDIF.
	
	RET	;END OF CLEMEN

	;************ DATA *************

HALF:		DW	0	;INTERRUPTS TO MAKE HALF NOTE
WHOLE:		DW	0	;INTERRUPTS TO MAKE WHOLE NOTE
SCACNT:		DB	0	;SCALE COUNT
CLEFLG::	DB	0	;FLAG TO TURN SONG OFF AND ON
CLETCB::	DS	6	;TCB FOR CLETSK
SONADD:		DW	0	;PRESENT ADDRESS IN SONG
SONVAL:		DW	0	;PRESENT VALUE OF SONG ENTRY
ADELAY:		DW	0	;NUMBER OF INTERRUPTS TO WAIT
COAA:		DB	0	;COARSE SPLIT OF NUMBER FOR A
FINA:		DB	0	;FINE SPLIT OF WORD FOR A
COAB:		DB	0	;COARSE SPLIT OF NUMBER FOR B
FINB:		DB	0	;FINE SPLIT OF WORD FOR B
COAC:		DB	0	;COARSE SPLIT OF NUMBER FOR C
FINC:		DB	0	;FINE SPLIT OF WORD FOR C

SONG:   DW	RC,RC,RC,T1,LG,T1	;OH MY DARLING
	DW	RE,RE,RE,T1,RC,T1	;OH MY DARLING
	DW	RC,RE,RG,T2,RG		;OH MY DARLING
	DW	RF,RE,RD,T1,T2		;CLEMENTINE
	DW	RD,RE,RF,T1,RF,T1	;YOU ARE LOST AND 
	DW	RE,RD,RE,T1,RC,T1	;GONE FOR EVER
	DW	RC,RE,RD,T1,LG,T1	;DREADFUL SORRY
	DW	RB,RD,RC,T1,T2		;CLEMENTINE
	DW	0FFH			;END OF SONG

	END	;END OF CLEM

