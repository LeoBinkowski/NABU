
	.Z80
;------------------------------------------------------------------------------
;MINER 2049er MONSTER TASK PLUS SPECIAL EFFECTS
;
;	16MAY84 JF	TRY LEAVING OUT ZOT STUFF UNTIL NOT QUEUED
;	15MAY84 JF	CLEAR OUT TONNAGE DISPLAY AFTER BOB JUMPS OK
;			FORCE ZOTTSE IF ONE IS QUEUED
;	08MAY84	JF	DEBUG CKLIM
;			MOVE PLUNGERS UP 4 PIX'S
;	08MAY84	JF	SOUNDS	SPRIZE	=	CLAIM TREASURE
;			TRRCOL THE GOOD
;			PUT M816 IN TO AVOID ARITHB IN JLIB
;	06MAY84 JF	TRRCOL TEST
;			NEW BOB SIZE
;	04MAY84	JF	PLUTSK PROBLEM
;			TONNAGE AND DANGER STUFF
;	02MAY84
;------------------------------------------------------------------------------
	.RADIX	10
	.XLIST

CCP	EQU	0	;1=JF TEST [DEFINE "RADIOACTIVE", 0=EXTRN "RADIOACTIVE"

	INCLUDE	COMMON.MRO
	INCLUDE	STRUCT.MRO
	INCLUDE	LINKTAB.EXA
	INCLUDE	CLKAT.MRO
	INCLUDE ARITH.MRO

FILL	MACRO	FRADD,BYTES,WHAT
	LOCAL	ZEROM,LOOP
	LD A,WHAT		;MAY BE VARIABLE, SO PUT IN A
ZEROM:	LD B,BYTES		;LENGTH OF BUFFER TO CLEAR
	LD HL,FRADD		;BASE ADDRESS
LOOP:	LD (HL),A		;LOAD INTO (HL) GIVEN VALUE WHAT
	INC HL			;INCREMENT BUFFER POINTER
	DJNZ LOOP		;DEC B, JUMP TO LOOP IF NOT ZERO
	ENDM

COPBUF	MACRO	FRADD,TOADD,TOTBTS
	LD DE,TOADD
	LD HL,FRADD
	LD BC,TOTBTS
	LDIR
	ENDM

	.LIST

	EXTRN SPRIZE,NUMIES,WALTAB,LASWAL,RANDOM,INITRAN
	EXTRN JFMON,CLAIM,LNKTB,DOPIC,PREZON,ORFLAG,OVFLAG
	EXTRN SPNAME,SPCOL,SPMOVE
	EXTRN ZOTTSE,TSEX,TSEY,TSEDX,TSEDY,ZOTX,ZOTY,ZOTDX,ZOTDY
	EXTRN VALX,VALY,VALDX,VALDY
	EXTRN TSETAB,MONTAB,BBX,BBY
	EXTRN SCRNUM,NXT_GUY
	EXTRN SCORUP
	EXTRN N80,N90,N100,N200,N300,N400,N500,N1100,NL100,NL200,NL300
	EXTRN ADSCOR,COLADD,PRELEV,PTRADD,TONNAGE
	IF CCP
	  SETPLA::		DB	0C9H	;LEO ROUTINE
	  BOBX::		DB	0	;BOB X COORD
	  BOBY::		DB	0	;BOB Y COORD
	  RADIOACTIVE::		DB	1	;BOB IS RADIOACTIVE FLAG
	ENDIF
	IFF CCP		;LEO HAS IT DEFINED
	  EXTRN RADIOACTIVE,BOBX,BOBY,SETPLA
	ENDIF
MONTSK::
;CHECK BOB-TREASURE COLLISION
;CHECK BOB-MONSTER COLLISION
;MOVE MOSTERS
	JP DOMNTK
;LEO PUTS IN...	TONNAGE::	DB	0	;BCD FOR TNT TONNAGE
	FAKEFLAG::	DB	0	;LAZINESS
	MONTOG::	DB	0	;SIGNALS TOGGLE OF MONSTERS
	MONTCB::	DS	6	;TCB FOR MONSTER TASK
	MONNUM::	DB	0	;KEEP TABS ON SPRITE NUMBER
	MONOFF::	DB	0	;OFFSET IN MONTER PATTERN TABLE
	MONCOL::	DB	0	;MONSTER'S COLOR
	COLFLG::	DB	0	;FLAGS WHETHER TO CHANGE COLOR 1=Y 0=N
	FLACNT::	DB	0	;FLASH COUNTER, WHEN TO FLASH MONSTERS
	GREENF::	DB	0	;0=MEAN ELSE=GREEN 
	GRETOG::	DB	0	;TOGGLE TO DEC GF EVERY 2 INTS
	FRBUFF::	DW	0	;FROM BUFF ADDRESS
	TXDIM::		DB	0	;TREASURE X DIMENSION
	TYDIM::		DB	0	;TREASURE Y DIMENSION
;------------------------------------------------------------------------------
	MVALUE::	DB	0	;COMPLETE MONSTER TABLE ENTRY
	MTOPPY::	DB	0	;MONSTER SPRITE TOP Y
	MLEFTX::	DB	0	;LEFT X LIMIT
	MRIGHX::	DB	0	;RIGHT X LIMIT
	MPRESX::	DB	0	;PRESENT X VALUE
	MDIREC::	DB	0	;MONSTER DIRECTION 1=R 0=L
	MINTER::	DB	0	;INTERRUPTS/STEP
	MCOUNT::	DB	0	;KEEP INTERRUPT COUNT
	MSPTNU::	DB	0	;SPRITE NUMBER OF SPRITE
	MTOGGL::	DB	0	;VALUE TO ADD PATTERN NUMBER FOR STEP
	MSTEPS::	DB	0	;NUMBER OF PIXELS/STEP 
	MSTNUM::	DB	0	;STEP NUMBER = STEPS/FEET MOVE
	MSTCNT::	DB	0	;COUNT OF STEPS BETWEEN FEET MOVE (TOG)
;------------------------------------------------------------------------------
	TVALUE::	DB	0	;TREASURE TABLE ENTRY
	TTOPPX::	DB	0	;TOP X
	TTOPPY::	DB	0	;TOP Y
	TBOTTX::	DB	0	;BOTTOM X
	TBOTTY::	DB	0	;BOTTOM Y
	TDX::		DB	0	;X DIMENSION TO BLANK OUT
	TDY::		DB	0	;Y DIMENSION TO BLANK OUT
	TCOUNT::	DB	0	;KEEP COUNT HOW LONG SCORE UP
;------------------------------------------------------------------------------
	VALENT::	DW	0	;POINTER TO VALTAB
	VALTAB::			;VALUE TABLE
		DW	0000H,0,0000	;DUMMY CAUSE WE USE 0 FOR EOT
		DW	0080H,2,N80	;80=6
		DW	0090H,2,N90	;90=12
		DW	0100H,3,N100	;100=18
		DW	0200H,3,N200	;200=24
		DW	0300H,3,N300	;300=30
		DW	0400H,3,N400	;400=36
		DW	0500H,3,N500	;500=42
		DW	1100H,4,N1100	;1100=48
		DW	0100H,2,NL100	;LITTLE 100=54
		DW	0200H,2,NL200	;LITTLE 200=60
		DW	0300H,2,NL300	;LITTLE 300=66

	BBTX::		DB	0	;BB TOP X
	BBTY::		DB	0	;BB TOP Y
	BBBX::		DB	0	;BB BOTTOM X
	BBBY::		DB	0	;BB BOTTOM Y
	ZOTFLG::	DB	0	;FLAG TO DO ZOTTSE [ERASE TREASURE]
	ZOVAFG::	DB	0	;FLAG TO DO ZOTTSE [ERASE VALUE]
	TSEFLG::	DB	0	;FLAG TO DO SCORUP
	TONFLG::	DB	0	;FLAG TO DO PUTTON
	DANFLG::	DB	0	;WE HAVE OVER 30 TONS
;------------------------------------------------------------------------------

DOMNTK:
;	IF8. (FAKEFLAG),.EQ.,0		;DON'T GO THRU HERE 1ST TIME
;		ASIGN8. (FAKEFLAG),1
;		RET
;	ENDIF.
ADDBB:		;SET UP BOB'S TOP AND BOTTOM COORDINATES	
    IF CCP	;FOR TEST USE TASK1 COORDINATES
	LD HL,BBX			;BBTX=BBX	GET BBX ADDR
	LD A,(HL)			;GET (BBX)
	LD (BBTX),A			;PUT IN (BBTX)
	LD C,3				;BBBX=BBX+3	ADD 3
	ADD A,C				;TO (BBX)
	LD (BBBX),A			;PUT IN (BBBX)
	LD HL,BBY			;BBBY=BBY-1
	LD A,(HL)			;GET (BBY)
	DEC A				;A_(BBY)-1
	LD (BBBY),A			;PUT IN (BBBY)
	LD A,(HL)			;BBTY=BBY-(BOB'S HEIGHT IN PIXELS)
	LD C,16				;USE 16 (BOB'S HEIGHT)
	SUB C				;SUB HEIGHT
	LD (BBTY),A			;PUT IN (BBTY)
    ELSE	;FOR REAL USE BOBX & BOBY
	LD A,(BOBX)		;GET BOBX
	ADD A,2			;ADJUST FOR LEFT BODY
	LD (BBTX),A		;PUT IN (BBTX)
	ADD A,11		;ADJUST FOR RIGHT OF BODY
	LD (BBBX),A		;PUT IN (BBBX)
	LD A,(BOBY)		;GET BOBY
	DEC A			;ADJUST FOR FEET
	LD (BBBY),A		;PUT IN (BBBY)
	SUB 20			;ADJUST FOR HEAD
	LD (BBTY),A		;PUT IN (BBTY)

    ENDIF

	LD HL,TSETAB			;START AT BOTTOM OF TSETAB
	LD (FRBUFF),HL
CKTSE:		;CHECK TREASURE COLLISION WITH BOB, ETC.
	LD HL,(FRBUFF)			;ENTRY POINTER
	LD A,(HL)			;VALUE FLAG
	AND A				;END-OF-TABLE ??
	JP NZ,CKTSDD			;NO, GO CHECK TREASURE DEATH
	JP XCKTSE			;YES, EXIT TREASURE CHECK LOOP
CKTSDD:		;CHECK IF TREASURE IS DEAD (DD IN VALUE FIELD)
	CP 0DDH				;DEAD ??
	JP NZ,CKTCC			;NO, GO CHECK TREASURE COUNTER
	JP NXTTSE			;YES, INCREMENT TO NEXT ENTRY
CKTCC:		;CHECK VALUE COUNTER
	CP 0CCH				;VALUE COUNTER GOING ?
	JP NZ,DOCKTS			;NO, GO CHECK TREASURE COLLISION
	LD HL,(FRBUFF)			;PUT ADRESS OF ENTRY IN IX
	PUSH HL
	POP IX
	LD A,(IX+7)			;DECREMENT (TCOUNT)
	DEC A
	LD (IX+7),A
	AND A				;IS COUNT 0 ?
	JP NZ,NXTTSE			;NO, CARRY ON
;FINALLY DEAD DEAD
	IF8. (ZOVAFG),.EQ.,1		;WE HAVE A ZOT OF VALUE PENDING
	    LD A,1			;DELAY FOR ANOTHER COUNT
	    LD (IX+7),A			;NEXT TIME IT WILL BE 0 AGAIN
	    JP NXTTSE			;GET NEXT ENTRY
	ENDIF.
	LD A,0DDH			;PUT DD IN TVALUE FIELD
	LD (IX+0),A
;BLANK OUT FOR GOOD
	ASIGN8. (ZOVAFG),1		;FLAG TO DO A ZOTTSE [ERASE SCORE]
	LD HL,ZOTX			;SET UP FOR ZOTTSE [ERASE SCORE]
	LD A,(IX+1)			;ZOTX_TTOPPX
	LD (HL),A
	LD HL,ZOTY			;ZOTY_TTOPPY
	LD A,(IX+2)
	LD (HL),A
	LD HL,ZOTDX			;ZOTDX_TDX
	LD A,(IX+3)
	LD (HL),A
	LD HL,ZOTDY			;ZOTDY_1
	LD A,1
	LD (HL),A
	JP NXTTSE			;GET NEXT ENTRY
DOCKTS:		;DO CHECK OF TREASURE COLLISION
	COPBUF (FRBUFF),TVALUE,8	;COPY ENTRY INTO VARIABLES
	CALL CKCOLL			;CHECK COLLISION
	AND A				;DO WE HAVE COLLISION
	JP NZ,TDD			;YES, GO SET TREASURE DEAD
	JP NXTTSE			;NO, GO INCREMENT TO NEXT ENTRY
	;WE PASS ALL TESTS, SO WE PUT UP SCORE, & SET GREEN COUNTER (FF??)
TDD:		;WE HAVE COLLISION, PROCEED...
	ASIGN8. (SPRIZE),1		;START SOUND [HE TURNS OFF]
	ASIGN8. (GREENF),180		;START GREEN FLAG FOR 6 SECS.
	ASIGN8. (GRETOG),0		;START GREEN FLAG TOGGLE
	ASIGN8. (FLACNT),92		;START FLASH COUNT TO FLASH AT GF=68
	ASIGN8. (COLFLG),1		;WE HAVE A CHANGE IN COLORS
	ASIGN8. (MONCOL),DGREEN		;COLOR MONSTERS GREEN
	IF8. (ZOTFLG),.EQ.,1		;WE HAVE ONE TO DO YET
	   CALL ZOTTSE			;DON'T CHECK NEW ONES TILL OLD GONE
	ENDIF. 
	ASIGN8. (ZOTFLG),1		;FLAG TO DO ZOTTSE [ERASE TREASURE]
	LD HL,TSEX			;SET UP FOR ZOTTSE [ERASE TREASURE]
	LD A,(TTOPPX)			;TSEX_TTOPPX
	LD (HL),A
	LD HL,TSEY			;TSEY_TTOPPY
	LD A,(TTOPPY)
	LD (HL),A
	LD HL,TSEDX			;TSEDX_TDX
	LD A,(TDX)
	LD (HL),A
	LD HL,TSEDY			;TSEDY_TDY
	LD A,(TDY)
	LD (HL),A
	IF8. (PRELEV),.EQ.,5	;WE ARE AT LEVEL 5
	  IF8. (TTOPPX),.EQ.,16	;RADIOACTIVE MARTINI
	    CALL RADMAR		;TURN SUCKER OFF
	    ;**** SET BOB RADIOACTIVE HERE ****;
	    ASIGN8. (RADIOACTIVE),1	;FOR LEO'S ROUTINE
	  ENDIF.
	ENDIF.
	IF8. (PRELEV),.EQ.,10	;LEVEL 10 CHECK FOR TNT CASKS
	  IF8. (TTOPPY),.EQ.,178	;10 TONNERS
		LD A,001H		;GO ADD IT
		CALL ADDTON		;
	  ELSE.		;(TTOPPY)#178
		IF8. (TTOPPY),.EQ.,156	;20 TONNERS
		    LD A,002H		;GO ADD IT
		    CALL ADDTON		;
		ELSE.			;30 TONNERS
		    LD A,003H		;GO ADD IT
		    CALL ADDTON		;
		ENDIF.	;(TTOPPY)=156
	  ENDIF.	;(TTOPPY)=178
	ENDIF.	;(PRELEV)=10
	IF8. (TSEFLG),.EQ.,1		;WE HAVE ONE TO DO YET
	    CALL SCORUP			;DON'T CHECK NEW ONES TILL OLD UP
	ENDIF. 
	ASIGN8. (TSEFLG),1		;FLAG TO DO SCORUP
	LD BC,(TVALUE)			;PUTUP TREASURE VALUE
	LD B,0				;ADD VALUE OFFSET TO VALTAB BASE
	LD HL,VALTAB			;ADDRESS OF VAL ENTRY
	ADD HL,BC			;ADD INTO HL
	LD (VALENT),HL			;PUT IN (VALENT)
	COPBUF (VALENT),VALTAB,6	;COPY SCORE ENTRY INTO DUMMY FOR SCORUP
	LD HL,TSEX			;JUSTIFY TO LEFT EDGE OF PATTERN
	LD A,(HL)			;ROUND OFF TO NEAREST 8
	AND 0F8H			;
	LD (HL),A			;PUT BACK IN TSEX FOR SCORUP
	LD (TTOPPX),A			;PUT BACK IN TTOPPX FOR NEXT ZOTTSE
	ASIGN8. (VALX),(TSEX)		;COPY FOR PUTTING VALUE UP
	ASIGN8. (VALY),(TSEY)		;COPY FOR PUTTING VALUE UP
	ASIGN8. (VALDX),(TSEDX)		;COPY FOR PUTTING VALUE UP
	ASIGN8. (VALDY),(TSEDY)		;COPY FOR PUTTING VALUE UP
	LD HL,(VALENT)			;GET LEN FOR ZOTTSE LATER
	INC HL
	INC HL
	LD A,(HL)
	LD (TBOTTX),A			;SAVE IT IN (TBOTTX)
	LD A,0CCH			;PUT CC IN VAL
	LD (TVALUE),A			;
	LD A,120			;2 SECONDS 
	LD (TCOUNT),A
WRTSE:
	COPBUF TVALUE,(FRBUFF),8	;COPY VARIABLES INTO ENTRY
NXTTSE:		;INCREMENT FRBUFF TO POINT TO NEXT TREASURE TABLE ENTRY
	LD HL,(FRBUFF)			;INCREMENT
	LD BC,8				;  POINTER
	ADD HL,BC			;    TO NEXT TREASURE ENTRY
	LD (FRBUFF),HL			;KEEP IN (FRBUFF)
	JP CKTSE			;AND CONTINUE DOWN TABLE
XCKTSE:		;EXIT CHECK TREASURE LOOP 

	IF8. (GREENF),.NE.,0		;WE HAVE A COUNTER GOING
	    IF8. (GREENF),.EQ.,1	;LAST TIME, TURN BACK TO MEAN
		ASIGN8. (COLFLG),1	;WE HAVE A COLOR CHANGE
		ASIGN8. (MONCOL),MAGENTA
		ASIGN8. (GREENF),0	;BACK TO MEAN
		JP MOVMON		;CARRY ON
	    ENDIF.
	    LD A,(GRETOG)		;TOGGLE (GRETOG)
	    XOR 1			;TURN 0 TO 1, 1 TO 0
	    LD (GRETOG),A		;PUT BACK IN (GRETOG)
	    AND A			;IS A ZERO ?
	    JP NZ,MOVMON		;NO, DO NOTHING, GO MOVMON
	    LD A,(GREENF)		;YES, SO DECREMENT
	    DEC A
	    LD (GREENF),A
DOFLSH:     IF8. (GREENF),.EQ.,(FLACNT)	;START A FLASH
		IF8. (MONCOL),.EQ.,DGREEN	;SWITCH TO GREY
		    ASIGN8. (MONCOL),GREY	;
		    LD A,(GREENF)		;FOR 4 INTERRUPTS = 2 COUNTS
		    SUB 2			;
		    LD (FLACNT),A		;
		    ASIGN8. (COLFLG),1		;FLAG COLOR CHANGE
		ELSE.				;SWITCH BACK TO GREEN
		    ASIGN8. (MONCOL),DGREEN	;
		    LD A,(GREENF)		;UNTIL =2 + (GREENF)/2
		    SRL A			;DIVIDE BY 2
		    ADD A,2			;ADD 2
		    LD (FLACNT),A		;PUT IN (FLACNT)
		    ASIGN8. (COLFLG),1		;WE GOTTA DO COLOR CHANGE
		ENDIF. 		;MONCOL=DGREEN OR NOT (GREY)
	    ENDIF.	;GREENF=FLACNT
	ENDIF.	;GREENF<>0

;********************************************************
MOVMON::	;MOVE MONSTERS, CHECK COLLISION, ETC.
;********************************************************
	CALL JFMON
XMNTSK:
	RET	;END OF MONTSK ROUTINE

ADDTON::
;ADD TO TONNAGE FOR LEVEL 10
	LD C,A			;SAVE AMMOUNT TO ADD
	XOR A			;CLEAR FLAGS
	LD	A,(TONNAGE)	;GET THE HIGH ORDER BYTE
	ADD A,C			;ADD AMOUNT TO A  
	DAA			;ADJUST IT TO BCD
	LD (TONNAGE),A		;PUT IT BACK
	ASIGN8. (TONFLG),1	;FLAG FOR PUTTON ON MAINLINE
	IF8. (TONNAGE),.GT.,3	;OVER 3 LEVELS
	    ADD8. (DANFLG),1	;SO WE ONLY PUT IT UP FIRST TIME
	ENDIF.

	RET	;END OF ADDTON

DODANG::
;PUT UP DANGER MESSAGE
	JP SDDANG
	TONBUF:		DS	24	;TONNAGE BUFFER FOR FAST LOAD
	TONADD:		DW	0	;WORKING ADDRESS FOR TONNAGE DISPLAY
SDDANG:
	IF8. (TONFLG),.EQ.,1
	    LD HL,NUMIES	;0 BASE OF NUMBER PATTERNS
	    LD A,(TONNAGE)	;GRAB VALUE
	    SRL A		;GET HIGH NIBBLE
	    SRL A
	    SRL A
	    SRL A
	    PUSH AF		;SAVE A IN CASE IT TURNS OUT TO BE BLANK
	    AND A		;IS IT A ZERO
	    JP NZ,DNUM1		;NO, GO GET FIRST NUMBER
	    LD A,12		;YES, PUT IN BLANK

DNUM1:	    SLA A		;MULTIPLY X 8
	    SLA A		;
	    SLA A		;
	    LD C,A		;PUT IN BC AS WORD
	    LD B,0		;
	    ADD HL,BC		;ADD TO NUMIES BASE
	    LD (TONADD),HL	;PUT IN (TONADD) FOR COP BUF
	    COPBUF (TONADD),TONBUF,8	;MOVE IT UP

;	    ****************** SECOND NUMBER *****************

	    LD HL,NUMIES	;0 BASE OF NUMBER PATTS
	    LD A,(TONNAGE)	;GRAB AMOUNT AGAIN
	    AND 00FH		;GET LOWER NIBBLE
	    POP BC		;IN CASE IT IS ZERO
	    CP 0		;IS IT 0
	    JP NZ,DNUM2		;NO, GO GET PATTERN
	    CP B		;IS FIRST DIGIT 0 [BLANK ALSO ?]
	    JP NZ,DNUM2		;NO, SO GO GET NUMBER PATTERN
	    LD A,12		;YES, PUT BLANK HERE ALSO

DNUM2:	    SLA A		;MULTIPLY X 8
	    SLA A		;
	    SLA A		;
	    LD C,A		;PUT IN BC AS WORD
	    LD B,0		;
	    ADD HL,BC		;ADD TO NUMIES BASE
	    LD (TONADD),HL	;PUT IN (TONADD) FOR COP BUF
	    COPBUF (TONADD),TONBUF+8,8	;MOVE IT UP

	    PCALL VRAMLD,16,TONBUF,656

	    ASIGN8. (TONFLG),0	;RESET AFTER UPDATING
	ENDIF.
	IF8. (DANFLG),.EQ.,1	;PUT UP DANGER
;*********** PUT UP DANGER ************
	    ASIGN16. (TONADD),(PTRADD)
	    ADD16. (TONADD),6*256+8*15
	    PCALL VRAMLD,48,TNTDAN,(TONADD)
	    ASIGN16. (TONADD),(COLADD)
	    ADD16. (TONADD),6*256+8*15
	    PCALL VFILL,48,060H,(TONADD)

	ENDIF.
	RET	;END OF DODANG

SETTON::
;PRESETS TONNAGE STUFF
	ASIGN16. (TONADD),(COLADD)
	ADD16. (TONADD),656
	PCALL VFILL,24,060H,(TONADD)	;PRESET COLOR TABLE
	COPBUF NUMIES+12*8,TONBUF,8	;PUT UP bb0	BLANK
	COPBUF NUMIES+12*8,TONBUF+8,8	;BLANK
	COPBUF NUMIES,TONBUF+16,8	;ZERO
	ASIGN16. (TONADD),(PTRADD)	;PATTERN TABLE
	ADD16. (TONADD),656		;WHERE THEY GO
	PCALL VRAMLD,24,TONBUF,(TONADD)	;PUT THEM UP	

	RET	;END OF SETTON

TRRCHG::
;	CHANGE TRANSPORTER COLORS
;	TRRCOL=1 MAKE RED
;	TRRCOL=2 MAKE GREEN
	JP STRCOL
	TRRCOL::	DB	0	;FLAGS WHAT TO DO HERE
	TR1ADD:		DW	0	;ADDRESS USED TO CHANGE COLOR
	TR2ADD:		DW	0	;ADDRESS USED TO CHANGE COLOR
	TR3ADD:		DW	0	;ADDRESS USED TO CHANGE COLOR
	TR4ADD:		DW	0	;ADDRESS USED TO CHANGE COLOR
	TRRADD:		DW	0	;WORKING ADDRESS
STRCOL:
	IF8. (TRRCOL),.EQ.,1
	  ASIGN8. (RADCOL),060H	;MAKE RED
	ELSE.
	  ASIGN8. (RADCOL),0C0H	;MAKE GREEN
	ENDIF.

	ASIGN16. (TR4ADD),02000H+256+24		;Y BASE ADDRESS FOR TRR 4 SC7
	ASIGN16. (TR3ADD),02000H+1792+24	;Y BASE ADDRESS FOR TRR 3 SC7
	ASIGN16. (TR2ADD),02000H+3328+24	;Y BASE ADDRESS FOR TRR 2 SC7
	ASIGN16. (TR1ADD),02000H+4615+24	;Y BASE ADDRESS FOR TRR 1 SC7

	IF8. (PRELEV),.EQ.,3	;IF 3 MOVE OVER 10 PATTERNS (24 -> 104)
	    ADD16. (TR4ADD),80
	    ADD16. (TR3ADD),80
	    ADD16. (TR2ADD),80
	    ADD16. (TR1ADD),80
	ENDIF.

	ASIGN8. (RAD.A),3		;TO DO TOP OF TRR 1
	ASIGN16. (TRRADD),(TR1ADD)	;TOP ADDRESS
	WHILE8. (RAD.A),.GT.,0		;3 TOP PATTERNS PIECES
	    PCALL VFILL,1,(RADCOL),(TRRADD)
	    ADD16. (TRRADD),8
	    SUB8. (RAD.A),1
	ENDW.

	ADD16. (TR1ADD),249	;GET READY FOR NEXT LOOP

	ASIGN8. (RAD.A),5	;DO 5 SETS OF PATTERNS [4 FOR TRR 1, TRR 2]
	WHILE8. (RAD.A),.GT.,0
	    IF8. (RAD.A),.GT.,1			;SKIP LAST TIME
	        PCALL VFILL,24,(RADCOL),(TR1ADD)
	        ADD16. (TR1ADD),256
	        PCALL VFILL,24,(RADCOL),(TR2ADD)	
	        ADD16. (TR2ADD),256
	    ENDIF.
	    PCALL VFILL,24,(RADCOL),(TR3ADD)	
	    ADD16. (TR3ADD),256
	    PCALL VFILL,24,(RADCOL),(TR4ADD)	
	    ADD16. (TR4ADD),256
	    SUB8. (RAD.A),1
	ENDW.

	ASIGN8. (RAD.A),3	;DO BOTTOMS OF TRR 4,2,1
	WHILE8. (RAD.A),.GT.,0	;
	    PCALL VFILL,1,(RADCOL),(TR4ADD)
	    ADD16. (TR4ADD),8		;MOVE OVER 1 PATTERN
	    PCALL VFILL,7,(RADCOL),(TR2ADD)
	    ADD16. (TR2ADD),8		;MOVE OVER 1 PATTERN
	    PCALL VFILL,6,(RADCOL),(TR1ADD)
	    ADD16. (TR1ADD),8		;MOVE OVER 1 PATTERN

	    SUB8. (RAD.A),1		;DEC COUNTER
	ENDW.

	ASIGN8. (TRRCOL),0	;RESET

	RET	;END OF TRRCOL

CKCOLL::
;	CHECK COLLISION BETWEEN "T" VARIABLES AND "BB" VARIABLES
;	EACH WITH (TOP X,TOP Y) AND (BOTTOM X,BOTTOM Y)
;	JOE'S 4 RULES OF NON-COLLISION
;	RETURNS A=1	YES COLLISION
;		A=0	NO  COLLISION

	IF8. (BBTX),.GT.,(TBOTTX)	;IF BB RIGHT OF TSE
		JP NOSUCH		;  NO CHANCE
	ENDIF.				;
	IF8. (TTOPPX),.GT.,(BBBX)	;IF TSE RIGHT OF BB
		JP NOSUCH		;  NO CHANCE
	ENDIF.				;
	IF8. (TTOPPY),.GT.,(BBBY)	;IF BB ABOVE TSE
		JP NOSUCH		;  NO CHANCE
	ENDIF.				;
	IF8. (BBTY),.GT.,(TBOTTY)	;IF TSE ABOVE BB
		JP NOSUCH		;  NO CHANCE
	ENDIF.				;

GOTCOL:
	LD A,1
	RET	
NOSUCH:
	LD A,0

	RET	;END OF CKCOLL
	
RADMAR::
;	TAKE CARE OF RADIOACTIVE MARTINI
;	IF MARFLG=1	TURN MARTSK ON		SET TO 2
;		 =2	TURN MARTSK OFF		SET TO 0

	JP SRDMAR
	MARFLG::	DB	0	;FLAG TO CALL RADMAR
	MARTCB::	DS	6	;TCB FOR MARTSK
	WASFLG::	DB	0	;FLAG TO CALL RADWAS
	WASTCB::	DS	6	;TCB FOR WASTSK
	PLUFLG::	DB	0	;FLAG TO CALL RADPLU
	PLUTCB::	DS	6	;TCB FOR PLUTSK
	RADCOL::	DB	0	;COLOR FOR MARTINI/WASTE/PLUNGERS
	COLCNT::	DB	0	;COLOR COUNTER FOR PLUNGERS
	COLORD::	DW	0	;KEEP ORDER OF PLUNGER TO COLOR
	MARADD::	DW	0	;STARTING ADDRESS OF MARTINI COLOR
	RAD.AA::	DW	0	;USED FOR WRITING TO COL TABLE
	RAD.A::		DB	0	;COUNTER
	RAD.B::		DB	0	;COUNTER	
SRDMAR:
	IF8. (MARFLG),.EQ.,2	;TURN MARTINI OFF
	  N.CLKRV MARTCB
	  ASIGN8. (MARFLG),0	;RESET
	ENDIF.
	IF8. (MARFLG),.EQ.,1	;TURN MARTINI ON
	  LD HL,(COLADD)	;COLOR TABLE BASE ADDRESS
	  LD A,160		;Y COORDINATE OF MARTINI
	  AND 0F8H		;ROUND OFF TO NEAREST 8
	  SRL A			;DIVIDE BY 8
	  SRL A
	  SRL A
	  LD B,A		;MULTIPLY BY 256 AND PUT IN BC
	  LD A,16		;X COORD OF MARTINI
	  AND 0F8H		;GET NEAREST PATTERN BOUNDARY
	  LD C,A		;ADD TO BC
	  ADD HL,BC		;ADD OFFSET [BC] TO BASE [HL]
	  LD (MARADD),HL	;SAVE IN MARADD
	  ASIGN8. (RADCOL),0	;START WITH 00
	  SETSK MARTSK,MARTCB,4,1
	  ASIGN8. (MARFLG),2	;FLAG IT
	ENDIF.

	RET	;END OF RADMAR

MARTSK::
;	PUT UP PRESENT COLOR FOR MARTINI AND CHANGE TO NEXT
	ASIGN16. (RAD.AA),(MARADD)
	ASIGN8. (RAD.A),0
	WHILE8. (RAD.A),.LT.,4
WRL:	    PCALL VRAMWR,(RAD.AA),(RADCOL)
	    ADD16. (RAD.AA),1
	    ADD8. (RAD.A),1
	ENDW.
	ADD16. (RAD.AA),4
WREND:	PCALL VRAMWR,(RAD.AA),(RADCOL)

	ADD8. (RADCOL),010H	;NEXT COLOR
	IF8. (RADCOL),.EQ.,0	;NEVER LET IT BE BLANK OR TRANSPARENT
	  ASIGN8. (RADCOL),020H	;
	ENDIF.
	
	RET	;END OF MARTSK

RADWAS::
	IF8. (WASFLG),.EQ.,2	;TURN WASTE OFF
	  N.CLKRV WASTCB
	  ASIGN8. (WASFLG),0	;RESET
	ENDIF.
	IF8. (WASFLG),.EQ.,1	;TURN WASTE ON
	  ASIGN8. (RADCOL),02	;START WITH 02
	  ASIGN8. (RAD.B),0
	  SETSK WASTSK,WASTCB,3,1	;30 FOR TEST 3 FOR REAL
	  ASIGN8. (WASFLG),2	;FLAG IT
	ENDIF.

	RET	;END OF RADWAS

WASTSK::
;	PUT UP PRESENT COLOR FOR WASTE AND CHANGE TO NEXT
	LD A,(RAD.B)			;TOGGLE (RAD.B)
	XOR 1	
	LD (RAD.B),A
	ASIGN16. (RAD.AA),(COLADD)
	ADD16. (RAD.AA),5160		;133RD PATTERN OF LAST THIRD COL TABLE
	IF8. (RAD.B),.EQ.,0			;ALTERNATE COLOR WITH WHITE
;		PCALL VRAMWR,(RAD.AA),(RADCOL)	;COLOR
		PCALL VFILL,152,(RADCOL),(RAD.AA)
		ADD16. (RAD.AA),256
		PCALL VFILL,56,(RADCOL),(RAD.AA)
	ELSE.
;	        PCALL VRAMWR,(RAD.AA),00FH	;WHITE
		PCALL VFILL,152,00FH,(RAD.AA)
		ADD16. (RAD.AA),256
		PCALL VFILL,56,00FH,(RAD.AA)

	ENDIF.

	IF8. (RAD.B),.EQ.,0	;
;	    ADD8. (RADCOL),010H	;NEXT COLOR
	    LD A,(RADCOL)	;CHANGE TO NEXT BACKGROUND COLOR
	    INC A		;NEXT ONE
	    AND 00FH		;GET RID OF HIGH NIBBLE
	    LD (RADCOL),A	;PUT BACK IN (RADCOL)
	ENDIF.
	IF8. (RADCOL),.EQ.,00FH	;NEVER LET IT BE WHITE, BLANK OR TRANSPARENT
	  ASIGN8. (RADCOL),002H	;
	ENDIF.

	RET	;END OF WASTSK

PUTWAS::
;	INITIATE RADIOACTIVE WASTE FOR STATION 6
	JP SPTWAS
;****************************************************************************
	WASTE::
		DB	001H	;MAIN PATTERN
		DB	085H,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
				;PUT AT 133 (085H) IN PATTERN TABLE
TNTDAN::
 DB 0F8H,0CCH,0CCH,0CCH,0F8H,000H,000H,000H	;DANGER
 DB 078H,0CCH,0FCH,0CCH,0CCH,000H,000H,000H
 DB 0F8H,0CCH,0CCH,0CCH,0CCH,000H,000H,000H
 DB 07CH,0C0H,0DCH,0CCH,0FCH,000H,000H,000H
 DB 0FCH,0C0H,0F8H,0C0H,0FCH,000H,000H,000H
 DB 0FCH,0CCH,0F8H,0CCH,0CCH,000H,000H,000H
DANGER::
           DB 01AH
 DB 085H,000H,000H,000H,000H,000H,000H,000H,000H	;BLANK
 DB 086H,0F8H,0CCH,0CCH,0CCH,0F8H,000H,000H,000H	;DANGER
 DB 087H,078H,0CCH,0FCH,0CCH,0CCH,000H,000H,000H
 DB 088H,0F8H,0CCH,0CCH,0CCH,0CCH,000H,000H,000H
 DB 089H,07CH,0C0H,0DCH,0CCH,0FCH,000H,000H,000H
 DB 08AH,0FCH,0C0H,0F8H,0C0H,0FCH,000H,000H,000H
 DB 08BH,0FCH,0CCH,0F8H,0CCH,0CCH,000H,000H,000H
 DB 08CH,000H,000H,000H,000H,000H,000H,000H,000H
 DB 08DH,0FCH,0CCH,0F8H,0CCH,0CCH,000H,000H,000H	;RADIOACTIVE
 DB 08EH,078H,0CCH,0FCH,0CCH,0CCH,000H,000H,000H
 DB 08FH,0F8H,0CCH,0CCH,0CCH,0F8H,000H,000H,000H
 DB 090H,0FCH,030H,030H,030H,0FCH,000H,000H,000H
 DB 091H,0FCH,0CCH,0CCH,0CCH,0FCH,000H,000H,000H
 DB 092H,078H,0CCH,0FCH,0CCH,0CCH,000H,000H,000H
 DB 093H,0FCH,0C0H,0C0H,0C0H,0FCH,000H,000H,000H
 DB 094H,0FCH,030H,030H,030H,030H,000H,000H,000H
 DB 095H,0FCH,030H,030H,030H,0FCH,000H,000H,000H
 DB 096H,0CCH,0CCH,0CCH,0CCH,078H,000H,000H,000H
 DB 097H,0FCH,0C0H,0F8H,0C0H,0FCH,000H,000H,000H
;SKIP ON TO 165
 DB 0A5H,000H,000H,000H,000H,000H,000H,000H,000H	;WASTE
 DB 0A6H,003H,003H,003H,003H,001H,000H,000H,000H
 DB 0A7H,00CH,06CH,06CH,06CH,0F8H,000H,000H,000H
 DB 0A8H,078H,0CCH,0FCH,0CCH,0CCH,000H,000H,000H
 DB 0A9H,0FCH,0C0H,0FCH,00CH,0FCH,000H,000H,000H
 DB 0AAH,0FCH,030H,030H,030H,030H,000H,000H,000H
 DB 0ABH,0FCH,0C0H,0F8H,0C0H,0FCH,000H,000H,000H

;****************************************************************************
	WX::	DB	0	;FOR WASTE COORDINATES
	WY::	DB	0	;

SPTWAS:

	ASIGN8. (RAD.A),01FH
CCTAB:
	ASIGN16. (RAD.AA),(COLADD)
	ADD16. (RAD.AA),5160
	PCALL VFILL,176,(RAD.A),(RAD.AA)  ;FILL COLOUR TABLE WITH BLACK/WHITE [01FH]
	ASIGN16. (RAD.AA),(COLADD)
	ADD16. (RAD.AA),5416
	PCALL VFILL,176,(RAD.A),(RAD.AA)  ;FILL COLOUR TABLE LINE 2
	ASIGN16. (RAD.AA),(COLADD)
	ADD16. (RAD.AA),5672
	PCALL VFILL,176,(RAD.A),(RAD.AA)  ;FILL COLOUR TABLE LINE 3
	ASIGN16. (RAD.AA),(COLADD)
	ADD16. (RAD.AA),5928
	PCALL VFILL,176,(RAD.A),(RAD.AA)  ;FILL COLOUR TABLE LINE 4

	ASIGN16. (RAD.AA),(PTRADD)	;WASTE TO PATTERN TABLE
	ADD16. (RAD.AA),4096		;BASE OF LAST THIRD
	PCALL LPATRN,DANGER,(RAD.AA)	;PUT MESSAGE IN PATTERN TABLE

	ASIGN8. (WY),20		;PUT 00H UP EVERYWHERE (WHITE)
	WHILE8. (WY),.LT.,24	;PATTERNS (5,20) TO (28,23) INCLUSIVE
	  ASIGN8. (WX),5		;PUT MAIN PATTERN UP
	  WHILE8. (WX),.LT.,27
;	    PCALL PUTPAT,(WX),(WY),085H+512	;PUT SUCKER UP
	    PCALL PUTPAT,(WX),(WY),085H		;PUT SUCKER UP
	    ADD8. (WX),1	;INC (WX)
	  ENDW.
	  ADD8. (WY),1		;INC (WY)
	ENDW.

	ASIGN8. (RAD.A),086H	;START PATTERN OF DANGER
	ASIGN8. (WX),13		;START X COORDINATE
	ASIGN8. (WY),21		;START Y COORD
	WHILE8. (WX),.LT.,19
	  PCALL PUTPAT,(WX),(WY),(RAD.A)
	  ADD8. (RAD.A),1	;NEXT PATTERN
	  ADD8. (WX),1		;KEEP COUNT
	ENDW.

	ASIGN8. (RAD.A),08DH	;START PATTERN OF RADIOACTIVE
	ASIGN8. (WX),11		;START X COORDINATE
	ASIGN8. (WY),22		;START Y COORD
	WHILE8. (WX),.LT.,22
	  PCALL PUTPAT,(WX),(WY),(RAD.A)
	  ADD8. (RAD.A),1	;NEXT PATTERN
	  ADD8. (WX),1		;KEEP COUNT
	ENDW.

	ASIGN8. (RAD.A),0A6H	;START PATTERN OF WASTE
	ASIGN8. (WX),13		;START X COORDINATE
	ASIGN8. (WY),23		;START Y COORD
	WHILE8. (WX),.LT.,19
	  PCALL PUTPAT,(WX),(WY),(RAD.A)
	  ADD8. (RAD.A),1	;NEXT PATTERN
	  ADD8. (WX),1		;KEEP COUNT
	ENDW.

	RET	;END OF PUTWAS

RADPLU::
;PUTS UP AND TAKES DOWN PLUNGER TASK [PLUTSK]
	IF8. (PLUFLG),.EQ.,2	;TURN PLUNGERS OFF
	  N.CLKRV PLUTCB
	  ASIGN8. (PLUFLG),0	;RESET
	ENDIF.
	IF8. (PLUFLG),.EQ.,1	;TURN PLUNGERS ON
	  CALL PUTPLU		;CREATE PLUNGERS
	  ASIGN8. (COLCNT),020H	;FIRST COLOR
	  ASIGN8. (RAD.B),0	;COLOR TOGGLE
	  SETSK PLUTSK,PLUTCB,1,1	; 1 FOR REAL
	  ASIGN8. (PLUFLG),2	;FLAG IT
	ENDIF.

	RET	;END OF RADPLU

PLUCOL::

;*************** CHECK COLOR CHANGE **************

	CALL PDOCOL		;PUTS UP PLUNGER COLOR WHEN REQUIRED
	ADD16. (COLORD),4	;SET UP NEXT PLUNGER
	IF16. (COLORD),.GE.,PLUTAB+16	;RESET WHEN LAST ONE HIT
	    ASIGN16. (COLORD),PLUTAB	;START WITH 1ST PLUNGER
;	    ************** CHANGE COLOR ****************
	    LD A,(RAD.B)		;TOGGLE (RAD.B)
	    XOR 1			; TO MAKE COLOR
	    LD (RAD.B),A		;  WHITE EVERY OTHER TIME
	    CP 0			;IS TOGGLE 0 ?
	    JP Z,ADDCOL			;YES, GO CHANGE COLOR
	    ASIGN8. (RADCOL),0F0H	;NO, MAKE IT WHITE
	    JP XPDCOL			;EXIT
ADDCOL:
	    ADD8. (COLCNT),010H	;NEXT COLOR
	    IF8. (COLCNT),.EQ.,0	;NEVER LET IT BE BLANK OR TRANSPARENT
	      ASIGN8. (COLCNT),020H	;
	    ENDIF.
	    ASIGN8. (RADCOL),(COLCNT)
	ENDIF.
XPDCOL:
	RET	;END OF PLUCOL

PDOCOL::

	JP SPDCOL

	COLTYP:: DW COL1,COL2,COL3,COL4 ;JUMP TABLE FOR PLUNGER TYPES

SPDCOL:
	LD IX,(COLORD)
	LD B,(IX+0)		;X COORDINATE
	LD C,(IX+1)		;Y COORDINATE
	LD A,C		;GET Y-COORD
	SRL A		;ROUND OFF TO NEAREST PATTERN NUMBER BY /8
	SRL A
	SRL A
	LD H,A		;MULTIPLY BY 256 BY PUTTING IN HL HIGH NIBBLE
	LD L,B		;GET X-COORD , ADD TO HL
	LD (OFFSET),HL	;SAVE PATTERN OFFSET

	LD A,C		;GET Y-COORDINATE AGAIN
	AND 007H	;ROUND OFF TO NEAREST [0,2,4,6]
	LD B,0		;PUT IN BC AS WORD
	LD C,A		;

	LD HL,COLTYP	;LOAD PLUNGER TYPE JUMP TABLE
	ADD HL,BC	;  AND ADD TYPE OFFSET
	LD C,(HL)	;GRAB ADDRESS OF JUMP (LOW BYTE)
	INC HL		; HIGH BYTE
	LD B,(HL)	;PUTTING IN BC
	PUSH BC		;MOVE FROM BC
	POP HL		; TO HL
	JP (HL)		;JUMP TO PROPER PICTURE SEGMENT

COL1:
	PCALL ADDOFF,0,0	
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,0		;GREEN
	PCALL VFILL,2,0C0H,(PCADD)

	PCALL ADDOFF,0,2	
	PCALL VFILL,4,(RADCOL),(PCADD)	;(RADCOL)

	PCALL ADDOFF,8,2		;(RADCOL)
	PCALL VFILL,4,(RADCOL),(PCADD)

	JP XPCOL	;EXIT 
COL2:
	PCALL ADDOFF,0,2
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,2
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,0,4		;(RADCOL)
	PCALL VFILL,4,(RADCOL),(PCADD)

	PCALL ADDOFF,8,4		;(RADCOL)
	PCALL VFILL,4,(RADCOL),(PCADD)

	JP XPCOL	;EXIT 

COL3:
	PCALL ADDOFF,0,4
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,4
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,0,6
	PCALL VFILL,2,(RADCOL),(PCADD)	;(RADCOL)

	PCALL ADDOFF,8,6
	PCALL VFILL,2,(RADCOL),(PCADD)	;(RADCOL)

	PCALL ADDOFF,0,256
	PCALL VFILL,2,(RADCOL),(PCADD)	;(RADCOL)

	PCALL ADDOFF,8,256
	PCALL VFILL,2,(RADCOL),(PCADD)

	JP XPCOL	;EXIT 

COL4:
	PCALL ADDOFF,0,6
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,6
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,0,256
	PCALL VFILL,6,(RADCOL),(PCADD)	;(RADCOL)

	PCALL ADDOFF,8,256
	PCALL VFILL,6,(RADCOL),(PCADD)	;(RADCOL)

XPCOL:

	RET	;END OF PCOCOL

PLUTSK::
;TASK TO MOVE PLUNGERS AND CHECK COLLISION WITH BOB

;************* BOB COORDINATES *****************
	LD A,(BOBX)		;GET BOBX
	ADD A,2			;ADJUST FOR LEFT BODY
	LD (BBTX),A		;PUT IN (BBTX)
	ADD A,11		;ADJUST FOR RIGHT OF BODY
	LD (BBBX),A		;PUT IN (BBBX)
	LD A,(BOBY)		;GET BOBY
	DEC A			;ADJUST FOR FEET
	LD (BBBY),A		;PUT IN (BBBY)
	SUB 20			;ADJUST FOR HEAD
	LD (BBTY),A		;PUT IN (BBTY)
;****************** GO THROUGH PLUNGER TABLE *****************

	ASIGN16. (PLUPTR),PLUTAB 	;INITIALIZE PLUNGER TABLE POINTER

PLULOO:
	LD IX,(PLUPTR)		;BASE ADDRESS OF ENTRY
	LD A,(IX+0)		;FIRST FIELD OF ENTRY
	CP 0			;IS IT END OF TABLE
	JP Z,XPLTSK		;YES, EXIT LOOP

;************ CHECK FOR PLUNGER / BOB COLLISION *******************
	LD A,(IX+0)		;X COORD
	LD (TTOPPX),A		;TOP X
	ADD A,15		;RIGHT EDGE
	LD (TBOTTX),A		;BOTTOM X
	LD A,(IX+1)		;Y COORD
	ADD A,2			;TOP Y
	LD (TTOPPY),A		;
	ADD A,5			;FOR BOTTOM Y
	LD (TBOTTY),A		;
	CALL CKCOLL		;RETURNS A=0 FOR NO , A=1 FOR YES COLLISION
	CP 0			;DO WE HAVE A COLLISION		
	JP Z,PLUOK		;NO, CARRY ON
PLUBBC:
	IF CCP		;FOR TESTING
	  ASIGN8. (NXT_GUY),1	;INSTANT DEATH
	ENDIF
	ASIGN8. (RADIOACTIVE),1 ;BB IS DEAD
PLUOK:
	LD A,(IX+3)		;NO, TEST TIME COUNTER
	CP 0			;ARE WE MOVING ?
	JP Z,MOVPLU		;YES, GO CHANGE Y
	DEC A			;NO DECREMENT TIMER
	LD (IX+3),A		;PUT IT BACK
	JP INCPLU		;JUST LEAVE ALONE 
MOVPLU:			;CHANGE Y ACCORDING TO DIRECTION
	LD A,(IX+2)		;PLUNGER DIRECTION 0=UP, 1=DOWN
	CP 0			;ARE WE GOING UP
	JP Z,PLUUP		;YES, GO PLUNGER UP
PLUDWN:			;NO,PLUNGER DOWN
	LD A,(IX+1)		;PRESENT Y-COORD
	ADD A,2			;MOVE 2 PIXELS AT A TIME
	LD (IX+1),A		;PUT IT BACK
	CP 180			;ARE WE ALL THE WAY DOWN ?
	JP NZ,DRAPLU		;NO, GO DRAW IT
	LD (IX+2),0		;YES, CHANGE DIRECTION TO UP
	JP DRAPLU		;GO DRAW IT	
PLUUP:				;PLUNGER MOVING UP
	LD A,(IX+1)		;PRESENT Y-COORD
	SUB 2			;MOVE 2 PIXELS AT A TIME
	LD (IX+1),A		;PUT IT BACK
	CP 158			;ARE WE ALL THE WAY UP ?
	JP NZ,DRAPLU		;NO, GO DRAW IT
	LD (IX+2),1		;YES, CHANGE DIRECTION TO DOWN
	LD A,(PLUTOT)		;RESET TIMER
	LD (IX+3),A		;

DRAPLU:			;NOW DRAW IT

	LD B,(IX+0)		;X COORDINATE
	LD C,(IX+1)		;Y COORDINATE
	CALL PLUPIC		;PUTS UP PLUNGER PICTURE

INCPLU:

	ADD16. (PLUPTR),4	;INCREMENT TO NEXT ENTRY
	JP PLULOO		;CONTINUE LOOP

XPLTSK:

	CALL PLUCOL		;PUTS UP PLUNGER COLOR WHEN REQUIRED

	RET	;END OF PLUTSK

CKLIM::
;CHECK LIMITS (WALLS AND ROOFS) OF BOUNTY BOB
;RETURNS A=0 NO COLLISION
;	 A=1 YES COLLISION
;	C=BOB X
;	E=BOB Y

;************* BOB COORDINATES *****************
	LD A,C			;GET BOBX
	ADD A,2			;ADJUST FOR LEFT BODY
	LD (BBTX),A		;PUT IN (BBTX)
	ADD A,11		;ADJUST FOR RIGHT OF BODY
	LD (BBBX),A		;PUT IN (BBBX)
	LD A,E			;GET BOBY
	DEC A			;ADJUST FOR FEET
	LD (BBBY),A		;PUT IN (BBBY)
	SUB 20			;ADJUST FOR HEAD
	CP 192			;DID WE WRAP AROUND THE TOP ? (DOWN 0FFH)
	JP C,OKUP		;LESS THAN 192 (SO OK)
	LD A,1			;TRY THIS
OKUP:
	LD (BBTY),A		;PUT IN (BBTY)
	ASIGN16. (LASWAL),WALTAB	;INITIALIZE POINTER
LIMLOO:
	LD IX,(LASWAL)			;TABLE POINTER
	LD A,(IX+0)			;FIRST ENTRY
	CP 0				;CHECK FOR END OF TABLE ??
	JP Z,OKLIM			;YES, EXIT
;*********** WALL/ROOF COORDINATES *****************
	LD A,(IX+0)		;X COORD
	LD (TTOPPX),A		;TOP X
	LD A,(IX+1)		;TOP Y
	LD (TTOPPY),A		;TOP Y
	LD A,(IX+2)		;BOTTOM X	
	LD (TBOTTX),A		;BOTTOM X
	LD A,(IX+3)		;BOTTOM Y
	LD (TBOTTY),A		;
;***********************************************

	CALL CKCOLL		;RETURNS A=0 FOR NO , A=1 FOR YES COLLISION
	CP 0			;DO WE HAVE A COLLISION		
	JP NZ,BADLIM		;YES, GO BADLIM
	ADD16. (LASWAL),4	;NO INCREMENT POINTER 
	JP LIMLOO		;CARRY ON

BADLIM:
	LD A,1			;YES COLLISION
	RET			;EXIT
OKLIM:
	LD A,0			;NO COLLISION
	RET	;END OF CKLIM

PLUPIC::
;	B=X COORDINATE
;	C=Y COORDINATE
;	HL=PATTERN TABLE BASE ADDRESS

	JP SPLPIC

	PLUTYP:: DW TYPE1,TYPE2,TYPE3,TYPE4 ;JUMP TABLE FOR PLUNGER TYPES
	PPADD::	 DW	0	;PLUNGER PICTURE ADDRESS
	PCADD::	 DW	0	;PLUNGER COLOR ADDRESS
	PPBASE::	 DW	0	;PLUNGER PICTURE BASE
	PCBASE::	 DW	0	;PLUNGER COLOR BASE
	OFFSET::	DW	0	;OFFSET TO ADD TO TABLE BASES

SPLPIC:
;	GET EXACT PATTERN ADDRESS TO START PICTURE
;	EXACT= BASE ADDRESS [PATTERN/COLOR TABLE BASE ADDRESS]
;	     + (Y-ROUND OFF) * 256 + LEFT-OVER
;	     + X-COORD

	LD A,C		;GET Y-COORD
	SRL A		;ROUND OFF TO NEAREST PATTERN NUMBER BY /8
	SRL A
	SRL A
	LD H,A		;MULTIPLY BY 256 BY PUTTING IN HL HIGH NIBBLE
	LD L,B		;GET X-COORD , ADD TO HL
	LD (OFFSET),HL	;SAVE PATTERN OFFSET

	LD A,C		;GET Y-COORDINATE AGAIN
	AND 007H	;ROUND OFF TO NEAREST [0,2,4,6]
	LD B,0		;PUT IN BC AS WORD
	LD C,A		;

	LD HL,PLUTYP	;LOAD PLUNGER TYPE JUMP TABLE
	ADD HL,BC	;  AND ADD TYPE OFFSET
	LD C,(HL)	;GRAB ADDRESS OF JUMP (LOW BYTE)
	INC HL		; HIGH BYTE
	LD B,(HL)	;PUTTING IN BC
	PUSH BC		;MOVE FROM BC
	POP HL		; TO HL
	JP (HL)		;JUMP TO PROPER PICTURE SEGMENT

TYPE1:
	PCALL ADDOFF,0,0	
	PCALL VRAMLD,16,P1L,(PPADD)	;MOVE PICTURE UP
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,0		;GREEN
	PCALL VFILL,2,0C0H,(PCADD)


	JP XPLPIC	;EXIT 
TYPE2:
	PCALL ADDOFF,0,2
	PCALL VRAMLD,6,P2LT,(PPADD)	;(PPADD) PRESET ABOVE
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,2
	PCALL VRAMLD,6,P2RT,(PPADD)	;MOVE PIC UP
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,0,256
	PCALL VRAMLD,2,P2LB,(PPADD)	;MOVE PIC UP

	PCALL ADDOFF,8,256
	PCALL VRAMLD,2,P2RB,(PPADD)	;MOVE PIC UP

	JP XPLPIC	;EXIT 

TYPE3:
	PCALL ADDOFF,0,4
	PCALL VRAMLD,4,P3LT,(PPADD)	;(PPADD) PRESET ABOVE
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,4
	PCALL VRAMLD,4,P3RT,(PPADD)	;MOVE PIC UP
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,0,256
	PCALL VRAMLD,4,P3LB,(PPADD)	;MOVE PIC UP

	PCALL ADDOFF,8,256
	PCALL VRAMLD,4,P3RB,(PPADD)	;MOVE PIC UP

	JP XPLPIC	;EXIT 

TYPE4:
	PCALL ADDOFF,0,6
	PCALL VRAMLD,2,P4LT,(PPADD)	;(PPADD) PRESET ABOVE
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,8,6
	PCALL VRAMLD,2,P4RT,(PPADD)	;MOVE PIC UP
	PCALL VFILL,2,0C0H,(PCADD)	;GREEN

	PCALL ADDOFF,0,256
	PCALL VRAMLD,6,P4LB,(PPADD)	;MOVE PIC UP

	PCALL ADDOFF,8,256
	PCALL VRAMLD,6,P4RB,(PPADD)	;MOVE PIC UP

XPLPIC:

	RET	;END OF PLUPIC

ADDOFF::
;ADD 
;	(PPADD)=(PTRADD)+(OFFSET)+BC+DE

	LD HL,(OFFSET)	;START WITH PATTERN OFFSET
	ADD HL,BC	;ADD X SHIFT
	ADD HL,DE	;ADD Y SHIFT
	PUSH HL		;SAVE

	LD BC,(PTRADD)	;GET PATTERN TABLE BASE
	ADD HL,BC	;ADD TO NEW OFFSET
	LD (PPADD),HL	;PUT IN (PPADD)

	POP HL		;RESTORE NEW OFFSET
	LD BC,(COLADD)	;GET COLOR TABLE BASE
	ADD HL,BC	;ADD TO NEW OFFSET
	LD (PCADD),HL	;PUT IN (PCADD)

	RET	;END OF ADDOFF

PUTPLU::
;	CREATE THE INITIAL PLUNGERS AND TABLES
	JP SPTPLU
	SEED::	DW	0	;SEED FOR RANDOM NUMBER GENERATOR
	I.C:	DB	0	;JUST A LOCAL COUNTER
	J.C:	DB	0	; "   
	PLUPTR:		DW	0	;POINTER TO PLUNGER TABLE
	PLUTOT:		DB	0	;TOTAL WAITING TIME
	PLUN::	DB	2,1
		DW	PLUNC
 		DB      007H,007H,03FH,03FH,0FFH,0FFH,000H,000H
 		DB      0E0H,0E0H,0FCH,0FCH,0FFH,0FFH,000H,000H
	PLUNC:	DB	020H,020H
	PLC1:	DB	0F0H,0F0H,0F0H,0F0H,0F0H,0F0H
		DB	020H,020H
	PLC2:	DB	0F0H,0F0H,0F0H,0F0H,0F0H,0F0H
	PLUTAB::	DS	17	;4*4+1
	P1L: 	DB      007H,007H,03FH,03FH,0FFH,0FFH,000H,000H ;TYPE 1 LEFT
 	P1R:	DB      0E0H,0E0H,0FCH,0FCH,0FFH,0FFH,000H,000H ;TYPE 1 RIGHT

	P2LT: 	DB      007H,007H,03FH,03FH,0FFH,0FFH 		;TYPE 2 L TOP
	P2LB:	DB	000H,000H				; "   2 L BOTT
 	P2RT:	DB      0E0H,0E0H,0FCH,0FCH,0FFH,0FFH 		;TYPE 2 R TOP
	P2RB:	DB	000H,000H				; "   2 R BOTT

	P3LT: 	DB      007H,007H,03FH,03FH			;TYPE 3 L TOP
	P3LB:	DB	0FFH,0FFH,000H,000H			; "   3 L BOTT
 	P3RT:	DB      0E0H,0E0H,0FCH,0FCH			;TYPE 3 R TOP
	P3RB:	DB	0FFH,0FFH,000H,000H			; "   3 R BOTT

	P4LT: 	DB      007H,007H				;TYPE 4 L TOP
	P4LB:	DB	03FH,03FH,0FFH,0FFH,000H,000H		; "   4 L BOTT
 	P4RT:	DB      0E0H,0E0H				;TYPE 4 R TOP
	P4RB:	DB	0FCH,0FCH,0FFH,0FFH,000H,000H		; "   4 R BOTT


SPTPLU:
	CALL INITRAN	;INITIALIZES SEED FOR RANDOM NUMBER GENERATOR
	ASIGN8. (PLUTOT),180	;PLUNGER TOTAL TIME BASE
	LD BC,(PREZON)	;SUBTRACT ZONE TIMES 12
	LD B,0		;PUT (PREZON) IN BC AS WORD
	LD DE,12	;TO MULTIPLY TIMES 12
	CALL MUL816	;DO MULTIPLICATION
	LD A,C		;ANSWER IN BC
	LD (RAD.A),A	;PUT IN (RAD.A)
	SUB8. (PLUTOT),(RAD.A)	;TOTAL WAITING TIME FOR THIS ZONE
	ASIGN8. (RAD.A),80	;X COORD OF FIRST PLUNGER
;	ASIGN8. (RAD.B),162	;Y COORD OF PLUNGERS
	ASIGN8. (RAD.B),158	;Y COORD OF PLUNGERS
	ASIGN8. (I.C),0		;START COUNTER
	ASIGN8. (J.C),2		;FOR ORDERING INTERVALS
	ASIGN16. (PLUPTR),PLUTAB	;INITIATE PLUNGER TABLE POINTER
	WHILE8. (I.C),.LT.,4	;FOUR PLUNGERS
		PCALL DOPIC,(RAD.A),(RAD.B),PLUN
		PCALL PLUENT,(RAD.A),(RAD.B),(PLUTOT)
		ADD8. (RAD.A),32	;MOVE ACCROSS 2 PATTERNS
		ADD8. (I.C),1
		ADD8. (J.C),2
	ENDW.
	ASIGN16. (COLORD),PLUTAB	;START WITH 1ST PLUNGER

	RET	;END OF PUTPLU

PLUENT::
;CREATE TABLE ENTRY FOR PLUNGER
;
;	BC=X COORD
;	DE=Y COORD
;	HL=PLUTOT [WAITING TIME]
;
;	  0   1    2     3
;	----------------------
;	| X | Y | DIR | TIME |
;	----------------------

	LD IX,(PLUPTR)		;ADDRESS OF TABLE ENTRY
	LD (IX+0),C		;PUT IN ENTRY X COORD
	LD (IX+1),E		;Y- COORD
	LD (IX+2),1		;DIRECTION STARTS AS DOWN
	LD A,R
	CALL RANDOM		;RANDOM NUMBER RETURNED IN A
	LD C,A			;PUT IN BC AS WORD FOR MULTIPLICATION
	LD B,0			;MAKE IT A WORD
	LD DE,(PLUTOT)		;POSSIBLE TOTAL WAIT
	LD D,0			;MAKE IT A WORD
	CALL MUL816		;DO MULTIPLICATION
	LD A,(J.C)		;GRAB ORDERING MASK
	OR 0F0H			;TURN HIGH NIBLE ON
	LD C,A			;PUT IN C
	LD A,B			;GRAB RESULT OF MULTIPLICATION / 256
	AND C			;MAKE SURE WE HAVE COUNTS 1 APPART
	LD (IX+3),A		;PUT IN ENTRY
	LD (IX+4),0		;END OF TABLE
	ADD16. (PLUPTR),4	;READY FOR NEXT TABLE ENTRY
		
	RET	;END OF PLUTAB

MUL816::
		LD B,08H	;B IS BIT COUNTER
		LD HL,0000H	;SET RESULT TO 0
MULTA:		SRL C		;SHIFT MULTIPLIER BIT INTO CARRY
		JR NC,$ + 3	;TEST CARRY
		ADD HL,DE	;ADD MULTIPLICAND TO RESULT
		SLA E		;SHIFT MULTIPLICAND LEFT
		RL D		;SAVE BIT IN D
		DJNZ MULTA	;LOOP AGAIN IF COUNTER NOT ZERO
		LD B,H		;PUT RESULT IN BC 
		LD C,L
		RET		;END OF MUL816

	ENÄ	;ENÄ OÆ PROGRAM

