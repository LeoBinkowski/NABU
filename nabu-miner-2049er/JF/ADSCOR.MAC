	.Z80
	.RADIX	10
	.XLIST
;****************************************************************************
;	SCORING SUBROUTINES FOR MINER 2049ER
;	17MAY84 JF	NEW SSTART FOR FREE MAN
;	11MAY84 JF	CHANGED PLAYER TO PREPLA IN ASCII SCORE STUFF
;	10MAY84 JF	DEATH PROBLEM... LET LEO SET PLAY1/PLAY2
;	08MAY84	JF	SOUNDS:		SBONUS=BONUS POINTS
;					SSTART=FREE EXTRA MAN
;	23APR84
;****************************************************************************
	CCP	EQU	0	;0=LEO PRODUCTION, 1=JF TEST

	INCLUDE CLKAT.MRO
	INCLUDE COMMON.MRO
	INCLUDE	STRUCT.MRO
	INCLUDE LINKTAB.EXA
	INCLUDE ARITH.MRO

FILL	MACRO	FRADD,BYTES,WHAT
	LOCAL	ZEROM,LOOP
ZEROM:	LD B,BYTES		;LENGTH OF BUFFER TO CLEAR
	LD HL,FRADD		;BASE ADDRESS
LOOP:	LD (HL),WHAT		;LOAD INTO (HL) GIVEN VALUE WHAT
	INC HL			;INCREMENT BUFFER POINTER
	DJNZ LOOP		;DEC B, JUMP TO LOOP IF NOT ZERO
	ENDM

COPBUF	MACRO	FRADD,TOADD,TOTBTS
	LD DE,TOADD
	LD HL,FRADD
	LD BC,TOTBTS
	LDIR
	ENDM



	.LIST

	EXTRN	PUTPAT,SCORUN
	EXTRN	INIT_SCR,ENDGAM,DDPLAY,NXT_GUY
	EXTRN	TIMTSK,DISTIM,SCORUN,TIMER,TICKY
	EXTRN	MARFLG,WASFLG,PLUFLG,RADIOACTIVE,TIMER
	EXTRN	SBONUS,SSTART

;SCORE
	FIRST::		DB	0	;FIRST TIME UP FLAG
	NUMPLA::			;FOR LEO'S REFERENCE
	PLANUM::	DB	1	;NUMBER OF PLAYERS 1-2
	PREPLA::	DB	1	;PRESENT PLAYER
	SCORE::		DB	0,0,0	;3 DIGIT BDC SCORE OF PRESENT PLAYER
	XMAN::		DB	0	;XMAN FLAG FOR CURRENT PLAYER
	SCOR1::		DB	0,0,0	;3 DIGIT BDC SCORE FOR PLAYER 1
	XMAN1::		DB	0	;XMAN FLAG FOR PLAYER 1
	SCOR2::		DB	0,0,0	;3 DIGIT BDC SCORE FOR PLAYER 2
	XMAN2::		DB	0	;XMAN FLAG FOR PLAYER 2
	JHISCO::	DB	0,0,0	;3 DIGIT BDC HIGH SCORE 
	ZONE1::		DB	1	;WHAT ZONE PLAY1 IS ON
	ZONE2::		DB	1	;WHAT ZONE PLAY2 IS ON
	LEV1::		DB	1	;WHAT LEVEL PLAY1 IS ON
	LEV2::		DB	1	;WHAT LEVEL PLAY2 IS ON
	PLAY1::		DB	3	;HOW MANY LIVES LEFT FOR PLAY1
	PLAY2::		DB	0	;HOW MANY LIVES LEFT FOR PLAY2
	SCOBUF::	DS	11	;WORKING SCORE BUFFER
	SCBUZZ::			;MARK END OF SCOBUF
	SBUF1::		DS	11	;SCORE BUFFER FOR PLAYER 1
	SBUF2::		DS	11	;SCORE BUFFER FOR PLAYER 1
	SBU2ZZ::			;END OF SBUF2
	SCOFLG::	DB	0	;FLAG FOR SCORUN TO PUT UP SCORE
	SCOTCB::	DS	6	;SCOTSK TASK CONTROL BLOCK
	LEN::		DB	0	;LENGTH OF SCORE SCOBUF
	J:		DB	0	;LOCAL TEMPORARY (BYTE)
	JJ:		DW	0	;LOCAL TEMPORARY (WORD)
	K:		DB	0	;LOCAL TEMPORARY
	LASTIM::	DB	0	;LAST TIME SCORE UPDATED
	BLKSCO::	DB	0	;FLAG SCORUN TO ERASE SCORE
	XMANFG::	DB	0	;FLAG TO TURN XMATSK ON/OFF
	XMATCB::	DS	6	;TCB FOR XMATSK
	REDTOG::	DB	0	;FLAG FOR XMATSK TOGGLE
	ASCBUF::	DS	6	;ASCII WORK BUFFER
	IF CCP	;FOR JF TEST ONLY
	  SCORE1::	DS	6	;ASCII SCORE BUFFER FOR QA
	  SCORE2::	DS	6	;ASCII SCORE BUFFER FOR QA
	  HISCOR::	DS	6	;ASCII SCORE BUFFER FOR QA
	ENDIF
	IFF CCP	;FOR PRODUCTION
	  EXTRN	SCORE1,SCORE2,HISCOR
	ENDIF
	PLAYER::	DB	0	;=PREPLA
	BUF1A::		DS	6	;???

ADSCOR::
	CALL	ACTADD		;DO ACTUAL ADDING OF BC TO SCORE
	CALL	FREMAN		;GIVE PLAYER FREE MEN IF SCORE HIGH ENOUGH
	CALL	DISCOR		;UPDATE HIGHSCORE AND PUT UP PLAYER'S SCORE

	RET


;***************************************************************************
;
;ROUTINE : 	ACTADD
;
;DESCRIPTION : 	THIS ROUTINE ADDS A BCD NUMBER PASSED IN THE BC REGISTER TO
;		THE VARIABLE 'SCORE', ALSO BCD.  'SCORE' IS 3 BYTES LONG,
;		TO HANDLE 5 DIGITS.
;
;PARAMETERS PASSED: BC = 4-DIGIT BCD NUMBER
;
;***************************************************************************
; CHANGE LOG:
; ----------
; 84-03-02  TMN  BORROWED FROM LEO'S 'HTOD' CODE.
;
;****************************************************************************

ACTADD::
	XOR	A		;CLEAR FLAGS
	LD	A,(SCORE)	;GET THE LOW ORDER BYTE
	ADD	A,C		;ADD THE LOW ORDER ONE TO IT
	DAA			;ADJUST IT TO BCD
	LD	(SCORE),A	;PUT IT BACK
	LD	A,(SCORE+1)	;GET THE HIGH ORDER OF THE SCORE
	ADC	A,B		;ADD THE HIGH ORDER 
	DAA			;ADJUST IT
	LD	(SCORE+1),A	;PUT IT BACK
	LD	A,(SCORE+2)	;GET THE NEXT HIGHER ORDER
	ADC	A,0		;JUST ADD THE CARRY TO IT
	DAA			;ADJUST IT
	LD	(SCORE+2),A	;PUT IT BACK
	;
	RET

;***************************************************************************
;
;ROUTINE : 	FREMAN
;
;DESCRIPTION : 	THIS ROUTINE CHECKS THE SCORE AND PUTS UP A FREE MAN AT
;		10,000 POINTS
;
;***************************************************************************
; CHANGE LOG:
;
;****************************************************************************

FREMAN::
	IF8. (XMAN),.EQ.,1	;ALREADY PUT EXTRA MAN SO EXIT
		JP XFREMAN
	ENDIF.
	LD A,(SCORE+2)		;HIGHEST ORDER BYTE
	CP 1			;IS IT 10,000 OR HIGHER ?
	JP C,XFREMAN		;TOO SHORT SO EXIT
	ASIGN8. (XMAN),1	;FLAG WE GOT EXTRA MAN
	ASIGN8. (XMANFG),5	;FLAG FOR MAINLINE TO ATTACH TASK
	ASIGN8. (REDTOG),0	;START IT RED
	ASIGN8. (SSTART),4	;TURN SOUND ON [I TURN OFF]
	SETSK XMATSK,XMATCB,15,1	;ATTACH BACK-GROUND TOGGLE
	IF8. (PREPLA),.EQ.,1	;GIVE HIM EXTRA LIFE
		ADD8. (PLAY1),1
	ELSE. 
		ADD8. (PLAY2),1
	ENDIF.
	
XFREMAN:
	 RET

XMATSK::
;	(XMANFG)=5 TURN ON
;		=1 TURN OFF
;		=0 IGNORE
;	ALTERNATE BACKGROUND BLUE/BLACK
	IF8. (XMANFG),.EQ.,1		;IN CASE WE DON'T TURN OFF IN TIME
	  JP XXMA			;GET OUT
	ENDIF.

	LD A,(REDTOG)			;CURRENT VALUE OF TOGGLE
	XOR 1				;TOGGLE IT
	LD (REDTOG),A			;REPLACE IN (REDTOG)
	JP NZ,XREDB			;IF NOT 0 GO XREDB
XBLKB:
	SETCOLR	BLACK			;SET BACK-GROUND COLOR DARK RED
	SUB8. (XMANFG),1		;COUNT TOGGLES
	JP XXMA
XREDB:
	SETCOLR	DBLUE			;SET BACK-GROUND COLOR BLACK
XXMA:	

	RET

;***************************************************************************
;
;ROUTINE : 	DISCOR
;
;DESCRIPTION : 	THIS ROUTINE WILL DISPLAY THE CURRENT SCORE ON THE SCREEN
;		AND DETERMINE IF ITS A HIGHSCORE
;
;***************************************************************************
; CHANGE LOG:
; ----------
; 
;****************************************************************************

DISCOR::  ;************* FIRST CONVERT BCD (SCORE) TO PAT#S (DIG1 - DIG5) *****
	FILL SCOBUF,11,12		;BLANK OUT BUFFER
	LD	HL,(SCORE)		;GET THE CURRENT SCORE
	LD	A,L			;GET THE LOW ORDER BYTE	
	AND	0FH			;MASK OUT UPPER NYBBLE
;	ADD	A,14H			;CHANGE IT TO A PATTERN # FROM 14H-1DH
	LD	(SCOBUF+10),A		;STORE IT AWAY
	LD	A,L			;GET THE LOWER BYTE
	SRL	A			;MOVE THE HIGH NYBBLE INTO THE LOWER
	SRL	A
	SRL	A
	SRL	A
;	ADD	A,14H			;CHANGE IT TO PAT#
	LD	(SCOBUF+9),A		;STORE IT AWAY
	LD	A,H			;GET HIGH ORDER BYTE
	AND	0FH			;MASK OUT UPPER NYBBLE
;	ADD	A,14H			;CHANGE IT TO PAT#
	LD	(SCOBUF+8),A		;STORE IT AWAY
	LD	A,H			;GET THE HIGH BYTE
	SRL	A			;MOVE THE UPPER NYBBLE INTO THE LOWER
	SRL	A
	SRL	A
	SRL	A
;	ADD	A,14H			;CHANGE IT TO PAT#
	LD	(SCOBUF+7),A		;STORE IT AWAY
	LD	HL,(SCORE+2)
	LD 	A,L
	AND	0FH
;	ADD	A,14H
	LD	(SCOBUF+6),A
	LD	A,L			;GET THE LOWER BYTE
	SRL	A			;MOVE THE HIGH NYBBLE INTO THE LOWER
	SRL	A
	SRL	A
	SRL	A
;	ADD	A,14H			;CHANGE IT TO PAT#
	LD	(SCOBUF+5),A		;STORE IT AWAY

;
DISNUM:
	;THIS PART WILL DETERMINE IF SCORE1 OR SCORE2 IS THE HIGH SCORE: *****
	;
	;

	;********** FIRST DESTROY ALL LEADING ZEROS **************************
    ASIGN8. (J),0			;WILL WE NEED A COMMA ? COUNT ZEROS
    IF8. (SCOBUF+5),.EQ.,0
    ASIGN8. (SCOBUF+5),12 
    ADD8. (J),1				;GOT ONE LEADING ZERO
      IF8.	(SCOBUF+6),.EQ.,0
      ASIGN8. (SCOBUF+6),12		;(REPLACE WITH BLANKS)
      ADD8. (J),1			;GOT TWO LEADING ZEROS
	IF8.	(SCOBUF+7),.EQ.,0		
	  ASIGN8. (SCOBUF+7),12
	  ADD8. (J),1			;3 0'S
	  IF8.    (SCOBUF+8),.EQ.,0
	    ASIGN8. (SCOBUF+8),12
	    ADD8. (J),1			;4 0'S
	    IF8.    (SCOBUF+9),.EQ.,0
	      ASIGN8. (SCOBUF+9),12
	      ADD8. (J),1		;5 0'S
;	      IF8.    (SCOBUF+10),.EQ.,0
;		ASIGN8.	(SCOBUF+10),12
;	  	ADD8. (J),1		;6 0'S [POOR SCORE, BUBBA]
;	      ENDIF.
	    ENDIF.
	  ENDIF.
	ENDIF.
      ENDIF.
    ENDIF.

	LD BC,6		;COUNT 6 POSITIONS
	LD DE,ASCBUF	;DESTINATION
	LD HL,SCOBUF+5	;SOURCE
JFL1:	LD A,(HL)	;
	CP 12		;IS IT A BLANK [12]
	JP NZ,JFLOK	;NO, JUST MAKE CONVERSION
	LD A,020H	;YES, MAKE IT ASCII BLANK
	JP JFCOP	;GO COPY IT
JFLOK:	ADD A,030H	;MAKE IT ASCII
JFCOP:	LD (DE),A	;PUT IT IN (DE)
	INC HL		;INCREMENT SOURCE ADDRESS
	INC DE		;INCREMENT DESTINATION ADDRESS
	DEC C		;DECREMENT COUNTER
	JP NZ,JFL1	;CONTINUE LOOP IF NOT ZERO

	ASIGN8. (LEN),6		;ASSUME MAX LEN
	SUB8. (LEN),(J)		;ADJUST LEN BY SUBTRACTING LEADING 0'S
	IF8. (J),.LT.,3		;WE HAVE LESS THAN 3 LEADING ZEROS, NEED COMMA
		LD A,(SCOBUF+5)		;MOVE OVER, ROVER
		LD (SCOBUF+4),A		;1
		LD A,(SCOBUF+6)		;MOVE OVER, ROVER
		LD (SCOBUF+5),A		;2
		LD A,(SCOBUF+7)		;MOVE OVER, ROVER
		LD (SCOBUF+6),A		;3
		LD A,10			;COMMA
		LD (SCOBUF+7),A		;
		ADD8. (LEN),1	;ADJUST LENGTH AGAIN
	ENDIF.
;PUT HATS ON
	IF8. (PREPLA),.EQ.,1		;FOR PRESENT PLAYER
		ASIGN8. (J),(PLAY1)	;GET LIVES LEFT
		FILL SBUF1,11,12	;BLANK OUT BUFFER
	ELSE.
		ASIGN8. (J),(PLAY2)	;
		FILL SBUF2,11,12	;BLANK OUT BUFFER
	ENDIF.
	IF8. (PREPLA),.EQ.,1		;IF PLAYER IS 1 (LEFT JUSTIFY)
		LD HL,SCBUZZ			;END OF SCOBUF
		LD BC,(LEN)			;POINT TO BEGINNING OF NUMBER
		LD B,0				;MAKE BC WORD
		AND A				;CLEAR CARRY FLAG
		SBC HL,BC		;SUBTRACT LENGTH FROM END OF BUFFER
					;HL NOW POINTING TO FIRST DIGIT
		LD DE,SBUF1		;START AT SBUF1
		LD BC,(LEN)		;COPY BUFFER LENGHT=(LEN)
		LD B,0			;
		LDIR			;NOW DE POINTS TO WHERE BLANK GOES
		PUSH DE			;SO GRAB IT
		POP HL
		LD (HL),12		;PUT IN BLANK [12]
		WHILE8. (J),.GT.,1	;DON'T SHOW HAT FOR CURRENT LIFE
		  INC HL		;NEXT SPOT
		  LD (HL),11		;PUT IN HAT [11]
		  SUB8. (J),1
		ENDW.
	ELSE.				;PLAYER 2 (RIGHT JUSTIFY)
		LD HL,SCBUZZ		;END OF SCOBUF
		DEC HL			;NOW POINTING TO LAST DIGIT
		LD DE,SBU2ZZ		;END OF SCORE BUFFER FOR PLAYER 2
		DEC DE			;POINT TO RIGHTMOST ELEMENT
		LD BC,(LEN)		;LENGHT OF NUMBER
		LD B,0			;MAKE A WORD
		LDDR			;NOW DE POINT TO WHERE BLANK GOES
		PUSH DE			;SO GRAB IT
		POP HL
		LD (HL),12		;PUT IN BLANK [12]
		WHILE8. (J),.GT.,1	;DON'T SHOW HAT FOR CURRENT LIFE
		  DEC HL		;NEXT SPOT LEFT
		  LD (HL),11		;HAT [11]
		  SUB8. (J),1		;DEC (J)
		ENDW.
	ENDIF.
;	;**** PUT BOTH PLAYERS' SCORES INTO ENDSCR'S SCORE1 AND SCORE2 ******

	IF8.	(PREPLA),.EQ.,1
	  LD	BC,6			
	  LD	HL,ASCBUF			;SCORE
	  LD	DE,SCORE1
	  LDIR
	ELSE.	
	  LD	BC,6			;FOR THE OTHER PLAYER TOO
	  LD	HL,ASCBUF
	  LD	DE,SCORE2
	  LDIR
	ENDIF.

	;******** NOW PUT THE HIGHER OF THE 2 SCORES INTO BUF1A **************

	LD	HL,SCORE1		;GET THE ADDRESS OF THE FIRST SCORE
	LD	DE,SCORE2		; ... AND OF THE SECOND
	LD	B,6			;INITIALIZE THE LOOP
HILOOP:	LD	A,(DE)			;GET SCORE2'S DIGIT
	CP	(HL)			;COMPARE IT TO SCORE1'S
	JR	Z,LOOPAG		;IF THE SAME, THE GO TO THE NEXT LOWER
	JR	C,SCO1HI		;IF GREATER, 
					;	THEN PUT SCORE1 INTO HISCOR
	JR	SCO2HI			;OTHERWISE SCORE2 IS HI
LOOPAG:	INC	HL			;GO ONTO NEXT DIGIT
	INC	DE
	DJNZ	HILOOP			;ONLY IF DIGITS LEFT
SCO1HI:	LD	BC,6			;MOVE SCORE1 INTO HISCOR
	LD	HL,SCORE1
	LD	DE,BUF1A
	LDIR
	JR	DO_HI
SCO2HI:	LD	BC,6			;MOV SCORE2 INTO THE BUFFER
	LD	HL,SCORE2
	LD	DE,BUF1A
	LDIR

;	;********* NOW PUT BUF1A INTO HIGHSCORE IF HIGHER THAN HIGHSCORE *****

DO_HI:	LD	HL,BUF1A		;NOW DO THE SAME AS BEFORE ONLY WITH
	LD	DE,HISCOR		;THE HISCORE
	LD	B,6
HI1LOOP:LD	A,(DE)
	CP	(HL)
	JR	Z,LOOP1G		;TRY THE NEXT DIGIT
	JR	C,SCONHI		;WE HAVE A NEW HIGH SCORE
	JR	GODIS			;THE OLD ONE IS STILL THE HIGHEST
LOOP1G:	INC	HL
	INC	DE
	DJNZ	HI1LOOP
SCONHI:	LD	BC,6			;MOVE THE NEW HI SCORE INTO THE PROPER
	LD	HL,BUF1A		;PLACE
	LD	DE,HISCOR
	LDIR
	;
	; *********** NOW PUT UP THE SCORE1 ON THE SCREEN *******************

GODIS:	
	ASIGN8. (SCOFLG),1		;FLAG TO PUT UP SCORE

	RET

INISCO::
;SCORE INITIALIZATION AT BEGINNING OF GAME
	FILL SCORE1,6,020H	;FILL WITH ASCII BLANKS
	FILL SCORE2,6,020H	;FILL WITH ASCII BLANKS
;	FILL HISCOR,6,020H	;FILL WITH ASCII BLANKS	;GOT RID OF THIS--LEO.
	FILL ASCBUF,6,020H	;FILL WITH ASCII BLANKS
	ASIGN8. (MARFLG),0	;RADIOACTIVE MARTINI FLAG
	ASIGN8. (WASFLG),0	;RADIOACTIVE WASTE FLAG
	ASIGN8. (PLUFLG),0	;RADIOACTIVE PLUNGERS FLAG
	ASIGN8. (INIT_SCR),1	;TO PUT UP FIRST SCREEN
	ASIGN8. (NXT_GUY),0	;NOT DEAD YET
	ASIGN8. (ENDGAM),0	;ENDGAME FLAG RESET
	ASIGN8. (DDPLAY),0	;DEAD PLAYER FLAG RESET
	ASIGN8. (FIRST),0		;HAVE WE DONE FIRST SCREEN YET ? NO
	ASIGN8. (PLANUM),(NUMPLA)	;LEO CHANGED THIS
	ASIGN8. (PREPLA),1	;PLAYER 1 GOES FIRST
	FILL SCORE,12,0		;SCORE=XMAN=SCOR1=XMAN1=SCOR2=XMAN2=JHISCO=0
	FILL ZONE1,4,1		;ZONE1=ZONE2=LEV1=LEV2=1	 
;LET LEO DO IT	FILL PLAY1,2,3		;PLAY1=PLAY2=3 (LIVES)
	ASIGN8. (PLAY1),3
	FILL SCOBUF,33,12	;SCOBUF=SBUF1=SBUF2=[BLANK]
	LD A,0			;ZEROS
	LD (SBUF1),A		;PLAYER 1, PUT IN ZERO
	LD (SBUF2+10),A		;PLAYER 2, ZERO
	LD A,11			;HATS
	LD (SBUF1+2),A		;PLAYER1 HATS
	LD (SBUF1+3),A		;
	LD (SBUF2+7),A		;PLAYER 2, HATS
	LD (SBUF2+8),A		;
	FILÌ SCORE1,6,020È	;SCORE1=SCORE2=HISCOR=[ASCIÉ BLANK]
	FILL SCORE2,6,20H	;I PUT THIS IS TO FILL EACH ONE INDIVIDUALLY.
;	FILL HISCOR,6,020H
	RET

COPSCO::
;COPY PLAYER SCORE AND XMAN FLAG TO WORKING SCORE
	IF8. (PREPLA),.EQ.,1		;IF PRESENT PLAYER = 1
		LD BC,0			;OFFSET IS 0
	ELSE.				;
		LD BC,04		;ELSE NEXT [SCOR2]
	ENDIF.				;
	LD HL,SCOR1			;ADD OFFSET
	ADD HL,BC			;  TO 
	LD (JJ),HL			;PUT IN (JJ)
	COPBUF (JJ),SCORE,4		;COPY TO SCORE AND XMAN
	
	RET


SCOCOP::
;COPY WORKING SCORE AND XMAN FLAG TO PLAYER SCORE
	IF8. (PREPLA),.EQ.,1		;IF PRESENT PLAYER = 1
		LD BC,0			;OFFSET IS 0
	ELSE.				;
		LD BC,04		;ELSE NEXT [SCOR2]
	ENDIF.				;
	LD HL,SCOR1			;ADD OFFSET
	ADD HL,BC			;  TO 
	LD (JJ),HL			;PUT IN (JJ)
	COPBUF SCORE,(JJ),4		;COPY FROM SCORE AND XMAN
	
	RET

SCOTSK::
;CHECK SCORE FLAG [SCOFLG], 
;IF NOT ZERO, CALL SCORUN TO PUT UP NEW SCORE

	IF8. (SCOFLG),.EQ.,0		;NO SCORE TO PUT UP
		JP XSCTSK		;EXIT
	ELSE.
		CALL SCORUN		;SO PUT SCORE UP
		ASIGN8. (LASTIM),(TIMER)	;LAST TIME SCORE UPDATED
		ASIGN8. (SCOFLG),0	;RESET FLAG
	ENDIF.
XSCTSK:	
	ASIGN8. (K),(LASTIM)		;GRAB LASTIM
	SUB8. (K),(TIMER)		;GET DIFFERENCE WITH TIME NOW [TIMER]
	IF8. (K),.GT.,3			;WAIT 3 TICKS
	  IF8. (TICKY),.EQ.,1		;WE ARE NOW IN FLASH MODE
	    ASIGN8. (BLKSCO),1		;FLAG FOR BLANK SCORE
	  ENDIF.
	  CALL SCORUN			;PUT SCORE OR BLANKS
	  ASIGN8. (BLKSCO),0		;RESET FLAG
	ENDIF.

	RET

TIMBON::
	ASIGN8. (SBONUS),1		;BONUS POINTS SOUND
	SETSK BONTSK,BONTCB,3,1		;ATTACH BONUS TASK
	WHILE8. (TIMER),.GT.,0	;LOOP HERE UNTIL RUN OUT OF TIME
		AND A
	ENDW.				;CHANGES FLAG [TIMER=0]
	N.CLKRV BONTCB			;REMOVE TASK
	ASIGN8. (SBONUS),0		;TURN SOUND OFF
	ASIGN8. (NXT_GUY),0		;RESET FLAG
	ASIGN8. (RADIOACTIVE),0		;RESET FLAG
	
	RET

BONTSK::
	JP SBNTSK
	BONTCB::	DS	6	;BONUS TASK TCB	
SBNTSK:
	CALL TIMTSK	;DECREMENT TIME BY 100
	CALL DISTIM	;DISPLAY NEW TIME
	IF8. (TICKY),.EQ.,1	;TIME ONLY CHANGES ON TICKY=1
	  PCALL ADSCOR,0100H	;ADD TO THE SCORE
	ENDIF.
	CALL SCORUN		;DISPLAY NEW SCORE

	RET

	END

