		 .Z80
 		.RADIX 10
 		CSEG
 		.XLIST
 		INCLUDE STRUCT.MRO
 		INCLUDE COMMON.MRO
		INCLUDE ARITH.MRO
		INCLUDE LINKTAB.EXA
 		.LIST
		
TOP:	
XT:	DB	0
YT:	DB	0
LT:	DB	0
	DB	0FH,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8,8

BOTTOM:
XB:	DB	0
YB:	DB	0
LB:	DB	0
	DB	0EH,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9,9

BLANKT:
XBLT:	DB	0
YBLT:	DB	0
LBL1:	DB	0
	DB	20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H
	DB	20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H

BLANKB:
XBLB:	DB	0
YBLB:	DB	0
LBL2:	DB	0
	DB	20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H
	DB	20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H,20H

RXCOOR:	DB	0
RYCOOR:	DB	0

REVERSE::
	PUSH	AF		;PUSH FLAG FROM A REG
	PUSH	HL		;SAVE STRING LENGTH VARIABLE
	LD	A,L		;L REG HAS PASSED STRING LENGTH
	INC	A		
	LD	(LT),A		;SET THE STRING LENGTH FOR TOP,BOTTOM AND WORD PATTERNS
	LD	(LB),A
	INC	A
	LD	(LBL1),A
	LD	(LBL2),A
	LD	A,C		;C REG HAS X COOR 
	LD	(XT),A		;SET THE X COOR FOR TOP,BOTTOM AND WORD PATTERNS
	LD	(XB),A
	LD	(XBLT),A	;SET X COOR FOR TOP AND BOTTOM BLANKS
	LD	(XBLB),A	
	INC	A
	LD	(RXCOOR),A	;SET THE BEGINNING X COOR FOR REVERSING PATTERNS
	LD	A,E		;E REG HAS Y COOR
	LD	(YT),A		;SET TOP PATTERNS Y COOR
	LD	(YBLT),A	;SET TOP BLANK
	INC	A		
	LD	(RYCOOR),A	;SET THE YCOOR FOR REVERSING PATTERNS
	INC	A
	LD	(YB),A		;SET BOTTOM PATTERNS Y COOR
	LD	(YBLB),A	;SET BOTTOM BLANK
	PCALL	PUTPAT,(XT),(RYCOOR),0BH	;FIRST PATTERN
	POP	HL		;POP SAVED STRING LENGTH VAR
	LD	B,L		;SET COUNTER
R10:	PUSH	BC
	PCALL	GETPAT,(RXCOOR),(RYCOOR)
	ADD	A,80H 		;ADD 80H TO PATTERN FOR REVERSED ONES
	LD	L,A		;SET UP FOR PUTPAT
	PCALL	PUTPAT,(RXCOOR),(RYCOOR)
	LD	HL,RXCOOR
	INC	(HL)		;INCREMENT X COOR
	POP	BC
	DJNZ	R10		;CONTINUE TILL ALL PATTERNS REVERSED
	PCALL	PUTPAT,(RXCOOR),(RYCOOR),0AH	;LAST PATTERN
	POP	AF		;POP A REG FLAG
	AND	A		;CHECK FOR BLANK OR OUTLINES
	JR	NZ,WANTB
	PCALL	PUTMSG,TOP	;PUT TOP  AND BOTTOM REVERSED FRINGE ON
	PCALL	PUTMSG,BOTTOM
	PCALL	PUTPAT,(RXCOOR),(YT),0CH 	;PUT ON THE LAST QUARTER PATTER
	PCALL	PUTPAT,(RXCOOR),(YB),0DH
	RET
WANTB:	PCALL	PUTMSG,BLANKT	;PUT BLANKS OVER THE REVERSED FRINGES TO
	PCALL	PUTMSG,BLANKB	;CHANGE BACK TO REGULAR LETTERS
	PCALL	PUTPAT,(XBLT),(RYCOOR),20H	;BLANKS
	PCALL	PUTPAT,(RXCOOR),(RYCOOR),20H
	RET

	END
