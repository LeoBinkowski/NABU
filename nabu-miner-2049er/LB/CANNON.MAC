	.Z80
	.RADIX	10
	INCLUDE	COMMON.MRO
	INCLUDE	STRUCT.MRO
	INCLUDE MINER.EQU
	CANLLIM	EQU	70H
	CANRLIM	EQU	0B6H
	EXTRN	CANNON
CAN1PAT	EQU	58H
CAN2PAT	EQU	5CH
CANX::	DB	00H
CANY::	DB	00H
CANSP::	DS	8
	REPT	8
	DB	0C0H
	ENDM
	;
EXTRN	BOBX,SPLAT
;
;		ROUTINE NAME:	MOVECAN
;		FILE NAME:	CANNON.MAC
;		AUTHOR:		Leo Binkowski
;		DATE:		APRIL 26, 1984
;
;	DESCRIPTION:
;
;	THIS ROUTINE WILL MOVE THE CANNON FROM SIDE TO SIDE (WITH BOB)
;
;
;
;	PARAMETERS PASSED:
;				BC-DIRECTION OF CANNON
;					1=LEFT
;					2=RIGHT
;				
;	PARAMETERS RETURNED:
;
;			NONE
;
;	GLOBALS ACESSED:
;				CANX,CANY,CANSP
;
;	COMMENTS AND WARNINGS:
;
;
MOVECAN::
	LD	A,C
	CP	1
	JR	Z,DOLEFT
	CP	2
	JR	Z,DORIGHT
	CP	3
	JR	Z,DORNOB
	JR	CANCONT
DOLEFT:
	IF8. (CANX),.GT.,CANLLIM
	  DEC8.	(CANX)
	  DEC8.	(BOBX)
	ENDIF.
	JR	CANCONT
DORIGHT:
	IF8.	(CANX),.LT.,CANRLIM
	  INC8.	(CANX)
	  INC8.	(BOBX)
	ENDIF.
	JR	CANCONT
DORNOB:
	INC8.	(CANX)		;IF BOB ISN'T IN CANNON BUT NEED TO MOVE RIGHT.
CANCONT:
	ASIGN8.	(SPLAT),8
	LD	HL,CANSP	;GET THE ADDRESS OF THE CANNON ATTRIBUTES
	LD	A,(CANY)	;GET THE Y
	LD	(HL),A		;PUT IT INTO THE TABLE
	INC	HL
	LD	A,(CANX)	;GET THE X
	LD	(HL),A		;PUT IT INTO THE TABLE
	INC	HL
	LD	A,CAN1PAT	;GET THE FIRST CANNON PATTERN
	LD	(HL),A
	INC	HL
	LD	A,GREY		;PUT THE COLOR
	LD	(HL),A
	INC	HL
	LD	A,(CANY)
	ADD	A,16
	LD	(HL),A		;PUT THE NEXT SPRITE 16 PIXELS LOWER
	INC	HL
	LD	A,(CANX)	;PUT THE X
	LD	(HL),A
	INC	HL
	LD	A,CAN2PAT	;PUT THE BOTTOM PATTERN IN
	LD	(HL),A
	INC	HL
	LD	A,GREY		;PUT THE GREY IN
	LD	(HL),A
	RET
RSTCAN::
	; ROUTINE USED BY MAINLINE TO RESET THE CANNON'S POSITION AFTER
	; FIRING.
	;
	LD	A,(CANY)	;IF CANNON NOT BEING USED, THEN DON'T
	LD	BC,(CANX)	;RESET IT.
	OR	C
	RET	Z
	IF8.	(CANNON),.NE.,TRUE
	  IF8.	(CANX),.LT.,CANRLIM
	    PCALL MOVECAN,3		;MOVE CANNON RIGHT WITHOUT BOB
	  ENDIF.
	ENDIF.
	RET
	END

